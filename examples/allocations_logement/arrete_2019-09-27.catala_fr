# Arrêté du 27 septembre 2019 relatif au calcul des aides personnelles au logement et de la prime de déménagement

## Chapitre II : Dispositions applicables aux ressources

### Article 3 | LEGIARTI000039160731

L'abattement forfaitaire prévu par l'article R. 822-7 du même code est fixé à 95 euros.

```catala
champ d'application RessourcesAidePersonnelleLogement:
  définition montant_forfaitaire_r_822_7 égal à 95€

```

### Article 4 | LEGIARTI000039160733

L'abattement forfaitaire prévu par l'article R. 822-8 du même code est fixé à 2 589 euros.

```catala
champ d'application RessourcesAidePersonnelleLogement:
  définition montant_forfaitaire_r_822_8 égal à 2 589€
```

### Article 6 | LEGIARTI000045011471

Pour l'application de l'article D. 822-21 du même code, le montant forfaitaire auquel sont réputées égales les ressources du bénéficiaire et, le cas échéant, de son conjoint, est fixé à 7 800 euros pour la location et à 6 000 euros pour la résidence en logement-foyer.

Toutefois, lorsque le demandeur est titulaire d'une bourse de l'enseignement supérieur qui n'est pas assujettie à l'impôt sur le revenu, le montant forfaitaire de ressources est fixé à 6 300 euros pour la location et à 4 900 euros pour la résidence en logement-foyer.

```catala
# Pas pertinent pour le calcul des APL, renvoie à D822-21 qui n'a pas été traité.
```

## Chapitre III : Calcul des aides personnelles au logement en secteur locatif

### Article 7 | LEGIARTI000044137432

Les plafonds de loyers visés au 2° de l'article D. 823-16 du même code sont fixés comme suit (en euros) :

Zone | Personne seule	| Couple sans personne à charge	| Personne seule ou couple    | Par personne à
                                                      | ayant une personne à charge | charge supplémentaire
-----------------------------------------------------------------------------------------------------------
I	   | 298,07	        | 359,49                        | 406,30	                    | 58,95
II   | 259,78         | 317,97                        | 357,80                      | 52,08
III	 | 243,48         | 295,15                        | 330,94                      | 47,43

Nota: Conformément au I de l’article 3 de l’arrêté du 23 septembre 2021, ces dispositions sont applicables pour les prestations dues à compter du 1er octobre 2021.

```catala-metadata
déclaration énumération SituationFamilialeCalculAPL:
  -- PersonneSeule
  -- Couple
```

```catala
champ d'application CalculAidePersonnelleLogement
  sous condition date_courante >=@ |2021-10-01|:

  définition situation_familiale_calcul_apl égal à
    selon situation_familiale sous forme
    -- Célibataire: PersonneSeule
    -- Mariés: Couple
    -- Pacsés: Couple
    -- Concubins: Couple
    -- CélibataireSéparéDeFait: PersonneSeule
    -- ConcubinageDontSéparéDeFait: Couple

  # Colonne 1
  étiquette base définition plafond_loyer_d823_16_2 sous condition
    (situation_familiale_calcul_apl sous forme PersonneSeule) et
    nombre_personnes_à_charge = 0
  conséquence égal à
    selon zone sous forme
    -- Zone1: 298,07€
    -- Zone2: 259,78€
    -- Zone3: 243,48€

  # Colonne 2
  étiquette base définition plafond_loyer_d823_16_2 sous condition
    (situation_familiale_calcul_apl sous forme Couple) et
    nombre_personnes_à_charge = 0
  conséquence égal à
    selon zone sous forme
    --Zone1: 359,49€
    --Zone2: 317,97€
    --Zone3: 295,15€

  # Colonnes 3 et 4
  étiquette base définition plafond_loyer_d823_16_2 sous condition
    nombre_personnes_à_charge >= 1
  conséquence égal à
    selon zone sous forme
    --Zone1:
      406,30€ +€ 58,95€ *€ (entier_vers_décimal de (nombre_personnes_à_charge - 1))
    --Zone2:
      357,80€ +€ 52,08€ *€ (entier_vers_décimal de (nombre_personnes_à_charge - 1))
    --Zone3:
      330,94€ +€ 47,43€ *€ (entier_vers_décimal de (nombre_personnes_à_charge - 1))

```

### Article 8 | LEGIARTI000044137429

Dans le cas où le logement occupé est une chambre, les plafonds
de loyers visés au 2° de l'article D. 823-16 du même code sont
fixés comme suit, quelle que soit la taille de la famille (en euros) :

- 90 % des loyers plafonds de location pour une personne isolée ;
- 75 % des loyers plafonds de location pour une personne isolée,
dans le cas des personnes âgées ou handicapées adultes hébergées
à titre onéreux chez des particuliers.

On obtient les loyers plafonds suivants (en euros) :

MONTANTS DES LOYERS PLAFONDS CHAMBRE EN APL ET EN AL
(arrondis au centime d'euro le plus proche)

Bénéficiaires	               |   Zones	 |   Montants

Cas général	                 |   I	     |   268,26
                             |   II	     |   233,80
                             |   III     |   219,13

Cas des personnes âgées      |   I       |   223,55
ou handicapées adultes       |   II      |   194,84
hébergées à titre onéreux    |   III     |   182,61
chez des particuliers

NOTA :

Conformément au I de l’article 3 de l’arrêté du 23 septembre 2021,
ces dispositions sont applicables pour les prestations dues à
compter du 1er octobre 2021.

```catala
champ d'application CalculAidePersonnelleLogement
  sous condition
    date_courante >=@ |2021-10-01| et
    logement_est_chambre:

  étiquette chambre exception base
  définition plafond_loyer_d823_16_2 égal à
    selon zone sous forme
    -- Zone1: 268,26€
    -- Zone2: 233,80€
    -- Zone3: 219,13€

  exception chambre définition plafond_loyer_d823_16_2 sous condition
    personnes_âgées_ou_handicapées_adultes_hébergées_à_titre_onéreux_chez_des_particuliers
  conséquence égal à
    selon zone sous forme
    -- Zone1: 223,55€
    -- Zone2: 194,84€
    -- Zone3: 182,61€

```

### Article 9 | LEGIARTI000044137426

Les montants forfaitaires au titre des charges visés au 3° de
l'article D. 823-16 du même code sont fixés comme suit (en euros) :


Désignation	             |   Toutes zones

Bénéficiaire isolé ou    |      54,22
couple sans personne
à charge

Par personne             |      12,29
supplémentaire
à charge

NOTA :

Conformément au I de l’article 3 de l’arrêté du 23 septembre 2021,
ces dispositions sont applicables pour les prestations dues à compter
du 1er octobre 2021.

```catala
champ d'application CalculAidePersonnelleLogement
  sous condition date_courante >=@ |2021-10-01|:
  définition montant_forfaitaire_charges égal à
    54,22€ +€ 12,29€ *€ (entier_vers_décimal de nombre_personnes_à_charge)

```

### Article 10 | LEGIARTI000039160745

Les coefficients appliqués au plafond de loyer prévu par l'article
D. 823-16 du même code, permettant de déterminer les premier et second
plafonds de loyer mentionnés au même article, sont établis selon le tableau
comme suit :


ZONE          | PREMIER PLAFOND DE LOYER |   SECOND PLAFOND DE LOYER

I	            |           3,4	           |               4
II            |           2,5	           |               3,1
III	          |           2,5	           |               3,1

```catala
champ d'application CalculAidePersonnelleLogement:
  définition plafond_dégressivité_d823_16 égal à
    selon zone sous forme
    -- Zone1: plafond_loyer_d823_16_2 *€ 3,4
    -- Zone2: plafond_loyer_d823_16_2 *€ 2,5
    -- Zone3: plafond_loyer_d823_16_2 *€ 2,5

  définition plafond_suppression_d823_16 égal à
    selon zone sous forme
    -- Zone1: plafond_loyer_d823_16_2 *€ 4,0
    -- Zone2: plafond_loyer_d823_16_2 *€ 3,1
    -- Zone3: plafond_loyer_d823_16_2 *€ 3,1
```


### Article 11 | LEGIARTI000039160747

Le montant de minoration forfaitaire prévu au 9e alinéa de l'article D. 823-16
du même code est fixé à 5 euros.

```catala
champ d'application CalculAidePersonnelleLogement:
  définition montant_forfaitaire_d823_16 égal à 5 €
```

### Article 12 | LEGIARTI000039160749

Le seuil de versement prévu au dernier alinéa de l'article D. 823-16 du même code,
avant application de la contribution pour le remboursement de la dette sociale,
est fixé à 10 euros pour les allocations de logement et à 0 euro pour l'aide
personnalisée au logement.

```catala
champ d'application CalculAidePersonnelleLogement:
  définition montant_minimal_aide égal à
    selon type_aide sous forme
    -- AidePersonnaliséeLogement: 0 €
    -- AllocationLogementFamiliale: 10 €
    -- AllocationLogementSociale: 10 €
```

### Article 13 | LEGIARTI000044137423

La participation minimale P0 définie au 2° de l'article D. 823-17 du même code est
égale à la plus élevée des deux valeurs suivantes : 8,5 % de la somme du loyer éligible
défini au 2° de l'article D. 823-16 du même code et du forfait charge ou 35,39 euros.

```catala
champ d'application CalculAidePersonnelleLogement
  sous condition date_courante >=@ |2021-10-01|:
  définition participation_minimale égal à
    si loyer_éligible *€ 8,5% >=€ 35,39 € alors
      loyer_éligible *€ 8,5%
    sinon
      35,39 €
```

NOTA :

Conformément au I de l’article 3 de l’arrêté du 23 septembre 2021, ces dispositions
sont applicables pour les prestations dues à compter du 1er octobre 2021.

### Article 14 | LEGIARTI000044137420

Pour l'application de l'article D. 823-17 du même code, le taux de participation
personnelle Tp du ménage, exprimé en pourcentage, est calculé selon la formule suivante :

Tp = TF + TL

dans laquelle :

1° TF représente un taux fonction de la taille de la famille donné par le tableau suivant :

VALEURS DE TF
Bénéficiaires                                         |     TF
Isolé	                                                |   2,83%
Couple sans personne à charge	                        |   3,15%
Personne seule ou couple ayant une personne à charge	|   2,70%
2 enfants ou 2 personnes                              | 	2,38%
3 enfants ou 3 personnes                              | 	2,01%
4 enfants ou 4 personnes                              | 	1,85%
5 enfants ou 5 personnes                              | 	1,79%
6 enfants ou 6 personnes                              | 	1,73%
Majoration par personne à charge	                    |   -0,06%

```catala
# TODO juridique: rechercher si la distinction enfants/personnes est juste
# un artefact de rédaction ou s'il y a vraiment une différence de calcul dans
# le cas où e.g. il y a 3 enfants + 2 adultes à charges : doit on prendre 1,79%
# ou 2,01%.
# TODO juridique: que se passe-t-il quand il y a 29 personnes à charge et que
# le taux devient négatif?
champ d'application CalculAidePersonnelleLogement
  sous condition date_courante >=@ |2021-10-01|:
  définition taux_composition_familiale égal à
    si nombre_personnes_à_charge = 0 alors
      selon situation_familiale_calcul_apl sous forme
      -- PersonneSeule: 2,83%
      -- Couple: 3,15%
    sinon (si nombre_personnes_à_charge = 1 alors
      2,70%
    sinon (si nombre_personnes_à_charge = 2 alors
      2,38%
    sinon (si nombre_personnes_à_charge = 3 alors
      2,01%
    sinon (si nombre_personnes_à_charge = 4 alors
      1,85%
    sinon (si nombre_personnes_à_charge = 5 alors
      1,79%
    sinon (si nombre_personnes_à_charge = 6 alors
      1,73%
    sinon
      (1,73% -. (0,06% *. (entier_vers_décimal de (nombre_personnes_à_charge - 6))))
    ))))))
# TODO informatique: corriger le parseur pour éviter d'avoir à mettre
# toutes ces parenthèses.
```

2° TL représente un taux complémentaire fixé ci-dessous en fonction de la valeur du rapport
RL entre le loyer retenu dans la limite du plafond L et un loyer de référence LR : RL = L / LR .

RL est exprimé en pourcentage et arrondi à la deuxième décimale.

```catala
champ d'application CalculAidePersonnelleLogement
  sous condition date_courante >=@ |2021-10-01|:
  définition rapport_loyers égal à
  # TODO informatique: faire l'arrondi
    loyer_éligible /€ loyer_référence
```

Pour la détermination de TL , les taux progressifs et les tranches successives de RL mentionnés
au 3° de l'article D. 823-17 du même code sont fixés comme suit :
- 0 % pour la tranche de RL inférieure à 45 % ;
- 0,45 % pour la tranche de RL entre 45 % et 75 % ;
- 0,68 % pour la tranche de RL supérieure à 75 %.

TL est exprimé en pourcentage et arrondi à la troisième décimale.
Le tableau suivant traduit cette formule :

Si RL<45 %    | 	Si 45% < RL < 75%	  |   Si RL >75 %
TL=0 %        |	TL=0,45 %*(RL-45%)    |	TL=0,45 %*30 %+0,68 %*(RL-75%)

```catala
champ d'application CalculAidePersonnelleLogement
  sous condition date_courante >=@ |2021-10-01|:
  # Ici on choisit de mettre des >= pour inclure le résultat sur la crête
  # dans la case de droite ; nous avons bien vérifié que sur la crête le
  # résultat est le même à gauche et à droite.
  définition taux_loyer_éligible égal à
    si rapport_loyers <. 45% alors 0% sinon (
    si rapport_loyers >=. 45% et rapport_loyers <. 75% alors
      0,45% *. (rapport_loyers -. 0,45%)
    sinon (si rapport_loyers >=. 75% alors
      0,45% *. 30% +. 0,68% *. (rapport_loyers -. 75%)
    sinon 0,0))
```

Le loyer de référence LR est défini selon le tableau suivant (en euros) :

Composition du ménage	                                  |     Montant
Bénéficiaire isolé	                                    |     259,78
Couple sans personne à charge	                          |     317,97
Personne seule ou couple ayant une personne à charge	  |     357,80
Majoration par personne à charge	                      |     52,08

```catala
champ d'application CalculAidePersonnelleLogement
  sous condition date_courante >=@ |2021-10-01|:
  définition loyer_référence égal à
    si nombre_personnes_à_charge = 0 alors
      selon situation_familiale_calcul_apl sous forme
      -- PersonneSeule: 259,79€
      -- Couple: 317,97€
    sinon (357,80€ +€
      (52,08€ *€ (entier_vers_décimal de (nombre_personnes_à_charge - 1))))
```

NOTA :
Conformément au I de l’article 3 de l’arrêté du 23 septembre 2021, ces dispositions
sont applicables pour les prestations dues à compter du 1er octobre 2021.

### Article 15 | LEGIARTI000045011468

Pour l'application du 5° de l'article D. 823-17 du même code, le forfait " R0 " est fixé selon
le tableau suivant (en euros) :

Composition du foyer	                      |     MONTANT  (en euros)
Personne seule sans personne à charge	      |     4 683
Couple sans personne à charge	              |     6 709
Personne seule ou couple ayant :
    -une personne à charge	                |     8 002
    -deux personnes à charge	              |     8 182
    -trois personnes à charge	              |     8 495
    -quatre personnes à charge	            |     8 811
    -cinq personnes à charge	              |     9 124
    -six personnes à charge                 |     9 439
    -par personne à charge supplémentaire	  |      311

```catala
champ d'application CalculAidePersonnelleLogement
  sous condition date_courante >=@ |2021-10-01|:
  définition abattement_forfaitaire_d823_17 égal à
    si nombre_personnes_à_charge = 0 alors
      selon situation_familiale_calcul_apl sous forme
      -- PersonneSeule: 4 683 €
      -- Couple: 6709 €
    sinon (si nombre_personnes_à_charge = 1 alors
      8 002 €
    sinon (si nombre_personnes_à_charge = 2 alors
      8 192 €
    sinon (si nombre_personnes_à_charge = 3 alors
      8 495 €
    sinon (si nombre_personnes_à_charge = 4 alors
      8 811 €
    sinon (si nombre_personnes_à_charge = 5 alors
      9 124 €
    sinon (si nombre_personnes_à_charge = 6 alors
      9 439 €
    sinon
      ( 9 439€ +€ (311 € *€ (entier_vers_décimal de (nombre_personnes_à_charge - 6))))
    ))))))
```

NOTA :
Conformément à l’article 2 de l’arrêté du 20 décembre 2021 (NOR : LOGL2134477A),
ces dispositions sont applicables pour les prestations dues à compter du 1er janvier 2022.

### Article 16 | LEGIARTI000044137417

Dans le cas des colocataires prévus à l'article D. 823-18 du même code :

1° Les plafonds de loyers sont fixés à 75 % des plafonds de loyers définis au 2° de l'article
D. 823-16 du même code et fixés à l'article 7.

Les montants obtenus par l'application de ces pourcentages sont arrondis au centime d'euro le plus proche.
On obtient les loyers plafonds suivants (en euros) :

Zone	                                |     I     |     II    |    III

Personne seule	                      |   223,55  |   194,84	|   182,61

Couple sans personne à charge	        |   269,62  |   238,48	|   221,36

Personne seule ou couple ayant une
personne à charge	                    |   304,73	|   268,35	|   248,21

Par personne à
charge supplémentaire                 |   44,21   |   39,06   |   35,57

```catala
champ d'application CalculAidePersonnelleLogement
  sous condition date_courante >=@ |2021-10-01| et colocation:

  # Ici l'exception est rapportée au cas de base puisqu'on suppose
  # qu'on ne peut pas être en colocation dans un logement constitué
  # d'une seule chambre.

  exception base définition plafond_loyer_d823_16_2 sous condition
    (situation_familiale_calcul_apl sous forme PersonneSeule) et
    nombre_personnes_à_charge = 0
  conséquence égal à
    selon zone sous forme
    -- Zone1: 223,55€
    -- Zone2: 194,84€
    -- Zone3: 182,61€

  exception base définition plafond_loyer_d823_16_2 sous condition
    (situation_familiale_calcul_apl sous forme Couple) et
    nombre_personnes_à_charge = 0
  conséquence égal à
    selon zone sous forme
    --Zone1: 269,62€
    --Zone2: 238,48€
    --Zone3: 221,36€

  exception base définition plafond_loyer_d823_16_2 sous condition
    nombre_personnes_à_charge >= 1
  conséquence égal à
    selon zone sous forme
    --Zone1:
      304,73€ +€ 44,21€ *€ (entier_vers_décimal de (nombre_personnes_à_charge - 1))
    --Zone2:
      268,35€ +€ 39,06€ *€ (entier_vers_décimal de (nombre_personnes_à_charge - 1))
    --Zone3:
      248,21€ +€ 35,57€ *€ (entier_vers_décimal de (nombre_personnes_à_charge - 1))
```

2° Le montant forfaitaire au titre des charges est fixé comme suit (en euros) :

Composition du foyer	           |       Montant
Bénéficiaire isolé               |       27,10
Couple sans personne à charge	   |       54,22
Majoration par personne à charge |       12,29

```catala
champ d'application CalculAidePersonnelleLogement
  sous condition date_courante >=@ |2021-10-01| et colocation:
  définition montant_forfaitaire_charges égal à
    (selon situation_familiale_calcul_apl sous forme
    -- PersonneSeule: 27,10€
    -- Couple: 54,22€) +€
    12,29€ *€ (entier_vers_décimal de nombre_personnes_à_charge)
```

NOTA :
Conformément au I de l’article 3 de l’arrêté du 23 septembre 2021, ces dispositions
sont applicables pour les prestations dues à compter du 1er octobre 2021.

### Article 17 | LEGIARTI000039160759

Pour l'application du 3° de l'article D. 832-10 du même code, les mensualités plafonds pour
les logements occupés par leur propriétaire, et financés par les prêts mentionnés au 1° de
l'article R. 832-5 du même code sont fixées, comme suit compte tenu de la date de signature
du contrat de prêt figurant sur le certificat daté prévu au 1° du I de l'article D. 832-12 du même code :

1° Lorsque la date de signature du contrat de prêt est intervenue après le 30 juin 1992

a) Logements neufs construits ou acquis par l'accédant à la propriété (en francs) :

DÉSIGNATION	                                                |   ZONE I	  |   ZONE II	  |   ZONE III
Bénéficiaire isolé	                                        |   2 085	    |   1 860     |   1 736
Couple sans personne à charge	                              |   2 515     |   2 239     |   2 082
Bénéficiaire isolé ou couple ayant une personne à charge	  |   2 945	    |   2 618     |   2 428
Par personne supplémentaire à charge	                      |    430	    |    379      |    346

b) Logements agrandis ou aménagés à partir de locaux non destinés à l'habitation ou acquis et améliorés (en francs) :

DÉSIGNATION	                                                |   ZONE I	  |   ZONE II	  |   ZONE III
Bénéficiaire isolé	                                        |   1 678	    |   1 469     |   1 397
Couple sans personne à charge	                              |   2 025     |   1 801     |   1 676
Bénéficiaire isolé ou couple ayant une personne à charge	  |   2 372	    |   2 106     |   1 955
Par personne supplémentaire à charge	                      |    374	    |    305      |    279

2° Lorsque la date de signature du contrat de prêt est intervenue après le 27 novembre 1994 :

a) Logements neufs construits ou acquis par l'accédant à la propriété (en francs) :

DÉSIGNATION	                                                |   ZONE I	  |   ZONE II	  |   ZONE III
Bénéficiaire isolé	                                        |   1 840	    |   1 642     |   1 532
Couple sans personne à charge	                              |   2 220     |   1 977     |   1 837
Bénéficiaire isolé ou couple ayant une personne à charge	  |   2 600	    |   2 312     |   2 142
Par personne supplémentaire à charge	                      |    380	    |    335      |    305

b) Logements agrandis ou aménagés à partir de locaux non destinés à l'habitation ou logements existants
acquis et améliorés (en francs) :

DÉSIGNATION	                                                |   ZONE I	  |   ZONE II	  |   ZONE III
Bénéficiaire isolé	                                        |   1 481	    |   1 320     |   1 233
Couple sans personne à charge	                              |   1 787     |   1 589     |   1 479
Bénéficiaire isolé ou couple ayant une personne à charge	  |   2 093	    |   1 858     |   1 725
Par personne supplémentaire à charge	                      |    306	    |    269      |    246

## Chapitre VIII : Prime de déménagement

### Article 45 | LEGIARTI000039160713

Le plafond de dépenses ouvrant droit à la prime de déménagement défini
à l'article D. 823-22 du même code s'exprime en pourcentage de la base
mensuelle de calcul des allocations familiales. Il est égal à 240 % pour
les bénéficiaires isolés ou couples ayant trois enfants à charge et est
majoré de 20 % par enfant supplémentaire à charge.

```catala
champ d'application ÉligibilitéPrimeDeDéménagement:
  définition plafond_d823_22 égal à
   base_mensuelle_allocations_familiales.montant *€ 240% +€
   (si
      (nombre pour personne_à_charge dans ménage.personnes_à_charge de
        personne_à_charge sous forme EnfantÀCharge) > 3
    alors
      base_mensuelle_allocations_familiales.montant *€ (entier_vers_décimal de (
      (nombre pour personne_à_charge dans ménage.personnes_à_charge de
      personne_à_charge sous forme EnfantÀCharge) - 3) *. 20%)
    sinon 0€)
```
