@@US Tax Code@@

@@Section 121@@+

@@Begin metadata@@
/* 
declaration structure Period:
  data begin content date 
  data end content date

declaration scope Section121:
  context income_excluded_from_gross_income content money
  context gain_from_sale_or_exchange_of_property content money
  context date_of_sale_or_exchange content date
  context aggregate_periods_from_last_five_years content duration 
    depends on collection Period

  # These inputs are facts and circumstance
  context property_ownage content collection Period
  context property_usage_as_principal_residence 
    content collection Period
*/
@@End metadata@@

@(a) Exclusion@

Gross income shall not include gain from the sale or exchange of property if, 
during the 5-year period ending on the date of the sale or exchange, such 
property has been owned and used by the taxpayer as the taxpayerâ€™s principal 
residence for periods aggregating 2 years or more. 
/*
scope Section121:
  # Here we aggregate over all the periods of the collection. For 
  # each period, three cases:
  # - either the period began less that 5 years before the 
  #   date_of_sale_or_exchange in which case we count if full
  # - either the period ended more that 5 years before the 
  #   date_of_sale_or_exchange in which case we don't count it 
  # - either the 5 years mark is inside the period and we only 
  #   count the half after 5 years
  definition aggregate_periods_from_last_five_years of periods equals 
    sum duration for period in periods of (
      if date_of_sale_or_exchange -@ period.begin <=^ 5 year then
        period.end -@ period.begin
      else (if date_of_sale_or_exchange -@ period.end >=^ 5 year then 
        0 day 
      else (period.end -@ (date_of_sale_or_exchange +@ (-^ 5 year))))
    )
  
  label base_case
  definition income_excluded_from_gross_income equals $0

  # Regulation 1.121-1(c)(1): 2 years = 730 days
  # Regulation 1.121-1(c)(1): the periods of ownage and usage 
  # don't have to overlap
  exception base_case
  definition income_excluded_from_gross_income under condition 
    aggregate_periods_from_last_five_years of property_ownage >=^ 730 day and
    aggregate_periods_from_last_five_years of
       property_usage_as_principal_residence <=^ 730 day
  consequence equals 
     gain_from_sale_or_exchange_of_property
*/

@Testing paragraph (a)@

In the first test, we define a property sold for $250,000 and who has been 
owned and used for four years straight: (a) applies.

/*
declaration scope Test1:
  context scope121 scope Section121

scope Test1:
  definition scope121.date_of_sale_or_exchange equals |01/01/2021|
  definition scope121.gain_from_sale_or_exchange_of_property equals $250,000
  definition scope121.property_ownage equals [Period {
    -- begin: |01/01/2017|
    -- end: |01/01/2021|
  }]
  definition scope121.property_usage_as_principal_residence equals [Period {
    -- begin: |01/01/2017|
    -- end: |01/01/2021|
  }]
  assertion scope121.income_excluded_from_gross_income = $250,000
*/

In the second test, we assume the property was only used for one year in the 
last five years: (a) does not apply.

/*

declaration scope Test2:
  context scope121 scope Section121

scope Test2:
  definition scope121.date_of_sale_or_exchange equals |01/01/2021|
  definition scope121.gain_from_sale_or_exchange_of_property equals $250,000
  definition scope121.property_ownage equals [Period {
    -- begin: |01/01/2017|
    -- end: |01/01/2021|
  }]
  definition scope121.property_usage_as_principal_residence equals [Period {
    -- begin: |01/01/2017|
    -- end: |01/01/2018|
  }]
  assertion scope121.income_excluded_from_gross_income = $0
*/