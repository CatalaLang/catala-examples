
@@Partie réglementaire - Décrets simples@@+

@@Livre 5 : Prestations familiales et prestations assimilées@@++

@@Titre 2 : Prestations générales d'entretien@@+++

@@Chapitre 1er : Allocations familiales@@++++

@Article D521-1@
I.- Pour l'application de l'article L. 521-1, le montant des allocations familiales et de la majoration pour âge prévue à l'article L. 521-3 est défini selon le barème suivant :
/*
# Composantes des allocations familiales
champ d'application CalculAllocationsFamiliales :
  définition allocations_familiales.base sous condition
    allocations_familiales.droits_ouverts
  conséquence égal à
    allocations_familiales.montant_deuxieme_enfant +
    allocations_familiales.montant_troisième_enfant_et_plus
*/
1° Lorsque le ménage ou la personne a disposé d'un montant de ressources inférieur ou égal au plafond défini au I de l'article D. 521-3, les taux servant au calcul des allocations familiales sont fixés, en pourcentage de la base mensuelle prévue à l'article L. 551-1, à :
a) 32 % pour le deuxième enfant à charge ;
/*
champ d'application CalculAllocationsFamiliales :
  définition allocations_familiales.montant_deuxieme_enfant sous condition
    l521_1.ressources_ménage <= d521_3.plafond_I
  conséquence égal à
    si nombre de ménage.enfants >= 2
    alors prestations_familiales.base_mensuelle * 32 %
    sinon 0 €
*/

b) 41 % pour le troisième enfant à charge et chacun des suivants.
/*
champ d'application CalculAllocationsFamiliales :
  définition allocations_familiales.montant_troisième_enfant_et_plus
  sous condition
    l521_1.ressources_ménage <= d521_3.plafond_I
  conséquence égal à
    si nombre de ménage.enfants >= 3
    alors prestations_familiales.base_mensuelle * 41 % * (
      (nombre de ménage.enfants) - 2
    ) sinon 0 €
*/
La majoration pour âge est fixée à 16 % de la base mensuelle de calcul des prestations familiales ;
/*
champ d'application CalculAllocationsFamiliales :
   définition
     majorations_allocations_familiales.base_par_enfant de enfant
   sous condition
     l521_1.ressources_ménage <= d521_3.plafond_I
   conséquence égal à
     prestations_familiales.base_mensuelle * 16 %
*/
2° Lorsque le ménage ou la personne a disposé d'un montant de ressources supérieur au plafond défini au I de l'article D. 521-3 et inférieur ou égal à celui défini au II du même article, les taux servant au calcul des allocations familiales sont fixés, en pourcentage de la base mensuelle prévue à l'article L. 551-1, à :
a) 16 % pour le deuxième enfant à charge ;
/*
champ d'application CalculAllocationsFamiliales :
  définition allocations_familiales.montant_deuxieme_enfant sous condition
    (l521_1.ressources_ménage > d521_3.plafond_I) et
    (l521_1.ressources_ménage <= d521_3.plafond_II)
  conséquence égal à
    si nombre de ménage.enfants >= 2
    alors prestations_familiales.base_mensuelles * 32 %
    sinon 0 €
*/
b) 20,5 % pour le troisième enfant à charge et chacun des suivants.
/*
champ d'application CalculAllocationsFamiliales :
  définition allocations_familiales.montant_troisième_enfant_et_plus
  sous condition
    (l521_1.ressources_ménage > d521_3.plafond_I) et
    (l521_1.ressources_ménage <= d521_3.plafond_II)
  conséquence égal à
    si nombre de ménage.enfants >= 3
    alors prestations_familiales.base_mensuelle * 20.5 % * (
      (nombre de ménage.enfants) - 2
    ) sinon 0 €
*/
La majoration pour âge est fixée à 8 % de la base mensuelle de calcul des prestations familiales ;
/*
champ d'application CalculAllocationsFamiliales :
   définition
     majorations_allocations_familiales.base_par_enfant de enfant
   sous condition
     (l521_1.ressources_ménage > d521_3.plafond_I) et
     (l521_1.ressources_ménage <= d521_3.plafond_II)
   conséquence égal à
     prestations_familiales.base_mensuelle * 8 %
*/

3° Lorsque le ménage ou la personne a disposé d'un montant de ressources supérieur au plafond défini au II de l'article D. 521-3, les taux servant au calcul des allocations familiales sont fixés, en pourcentage de la base mensuelle prévue à l'article L. 551-1, à :
a) 8 % pour le deuxième enfant à charge ;
/*
champ d'application CalculAllocationsFamiliales :
  définition allocations_familiales.montant_deuxieme_enfant sous condition
    (l521_1.ressources_ménage > d521_3.plafond_II)
  conséquence égal à
    si nombre de ménage.enfants >= 2
    alors prestations_familiales.base_mensuelle * 8 %
    sinon 0 €
*/
b) 10,25 % pour le troisième enfant à charge et chacun des suivants.
/*
champ d'application CalculAllocationsFamiliales :
  définition allocations_familiales.montant_troisième_enfant_et_plus
  sous condition
    (l521_1.ressources_ménage > d521_3.plafond_II)
  conséquence égal à
    si nombre de ménage.enfants >= 3
    alors prestations_familiales.base_mensuelle * 10.25 % * (
      (nombre de ménage.enfants) - 2
    ) sinon 0 €
*/
La majoration pour âge est fixée à 4 % de la base mensuelle de calcul des prestations familiales.
/*
champ d'application CalculAllocationsFamiliales :
   définition
     majorations_allocations_familiales.base_par_enfant de enfant
   sous condition
     (l521_1.ressources_ménage > d521_3.plafond_II)
   conséquence égal à
     prestations_familiales.base_mensuelle * 4 %
*/

II.- En application du sixième alinéa de l'article L. 521-1, le montant mensuel des allocations familiales et, le cas échéant, de la majoration pour âge est majoré d'un complément dégressif, lorsque les ressources annuelles du ménage ou de la personne dépassent l'un des plafonds défini au I ou au II de l'article D. 521-3 d'une somme inférieure à douze fois le montant mensuel des allocations familiales augmenté, le cas échéant, de la ou des majorations pour âge.

Ce complément dégressif est égal, pour chaque mois, au douzième de la différence entre, d'une part, ce plafond de ressources majoré de la somme définie à l'alinéa précédent et, d'autre part, le montant des ressources.
/*
champ d'application CalculAllocationsFamiliales :
  définition complément_dégressif.dépassement de allocation
  sous condition
    (l521_1.ressources_ménage > d521_3.plafond_I) et
    (l521_1.ressources_ménage <= d521_3.plafond_I + 12 * allocation)
  conséquence égal à
    d521_3.plafond_I + 12 * allocation - l521_1.ressources_ménage

  définition complément_dégressif.dépassement de allocation
  sous condition
    (l521_1.ressources_ménage > d521_3.plafond_II) et
    (l521_1.ressources_ménage <= d521_3.plafond_II + 12 * allocation)
  conséquence égal à
      d521_3.plafond_II + 12 * allocation - l521_1.ressources_ménage

  # Dans les autres cas, le dépassement est nul
  définition complément_dégressif.dépassement de allocations égal à 0 €

  règle complément_dégressif.droits_ouverts de allocation
    sous condition complément_dégressif.dépassement de allocation > 0 €
    conséquence rempli

  définition
    complément_dégressif.pour_allocations_familiales_et_majorations
  sous condition complément_dégressif.droits_ouverts de complément_dégressif.base
  conséquence égal à
      (complément_dégressif.dépassement de complément_dégressif.base) / 12

  définition complément_dégressif.base
  égal à
    (si allocations_familiales.droits_ouverts alors
        allocations_familiales.montant_versé
     sinon 0 €) +
    (si majorations_allocations_familiales.droits_ouverts alors
        majorations_allocations_familiales.montant_versé
     sinon 0 €)
*/

@Article D521-2@
I.- Le montant mensuel de l'allocation forfaitaire prévue au deuxième alinéa de l'article L. 521-1 est défini selon le barème suivant :

1° Lorsque le ménage ou la personne a disposé d'un montant de ressources inférieur ou égal au plafond défini au I de l'article D. 521-3, le montant mensuel de l'allocation forfaitaire est fixé à 20,234 % de la base mensuelle de calcul des allocations familiales par enfant ;
/*
champ d'application CalculAllocationsFamiliales :
  définition allocation_forfaitaire.montant_versé sous condition
    l521_1.ressources_ménage <= d521_3.plafond_I
  conséquence égal à
    prestations_familiales.base_mensuelle * 20.234 % *
    nombre pour enfant dans ménage.enfants de
      allocation_forfaitaire.droits_ouverts de enfant
*/

2° Lorsque le ménage ou la personne a disposé d'un montant de ressources supérieur au plafond défini au I de l'article D. 521-3 et inférieur ou égal à celui défini au II du même article, le montant mensuel de l'allocation forfaitaire est fixé à 10,117 % de la base mensuelle de calcul des allocations familiales par enfant ;
/*
champ d'application CalculAllocationsFamiliales :
  définition allocation_forfaitaire.montant_versé sous condition
    (l521_1.ressources_ménage > d521_3.plafond_I) et
    (l521_1.ressources_ménage <= d521_3.plafond_II)
  conséquence égal à
    prestations_familiales.base_mensuelle * 10.117 % *
    nombre pour enfant dans ménage.enfants de
      allocation_forfaitaire.droits_ouvert de enfant
*/

3° Lorsque le ménage ou la personne a disposé d'un montant de ressources supérieur au plafond défini au II de l'article D. 521-3, le montant mensuel de l'allocation forfaitaire est fixé à 5,059 % de la base mensuelle de calcul des allocations familiales par enfant.
/*
champ d'application CalculAllocationsFamiliales :
  définition allocation_forfaitaire.montant_versé sous condition
    l521_1.ressources_ménage > d521_3.plafond_II
  conséquence égal à
    prestations_familiales.base_mensuelle * 5.059 % *
    nombre pour enfant dans ménage.enfants de
      allocation_forfaitaire.droits_ouvert de enfant
*/

II.- En application du sixième alinéa de l'article L. 521-1, le montant mensuel de l'allocation forfaitaire est majoré d'un complément dégressif lorsque les ressources annuelles du ménage ou de la personne dépassent l'un des plafonds défini au I ou au II de l'article D. 521-3 d'une somme inférieure à douze fois le montant mensuel de l'allocation forfaitaire auquel l'enfant ouvre droit.
Ce complément dégressif est égal, pour chaque mois, au douzième de la différence entre, d'une part, ce plafond de ressources majoré de la somme définie à l'alinéa précédent et, d'autre part, le montant des ressources.
/*
champ d'application CalculAllocationsFamiliales :
  définition
    complément_dégressif.pour_allocation_forfaitaire
  sous condition
    allocation_forfaitaire.droits_ouverts et
    complément_dégressif.droits_ouverts de allocation_forfaitaire.droits_ouverts
  conséquence égal à
    (complément_dégressif.dépassement
      de allocation_forfaitaire.montant_versé) / 12
*/

III.- Le nombre minimum d'enfants à charge mentionné au deuxième alinéa de l'article L. 521-1 est fixé à trois.
/*
champ d'application CalculAllocationsFamiliales :
  définition l512_1.nombre_minimum_enfants égal à 3
*/

@Article D521-3@

I.- Le plafond prévu au 1° du I des articles D. 521-1 et D. 521-2 est fixé à 55 950 euros. Il est majoré de 5 595 euros par enfant à charge.
/*
champ d'application CalculAllocationsFamiliales :
  définition d521_3.plafond_I égal à 55950 € +
    5595 € * nombre de ménage.enfants
*/

II.- Le plafond prévu au 2° du I des articles D. 521-1 et D. 521-2 est fixé à 78 300 euros. Il est majoré de 5 595 euros par enfant à charge.
/*
champ d'application CalculAllocationsFamiliales :
  définition d521_3.plafond_II égal à 78300 € +
    5595 € * nombre de ménage.enfants
*/

III.- Les montants des plafonds et de leur majoration respective fixés au présent article sont revalorisés au 1er janvier de chaque année, conformément à l'évolution en moyenne annuelle des prix à la consommation hors tabac de l'année civile de référence, par arrêté des ministres chargés de la sécurité sociale, du budget et de l'agriculture.

@@Livre 7 : Régimes divers - Dispositions diverses@@++

@@Titre 5 : Départements d'outre-mer@@+++

@@Chapitre 5 : Prestations familiales et prestations assimilées@@++++

@Article D755-5@
I. - Les taux servant au calcul des allocations familiales et de la majoration prévue à l'article L. 755-11 sont identiques à ceux mentionnés à l'article D. 521-1.

/*
# Pas de changement à déclarer
*/

II. - En application de l'article L. 755-11, 2e alinéa, le taux servant au calcul des allocations familiales servies pour un seul enfant à charge est fixé à 5,88 p. 100 de la base mensuelle prévue à l'article L. 755-3.

/*
# Composantes des allocations familiales
champ d'application CalculAllocationsFamiliales :
  définition allocations_familiales.base sous condition
    l751_1.régime_outre_mer et
    allocations_familiales.droits_ouverts
  conséquence égal à
    si nombre de ménage.enfants = 1 alors
      allocations_familiales.montant_premier_enfant
    sinon (
      allocations_familiales.montant_deuxieme_enfant +
      allocations_familiales.montant_troisième_enfant_et_plus
    )

  définition allocations_familiales.montant_premier_enfant égal à
    prestations_familiales.base_mensuelle_dom * 5.88 %

# Question : le complément dégressif s'applique-t-il ici ? Car le complément
# est basé sur les plafonds du taux de base qui ne s'appliquent pas ici,
# donc il serait logique que le plafond ne s'applique pas.
*/
La majoration des allocations familiales pour un seul enfant à charge est fixée à 3,69 p. 100 de la base mensuelle prévue à l'article L. 755-3 à partir de onze ans et à 5,67 p. 100 à partir de seize ans.
/*
champ d'application CalculAllocationsFamiliales :
  définition l521_3.âge_limite_alinéa_1 de enfant
  sous condition l751_1.régime_outre_mer
  conséquence égal à 11 an
définition
  majorations_allocations_familiales.base_par_enfant de enfant
sous condition
  l751_1.régime_outre_mer et
  (majorations_allocations_familiales.droits_ouverts de enfant)
conséquence égal à
  prestations_familiales.base_mensuelle_dom *
  (si enfant.âge >= 16 an alors 5.67 % sinon 3.69 %)
*/

@@Instruction  interministérielle  no  DSS/SD2B/2020/33  du  18  février  2020
relative  à  la  revalorisation  au  1er  avril  2020  des  prestations
familiales  servies  en  métropole,  en  Guadeloupe,  en  Guyane, en
Martinique, à La Réunion, à Saint-Barthélemy, à Saint-Martin et dans
le département de Mayotte@@

Au  titre  de  l’année  2020,  l’article  81  de  la  loi  du  24  décembre  2019  de  fnancement  de  la  sécurité  sociale  a  prévu,  par  dérogation  aux  dispositions  de  l’article  L.  161-25,  une  revalorisation  de  0,3  %  du montant des prestations relevant de cet article.

Le montant précité de la base mensuelle de calcul des allocations familiales (BMAF), en pourcentage duquel sont fxés les montants des prestations familiales, est ainsi porté de 413,16 € à 414,4 € au 1er avril 2020.

/*
champ d'application CalculAllocationsFamiliales :
  définition prestations_familiales.base_mensuelle
  sous condition
    date_aujourd_hui >= |01/04/2020| et
    date_aujourd_hui < |01/04/2021|
  conséquence égal à 414.4 €
*/

Des  règles  spécifques  s’appliquent  par  ailleurs  aux  prestations  familiales  suivantes  en  métropole  tout  comme  dans  les  collectivités  de  Guadeloupe,  Guyane,  Martinique,  La  Réunion,  Saint-Barthélemy et à Saint-Martin : l’article 37 de la LFSS pour 2018 a prévu une mesure d’harmonisation du barème de l’allocation de  base  (AB),  de  la  prime  à  la  naissance  et  de  la  prime  à  l’adoption  sur  celui  du  complément  familial. Cette réforme est applicable aux enfants nés ou adoptés à compter du 1er avril 2018 et en conséquence à l’ensemble des enfants à compter du 1er avril 2021.

En  revanche,  elle  ne  s’applique  pas  aux  familles  au  titre  des  enfants  à  leur  charge  nés  ou  adoptés  jusqu’au  31  mars  2018.  Au  titre  de  ces  enfants,  les  montants  de  l’AB  (à  taux  plein  et  à  taux  partiel),  de  la  prime  à  la  naissance  et  de  la  prime  à  l’adoption  sont  maintenus  à  leur  niveau  en  vigueur  au  1er  avril  2013,  conformément  aux  dispositions  de  l’article  74  de  la  loi  de  fnancement  de  la  sécurité  sociale  pour  2014.  Ces  familles  se  voient  toutefois  appliquer  les  nouvelles  dispositions  au  titre  d’une  nouvelle  naissance  ou  adoption  intervenant  à  compter  du 1er avril 2018 ;–le  montant  du  sixième  complément  de  l’allocation  d’éducation  de  l’enfant  handicapé  (AEEH),  revalorisé  de  la  même  manière  que  les  pensions  mentionnées  à  l’article  L.  341-6  du  code  de  la  sécurité  sociale,  fait  l’objet  d’une  revalorisation  de  0,3  %  au  1er  avril  2020  qui  le  porte  de  1 121,92 € par mois au 1er avril 2019 à 1 125,29 € par mois au 1er avril 2020.

D’autre  part,  dans  les  collectivités  de  Guadeloupe,  Guyane,  Martinique,  La  Réunion,  Saint-Barthélemy  et  Saint-Martin,  conformément  à  l’article  25  de  la  loi  du  28  février  2017  de  programmation  relative  à  l’égalité  réelle  outre-mer  et  portant  autres  dispositions  en  matière  sociale  et  économique, les montants respectifs du complément familial et de son montant majoré sont alignés sur ceux de la métropole à compter du 1er avril 2020.

Enfn, le montant des allocations familiales pour les familles de deux enfants et plus connaît dans le département de Mayotte une dernière majoration exceptionnelle au 1er janvier 2021 liée à la mise en œuvre du plan de convergence qui s’achèvera à cette date.

Les  tableaux  annexés  ont  pour  objet  d’indiquer  aux  organismes  débiteurs  des  prestations  familiales le montant des prestations familiales (avant le précompte de la contribution au remboursement  de  la  dette  sociale)  applicable  pour  procéder  à  la  liquidation  des  prestations  familiales  à  compter du 1er avril 2020 en métropole, en Guadeloupe, en Guyane, en Martinique, à La Réunion, à Saint-Barthélemy et à Saint-Martin. Ces montants sont arrondis au centième d’euro le plus proche ; il en est de même lorsqu’il s’agit du service d’une allocation différentielle.

Ils  indiquent  les  montants  relatifs  aux  allocations  familiales,  à  l’allocation  de  rentrée  scolaire,  à  l’AEEH  (allocation  de  base,  complément  et  majoration  pour  parent  isolé),  au  complément  familial  et  à  son  montant  majoré  sur  la  période  du  1er  avril  2020  au  31  mars  2021  dans  le  département  de  Mayotte.

Je  vous  demande  de  bien  vouloir  transmettre  à  la  connaissance  des  organismes  débiteurs  les  présentes instructions.

@@Inclusion: sources/ste_20200002_0000_p000.pdf@ p. 254@@

/*
# L'annexe ne définit rien de plus que ce qui a déjà été défini dans les décrets
# pour la métropole et les DOM
*/

@@Inclusion: sources/ste_20200002_0000_p000.pdf@ p. 257@@
@@Inclusion: sources/ste_20200002_0000_p000.pdf@ p. 258@@

/*
# Cependant, le cas de Mayotte n'est pas traité dans la loi et ce sont donc
# les règles de cette annexe qui s'apppliquent.
champ d'application CalculAllocationsFamiliales :
  définition allocations_familiales.base sous condition
    ménage.résidence = Mayotte et
    allocations_familiales.droits_ouverts
  conséquence égal à
    si nombre de ménage.enfants = 1 alors
      allocations_familiales.montant_premier_enfant
    sinon (
      allocations_familiales.montant_deuxieme_enfant +
      allocations_familiales.montant_troisième_enfant_et_plus
    )

  définition allocations_familiales.montant_deuxieme_enfant sous condition
    ménage.résidence = Mayotte et
    date_aujourd_hui > |30/04/2020| et
    date_aujourd_hui <= |31/12/2020|
  conséquence égal à
    prestations_familiales.base_mensuelle * 30.68 %

  définition allocations_familiales.montant_troisième_enfant_et_plus sous condition
    ménage.résidence = Mayotte et
    date_aujourd_hui > |30/04/2020| et
    date_aujourd_hui <= |31/12/2020|
  conséquence égal à
    prestations_familiales.base_mensuelle * 14.3 % +
    (si nombre de ménage.enfants >= 4 alors
      prestations_familiales.base_mensuelle * 4.63 % *
        (nombre de ménage.enfants - 3)
      sinon 0 €)

  définition allocations_familiales.montant_deuxieme_enfant sous condition
    ménage.résidence = Mayotte et
    date_aujourd_hui > |01/01/2021| et
    date_aujourd_hui <= |31/03/2021|
  conséquence égal à
    prestations_familiales.base_mensuelle * 32 %

  définition allocations_familiales.montant_troisième_enfant_et_plus sous condition
    ménage.résidence = Mayotte et
    date_aujourd_hui > |01/01/2021| et
    date_aujourd_hui <= |31/03/2021|
  conséquence égal à
    prestations_familiales.base_mensuelle * 16 % +
    (si nombre de ménage.enfants >= 4 alors
      prestations_familiales.base_mensuelle * 4.63 % *
        (nombre de ménage.enfants - 3)
      sinon 0 €)

  définition allocations_familiales.montant_premier_enfant sous condition
    ménage.résident = Mayotte et
    allocations_familiales.date_ouverture_droits < |01/01/2012|
  conséquence égal à
    57.28 €

  définition allocations_familiales.montant_premier_enfant sous condition
    ménage.résident = Mayotte et
    allocations_familiales.date_ouverture_droits >= |01/01/2012| et
    date_aujourd_hui > |30/04/2020| et
    date_aujourd_hui <= |31/12/2020|
  conséquence égal à
    prestations_familiales.base_mensuelle * 7.17 %

  définition allocations_familiales.montant_premier_enfant sous condition
    ménage.résident = Mayotte et
    allocations_familiales.date_ouverture_droits >= |01/01/2012| et
    date_aujourd_hui > |01/01/2021| et
    date_aujourd_hui <= |31/03/2021|
  conséquence égal à
    prestations_familiales.base_mensuelle * 5.88 %
*/


@@Décret n° 2019-1387 du 18 décembre 2019 portant relèvement du salaire minimum de croissance@@

@Article 1@
A compter du 1er janvier 2020, pour les catégories de travailleurs mentionnés à l'article L. 2211-1 du code du travail, le montant du salaire minimum de croissance est relevé dans les conditions ci-après :
1° En métropole, en Guadeloupe, en Guyane, en Martinique, à La Réunion, à Saint-Barthélemy, à Saint-Martin et à Saint-Pierre-et-Miquelon, son montant est porté à 10,15 € l'heure ;
/*
champ d'application CalculAllocationsFamiliales :
  définition smic.brut_horaire sous condition
    (ménage.résidence = Métropole) ou
    (ménage.résidence = Guyane) ou
    (ménage.résidence = Martinique) ou
    (ménage.résidence = LaRéunion) ou
    (ménage.résidence = SaintBarthélemy) ou
    (ménage.résidence = SaintMartin) ou
    (ménage.résidence = SaintPierreEtMiquelon)
  conséquence égal à 10.15 €
*/
2° A Mayotte, son montant est fixé à 7,66 € l'heure.
/*
champ d'application CalculAllocationsFamiliales :
  définition smic.brut_horaire sous condition
    (ménage.résidence = Mayotte)
  conséquence égal à 7.66 €
*/
