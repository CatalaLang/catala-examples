## Rozdzial 3

### [Art. 7.]

1. Stawki podatku wynoszą:

1) od umowy sprzedaży:

a) nieruchomości, rzeczy ruchomych, prawa użytkowania wieczystego, własnościowego spółdzielczego prawa do lokalu mieszkalnego, spółdzielczego prawa do lokalu użytkowego oraz wynikających z przepisów prawa spółdzielczego: prawa do domu jednorodzinnego oraz prawa do lokalu w małym domu mieszkalnym – 2%,

```catala
deklaracja zakres UmowaSprzedazy:
  kontekst kwota zawartosc pieniadze
  kontekst stawka zawartosc dziesietna
  kontekst podatek zawartosc pieniadze
  kontekst inne_prawa_majatkowe warunek

zakres UmowaSprzedazy:
  definicja podatek wynosi
    kwota *$ stawka

zakres UmowaSprzedazy:
  etykieta a7_u1_p1_ppa
  definicja stawka wynosi 2%
```


b) innych praw majątkowych – 1%;
```catala
zakres UmowaSprzedazy:
  wyjatek a7_u1_p1_ppa
  definicja stawka pod warunkiem
    inne_prawa_majatkowe
  konsekwencja wynosi 1%
```

2) od umów zamiany, dożywocia, o dział spadku, o zniesienie współwłasności oraz darowizny:

a) przy przeniesieniu własności nieruchomości, rzeczy ruchomych, prawa użytkowania wieczystego, własnościowego spółdzielczego prawa do lokalu
mieszkalnego, spółdzielczego prawa do lokalu użytkowego oraz wynikających z przepisów prawa spółdzielczego: prawa do domu jednorodzinnego oraz prawa do lokalu w małym domu mieszkalnym – 2%,

```catala
deklaracja zakres UmowaZamianyLubDarowizny:
  kontekst kwota zawartosc pieniadze
  kontekst stawka zawartosc dziesietna
  kontekst podatek zawartosc pieniadze
  kontekst inne_prawa_majatkowe warunek

zakres UmowaZamianyLubDarowizny:
  definicja podatek wynosi
    kwota *$ stawka

zakres UmowaZamianyLubDarowizny:
  etykieta a7_u1_p2_ppa
  definicja stawka wynosi 2%
```

b) przy przeniesieniu własności innych praw majątkowych – 1%;

```catala
zakres UmowaZamianyLubDarowizny:
  wyjatek a7_u1_p2_ppa
  definicja stawka pod warunkiem
    inne_prawa_majatkowe
  konsekwencja wynosi 1%
```

3) od umowy ustanowienia odpłatnego użytkowania, w tym nieprawidłowego, oraz odpłatnej służebności – 1%, z zastrzeżeniem ust. 5;

```catala
deklaracja zakres OdplatneUzytkowanie:
  kontekst kwota zawartosc pieniadze
  kontekst stawka zawartosc dziesietna
  kontekst podatek zawartosc pieniadze

zakres OdplatneUzytkowanie:
  definicja podatek wynosi
    kwota *$ stawka

zakres OdplatneUzytkowanie:
  definicja stawka wynosi 1%
```

4) od umowy pożyczki oraz depozytu nieprawidłowego – 0,5%, z zastrzeżeniem ust. 5;

```catala
deklaracja zakres PozyczkaLubDepozytNieprawidlowy:
  kontekst kwota zawartosc pieniadze
  kontekst stawka zawartosc dziesietna
  kontekst podatek zawartosc pieniadze

zakres PozyczkaLubDepozytNieprawidlowy:
  definicja podatek wynosi
    kwota *$ stawka # TODO: extract somehow?

zakres PozyczkaLubDepozytNieprawidlowy:
  definicja stawka wynosi 0.5%
```
