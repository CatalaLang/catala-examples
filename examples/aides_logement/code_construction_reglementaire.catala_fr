## Partie réglementaire

### Livre VIII : Aides personnelles au logement

#### Titre II : Dispositions communes aux aides personnelles au logement

##### Chapitre Ier : Principes généraux

###### Section 1 : Règles de non-cumul

####### Article R821-1 | LEGIARTI000038879023

En vertu de la règle énoncée à l'article L. 821-2 , une aide personnelle au
logement ne peut être attribuée, au profit d'une même personne ou d'un même
ménage, au titre de plusieurs logements.

```catala
# À voir quelles sont exactement les condition pour la résidence principale,
# mais nous faisons ici l'hypothèse simplificatrice qu'il n'y a qu'une
# résidence principale. Si ce n'est pas le cas, alors l'utilisateur du programme
# pourra ne déclarer qu'une seule résidence principale (de son choix).
```

####### Article R821-2 | LEGIARTI000038879021

Lorsque le conjoint ou les enfants à charge du bénéficiaire occupent, à
titre de résidence principale, un local indépendant du logement occupé
par le bénéficiaire et situé dans le même bâtiment, ces deux locaux sont
assimilés au logement défini à l'article R. 822-23 .

```catala
# Cet article définit la manière de remplir les champs du formulaire
# de déclaration des résidences mais ne se traduit pas computationellement
# pour nous.
```

####### Article R821-3 | LEGIARTI000038879019

En cas de séparation, légale ou de fait, des conjoints entraînant la
création de deux foyers distincts et l'occupation de deux résidences
principales constatées par l'organisme payeur lors de l'ouverture du
droit ou au début de la période de paiement, une aide personnelle au
logement peut être accordée à chacun des conjoints.

```catala
# Voir SituationFamiliale dans L822-1.
```

####### Article R821-4 | LEGIARTI000038879017

Lorsque plusieurs personnes ou ménages constituant des foyers distincts
occupent le même logement et qu'ils sont copropriétaires du logement
et cotitulaires du prêt ouvrant droit aux aides personnelles au
logement, ou cotitulaires du bail ou de l'engagement de location,
une aide personnelle au logement peut être accordée à chacune de ces
personnes ou chacun de ces ménages.

```catala
# Chaque colocataire ou copropriétaire remplit sa demande d'APL en cochant
# les cases déjà prévues.
```

###### Section 2 : Règle d'exclusivité

####### Article R821-5 | LEGIARTI000038879013

Lorsque les conditions d'ouverture du droit à l'aide personnalisée au logement
sont remplies au titre d'un logement, seule cette aide est attribuée pour ce logement.

```catala
# Attention, Les APL ne sont pas cumulables avec les allocations de logement.
```

####### Article R821-6 | LEGIARTI000038879011

Lorsqu'une personne bénéficie de l'allocation de logement au titre de
l'acquisition du logement qu'elle occupe et qu'il lui est accordé un
prêt aidé par l'Etat en accession à la propriété pour l'agrandissement
de ce logement ou un prêt conventionné pour son amélioration, seule l'aide
personnalisée au logement lui est attribuée dans les conditions prévues par
les dispositions de la section 2 du chapitre II du titre III du présent livre
applicables à ces catégories de prêts et le droit à l'allocation de logement
est éteint à compter de l'ouverture du droit à l'aide personnalisée.

```catala
# Hypothèse : peut-être que cet article est une disposition de transition avec
# la période où les allocations de logement n'étaient pas fusionnées en aide
# personnalisée au logement.
# Si c'est le cas, nous le laissons hors du champ du programme.
```

##### Chapitre II : Conditions générales d'attribution

###### Section 1 : Conditions relatives au bénéficiaire

####### Article R822-1 | LEGIARTI000038879005

Les seuils mentionnés au second alinéa de l'article L. 822-3 sont
fixés à 10 % pour les parts de propriété et à 10 % pour les parts
d'usufruit, sans que l'ensemble de ces parts puisse égaler ou dépasser
10 % de la propriété ou de l'usufruit du logement.

```catala
champ d'application ÉligibilitéAidesPersonnelleLogement:
  définition seuil_l822_3_parts_propriété égal à 10%
  définition seuil_l822_3_parts_usufruit égal à 10%
```

###### Section 2 : Conditions relatives aux ressources

####### Sous-section 1 : Modalités générales de l'appréciation des ressources

######## Article R822-2 | LEGIARTI000041419135

Les ressources prises en compte pour le calcul de l'aide personnelle au logement
sont celles dont bénéficient le demandeur ou l'allocataire, son conjoint et
les personnes vivant habituellement au foyer.

```catala
# Ici "conjoint" veut implicitement dire également "concubin" car on
# a vu que les deux cas étaient gérés de la même manière précédemment.
# Interprétation confirmée par DGALN/DHUP/FE4 le 25/05/2022.
champ d'application RessourcesAidesPersonnelleLogement:
  définition ressources_prises_en_compte égal à
    ressources_demandeur +€
    ressources_conjoint +€
    ressources_personnes_vivant_habituellement_foyer

  définition ressources_personnes_vivant_habituellement_foyer égal à
    somme argent pour personne dans personnes_vivant_habituellement_foyer de
      personne.ressources
```

Sont considérées comme vivant habituellement au foyer les personnes y ayant
résidé plus de six mois au cours de la période mentionnée au 1° de l'article
R. 822-3 précédant la période de paiement prévue par l'article R. 823-6 et
qui y résident encore au moment de la demande de l'aide ou du réexamen du
droit à celle-ci.

```catala
champ d'application RessourcesAidesPersonnelleLogement:
  assertion pour tout personne dans personnes_vivant_habituellement_foyer on a
    personne.durée_résidence_durant_période_r_822_3_1_supérieure_à_6_mois
  # Ici on pourrait coder ça de manière beaucoup plus précise en tenant compte
  # des différentes périodes mentionnées et en forçant l'utilisateur à bien
  # remplir toutes ses périodes de résidence mais cela rendrait le formulaire
  # plus lourd et ne correspondrait peut-être pas au besoin d'un système expert
  # comme celui utilisé à la CNAF. On pourrait également laisser l'utilisateur
  # spécifier une durée en jours mais cela aurait deux inconvénients :
  # premièrement c'est une intrusion dans la vie privée du potentiel
  # bénéficiaire (cette information serait nécéssaire en cas de contrôle mais
  # pas dans tous les cas), et deuxièmement la comparaison d'un nombre de jours
  # avec "6 mois" est computationellement ambiguë.
```

NOTA :

Se reporter aux conditions d'application prévues à l'article 25 du décret
n° 2019-1574 du 30 décembre 2019. Conformément à l'article 1 du décret
n° 2020-451 du 20 avril 2020 l'article susvisé a été modifié : Ces dispositions
sont applicables au calcul des droits, d'une part, à l'aide personnalisée au
logement prévue pour les accédants à la propriété par l'article L. 831-1 du
code de la construction et de l'habitation, d'autre part, aux autres aides
personnelles au logement prévues par le même code, à compter des mois respectifs
fixés par arrêté des ministres en charge du logement, de la santé et du budget
et, au plus tard, du 1er janvier 2021.

Aux termes de l'article 2 du décret n° 2020-1816 du 29 décembre 2020, l'article
25 du décret n° 2019-1574 du 30 décembre 2019 a été modifié : Ces dispositions
sont applicables au calcul des droits à l’aide personnelle au logement à compter
du mois de janvier 2021 ou, pour le calcul des droits à l’aide personnalisée au
logement prévue pour les accédants à la propriété par l’article L. 831-1 du code
de la construction et de l’habitation, à compter du mois de mai 2021.

```catala
# La nota ne nous concerne pas car on ne considère le calcul qu'après 2021.
```

######## Article R822-3 | LEGIARTI000041419142

Les ressources et les charges prises en compte pour le calcul de l'aide
personnelle au logement sont appréciées, tous les trois mois, sous réserve
des dispositions prévues à l'article R. 823-6-1, selon les périodes de
référence suivantes :

1° Pour les ressources mentionnées à l'article R. 822-4 prises en compte par
la déclaration sociale nominative définie à l' article L. 133-5-3 du code de
la sécurité sociale et les revenus d'activité perçus hors de France ou versés
par une organisation internationale, sur une période de référence courant du
treizième au deuxième mois précédant la date d'ouverture ou de réexamen du
droit à l'aide personnelle au logement ;

2° Pour les pensions alimentaires versées ou perçues, les frais de tutelle,
les frais professionnels exposés, lorsque ceux-ci excèdent la déduction
forfaitaire mentionnée au 3° de l'article 83 du code général des impôts ,
et pour l'assujettissement à l'impôt sur la fortune immobilière mentionné à
l'article 964 du même code, sur une période de référence correspondant à
l'année civile qui précède la date d'ouverture ou de réexamen du droit à
l'aide personnelle au logement.

A défaut de déclaration par le bénéficiaire des ressources mentionnées au 2°,
sont pris en compte à titre provisoire lors du réexamen de ses droits :

a) Pour les pensions alimentaires versées et les frais de tutelle exposés,
un montant nul ;

b) Pour les pensions alimentaires reçues, les dernières ressources connues
deux ans avant la date d'ouverture ou de réexamen du droit ;

c) Pour la déduction des frais professionnels, la déduction forfaitaire
mentionnée au 3° de l'article 83 du code général des impôts .

Ces montants provisoires donnent lieu, le cas échéant, à régularisation, au
vu des données de l'année civile antérieure à la période de référence
transmises par l'administration fiscale ;

3° Pour les autres revenus imposables, sous réserve pour les travailleurs
indépendants des dispositions de l'article R. 822-5, sur une période de
référence correspondant à l'avant-dernière année précédant la date d'ouverture
ou de réexamen du droit à l'aide personnelle au logement.

NOTA :

Se reporter aux conditions d'application prévues à l'article 25 du décret
n° 2019-1574 du 30 décembre 2019. Conformément à l'article 1 du décret
n° 2020-451 du 20 avril 2020 l'article susvisé a été modifié : Ces dispositions
sont applicables au calcul des droits, d'une part, à l'aide personnalisée au
logement prévue pour les accédants à la propriété par l'article L. 831-1 du
code de la construction et de l'habitation, d'autre part, aux autres aides
personnelles au logement prévues par le même code, à compter des mois
respectifs fixés par arrêté des ministres en charge du logement, de la santé
et du budget et, au plus tard, du 1er janvier 2021.

Aux termes de l'article 2 du décret n° 2020-1816 du 29 décembre 2020,
l'article 25 du décret n° 2019-1574 du 30 décembre 2019 a été modifié :
Ces dispositions sont applicables au calcul des droits à l’aide personnelle
au logement à compter du mois de janvier 2021 ou, pour le calcul des droits
à l’aide personnalisée au logement prévue pour les accédants à la propriété
par l’article L. 831-1 du code de la construction et de l’habitation, à compter
du mois de mai 2021.

```catala
# Nous choissisons par manque de ressources (sic) de développement de laisser
# le calcul des ressources à l'utilisateur du programme qui devra suivre
# les instructions de cet article et de ceux auxquels il se réfère pour
# correctement évaluer leur montant.
```

######## Article R822-4 | LEGIARTI000045920163

I.-Les ressources prises en compte s'entendent du total des revenus nets
catégoriels retenus pour l'établissement de l'impôt sur le revenu, des revenus
taxés à un taux proportionnel ou soumis à un prélèvement libératoire de l'impôt
sur le revenu ainsi que des revenus perçus hors de France ou versés par une
organisation internationale.

Sont également pris en compte :

1° Suivant les règles applicables en matière d'imposition aux traitements
et salaires prévues au deuxième alinéa du 3° de l' article 83 et au 5 (a) de
l'article 158 du code général des impôts , les indemnités journalières
mentionnées au 2° de l'article L. 431-1 du code de la sécurité sociale ;

2° Les rémunérations liées aux heures supplémentaires ou assimilées mentionnées
à l' article 81 quater du code général des impôts après application d'une
déduction calculée selon les mêmes règles que celles mentionnées au deuxième
alinéa du 3° de l'article 83 du même code.

II.-Sont déduits du décompte des ressources :

1° Les créances alimentaires mentionnées au 2° du II de l'article 156 du code
général des impôts et majorées dans les conditions prévues au 7 de l'article
158 du même code ;

2° L'abattement mentionné à l' article 157 bis du code général des impôts pour
les personnes nées avant le 1er janvier 1931 et pour les personnes invalides.

III.-Ne sont pas déduits du décompte des ressources les déficits constatés au
cours d'une année antérieure à celle de la période de référence définie au 3°
de l'article R. 822-3 et qui font l'objet d'un report, en vertu des dispositions
du I de l'article 156 du code général des impôts.

IV.-Ne sont pas pris en compte :

1° Les arrérages des rentes viagères constituées en faveur d'une personne
handicapée, mentionnés à l'article 199 septies du code général des impôts ;

2° Les indemnités versées aux personnes tirées au sort mentionnées à l'article
4-3 et au 2° de l'article 12 de l'ordonnance n° 58-1360 du 29 décembre 1958
portant loi organique relative au Conseil économique, social et environnemental.

NOTA :

Se reporter aux conditions d'application prévues à l'article 25 du décret
n° 2019-1574 du 30 décembre 2019. Conformément à l'article 1 du décret
n° 2020-451 du 20 avril 2020 l'article susvisé a été modifié :
Ces dispositions sont applicables au calcul des droits, d'une part,
à l'aide personnalisée au logement prévue pour les accédants à la propriété
par l'article L. 831-1 du code de la construction et de l'habitation,
d'autre part, aux autres aides personnelles au logement prévues par le
même code, à compter des mois respectifs fixés par arrêté des ministres
en charge du logement, de la santé et du budget et, au plus tard,
du 1er janvier 2021.

Aux termes de l'article 2 du décret n° 2020-1816 du 29 décembre 2020,
l'article 25 du décret n° 2019-1574 du 30 décembre 2019 a été modifié :
Ces dispositions sont applicables au calcul des droits à l’aide personnelle
au logement à compter du mois de janvier 2021 ou, pour le calcul des droits à
l’aide personnalisée au logement prévue pour les accédants à la propriété par
l’article L. 831-1 du code de la construction et de l’habitation, à compter du
mois de mai 2021.


```catala
# Nous choissisons par manque de ressources (sic) de développement de laisser
# le calcul des ressources à l'utilisateur du programme qui devra suivre
# les instructions de cet article et de ceux auxquels il se réfère pour
# correctement évaluer leur montant.
```

######## Article R822-5 | LEGIARTI000041419180

Les revenus professionnels des travailleurs indépendants sont ceux pris en
compte dans la catégorie des bénéfices industriels et commerciaux, des
bénéfices agricoles et des bénéfices non commerciaux de l'avant-dernière
année précédant la date d'ouverture ou de réexamen du droit.

Pour les travailleurs ayant débuté une activité indépendante postérieurement
ou au cours de l'avant-dernière année précédant la date d'ouverture ou de
réexamen du droit, les revenus professionnels sont calculés par l'organisme
chargé du calcul des aides personnelles au logement en appliquant au montant
du chiffre d'affaires ou du total des recettes déclarés par le demandeur ou
l'allocataire pendant la période de référence visée au 1° de l'article
R. 822-3 précédant l'examen ou la révision du droit, un abattement dont le
taux correspond à celui qui est mentionné aux articles 50-0 , 64 bis et 102 ter
du code général des impôts pour chaque catégorie d'activité mentionnée à ces
articles.

NOTA :

Se reporter aux conditions d'application prévues à l'article 25 du décret
n° 2019-1574 du 30 décembre 2019. Conformément à l'article 1 du décret
n° 2020-451 du 20 avril 2020 l'article susvisé a été modifié : Ces dispositions
sont applicables au calcul des droits, d'une part, à l'aide personnalisée au
logement prévue pour les accédants à la propriété par l'article L. 831-1 du
code de la construction et de l'habitation, d'autre part, aux autres aides
personnelles au logement prévues par le même code, à compter des mois respectifs
fixés par arrêté des ministres en charge du logement, de la santé et du budget
et, au plus tard, du 1er janvier 2021.

Aux termes de l'article 2 du décret n° 2020-1816 du 29 décembre 2020, l'article
25 du décret n° 2019-1574 du 30 décembre 2019 a été modifié : Ces dispositions
sont applicables au calcul des droits à l’aide personnelle au logement à compter
du mois de janvier 2021 ou, pour le calcul des droits à l’aide personnalisée au
logement prévue pour les accédants à la propriété par l’article L. 831-1 du code
de la construction et de l’habitation, à compter du mois de mai 2021.

```catala
# Nous choissisons par manque de ressources (sic) de développement de laisser
# le calcul des ressources à l'utilisateur du programme qui devra suivre
# les instructions de cet article et de ceux auxquels il se réfère pour
# correctement évaluer leur montant.
```

######## Article R822-6 | LEGIARTI000038878991

Ne sont prises en compte que pour la fraction dépassant le plafond
individuel prévu à l' article L. 815-9 du code de la sécurité sociale multiplié
par 1,25 les ressources de chacune des personnes vivant habituellement au foyer
qui sont :

1° Soit enfants du bénéficiaire de l'aide ou de son conjoint ;

2° Soit ascendants du bénéficiaire ou de son conjoint ayant atteint un âge au
moins égal à celui prévu par l' article L. 161-17-2 du code de la sécurité
sociale augmenté de cinq années, ou d'un âge au moins égal à celui prévu par
le même article en cas d'inaptitude au travail ou âgés d'au moins soixante-cinq
ans s'ils percevaient l'allocation de solidarité aux personnes âgées ;

3° Soit ascendants, descendants ou collatéraux au deuxième degré ou au troisième
degré du bénéficiaire ou de son conjoint et titulaires de la carte
" mobilité-inclusion " comportant la mention " invalidité " prévue à l' article
L. 241-3 du code de l'action sociale et des familles ou de la carte d'invalidité
prévue à l'article L. 241-3 du même code, dans sa rédaction antérieure au 1er
janvier 2017.

```catala
# Nous choissisons par manque de ressources (sic) de développement de laisser
# le calcul des ressources à l'utilisateur du programme qui devra suivre
# les instructions de cet article et de ceux auxquels il se réfère pour
# correctement évaluer leur montant.
```

####### Sous-section 2 : Principes de neutralisation et d'abattement

######## Article R822-7 | LEGIARTI000041419409

Les ressources, déterminées dans les conditions prévues aux articles
R. 822-2 à R. 822-6, sont diminuées d'un abattement forfaitaire, lorsque
les deux conjoints ont exercé une activité professionnelle productrice de
revenus au cours de la période de référence définie au 1° de l'article
R. 822-3 et que chacun des deux revenus professionnels pris en compte dans
les ressources a été au moins égal à douze fois la base mensuelle de calcul
des allocations familiales en vigueur à la date de la demande ou du réexamen
du droit.

```catala
champ d'application RessourcesAidesPersonnelleLogement:
  définition abattement_r_822_7 égal à
    si demandeur_exerce_activité_rémunérée et
      conjoint_exerce_activité_rémunérée et
      (ressources_demandeur +€ ressources_conjoint) >=€
       base_mensuelle_allocations_familiales.montant *€ 12,0
    alors
      montant_forfaitaire_r_822_7
    sinon 0€

  définition base_mensuelle_allocations_familiales.date_courante égal à
    date_demande_ou_reexamen_droit
```

Le montant de cet abattement est fixé par arrêté des ministres chargés du
logement, du budget, de la sécurité sociale et de l'agriculture.

NOTA :

Se reporter aux conditions d'application prévues à l'article 25 du décret
n° 2019-1574 du 30 décembre 2019. Conformément à l'article 1 du décret
n° 2020-451 du 20 avril 2020 l'article susvisé a été modifié : Ces dispositions
sont applicables au calcul des droits, d'une part, à l'aide personnalisée au
logement prévue pour les accédants à la propriété par l'article L. 831-1 du code
de la construction et de l'habitation, d'autre part, aux autres aides
personnelles au logement prévues par le même code, à compter des mois respectifs
fixés par arrêté des ministres en charge du logement, de la santé et du budget
et, au plus tard, du 1er janvier 2021.

Aux termes de l'article 2 du décret n° 2020-1816 du 29 décembre 2020, l'article
25 du décret n° 2019-1574 du 30 décembre 2019 a été modifié : Ces dispositions
sont applicables au calcul des droits à l’aide personnelle au logement à compter
du mois de janvier 2021 ou, pour le calcul des droits à l’aide personnalisée au
logement prévue pour les accédants à la propriété par l’article L. 831-1 du code
de la construction et de l’habitation, à compter du mois de mai 2021.


```catala
# Nous choissisons par manque de ressources (sic) de développement de laisser
# le calcul des ressources à l'utilisateur du programme qui devra suivre
# les instructions de cet article et de ceux auxquels il se réfère pour
# correctement évaluer leur montant.
```

######## Article R822-8 | LEGIARTI000041419400

Lorsque le bénéficiaire justifie qu'en raison d'obligations professionnelles,
lui-même ou son conjoint est contraint d'occuper, de manière habituelle, un
logement distinct de sa ou de leur résidence principale et qu'il supporte des
charges de loyer supplémentaires correspondant à ce logement, il est procédé
à un abattement forfaitaire sur ses ressources ou sur celles du ménage.

```catala
champ d'application RessourcesAidesPersonnelleLogement:
  définition abattement_r_822_8 égal à
    selon paiement_logement_distinct_professionnel sous forme
    -- OuiAvecLoyerOuCharges de loyer_ou_charges:
      montant_forfaitaire_r_822_8
    -- PaiementLogementDistinctProfessionnel.Non: 0€
```

L'abattement est appliqué à compter du premier jour du mois civil suivant celui
au cours duquel le bénéficiaire doit supporter ces charges. Il est supprimé à
compter du premier jour du mois civil au cours duquel le bénéficiaire cesse de
les supporter.

Le montant de cet abattement est fixé par un arrêté des ministres chargés du
logement, du budget, de la sécurité sociale et de l'agriculture.

NOTA :

Se reporter aux conditions d'application prévues à l'article 25 du décret
n° 2019-1574 du 30 décembre 2019. Conformément à l'article 1 du décret
n° 2020-451 du 20 avril 2020 l'article susvisé a été modifié : Ces dispositions
sont applicables au calcul des droits, d'une part, à l'aide personnalisée au
logement prévue pour les accédants à la propriété par l'article L. 831-1 du
code de la construction et de l'habitation, d'autre part, aux autres aides
personnelles au logement prévues par le même code, à compter des mois respectifs
fixés par arrêté des ministres en charge du logement, de la santé et du budget
et, au plus tard, du 1er janvier 2021.

Aux termes de l'article 2 du décret n° 2020-1816 du 29 décembre 2020, l'article
25 du décret n° 2019-1574 du 30 décembre 2019 a été modifié : Ces dispositions
sont applicables au calcul des droits à l’aide personnelle au logement à
compter du mois de janvier 2021 ou, pour le calcul des droits à l’aide
personnalisée au logement prévue pour les accédants à la propriété par
l’article L. 831-1 du code de la construction et de l’habitation, à compter
du mois de mai 2021.

######## Article R822-9 | LEGIARTI000041419395

L'abattement prévu à l'article R. 822-8 est applicable aux
ressources prises en compte pour le calcul de l'aide personnelle au
logement due, à compter de la date d'ouverture du droit ou de son
renouvellement, aux personnes isolées résidant en logement-foyer,
lorsqu'elles apportent la preuve qu'elles assument ou contribuent à
assumer financièrement des charges familiales.

```catala
# Cet article indique comment remplir le formulaire quand on est dans
# la situation décrite mais n'apporte pas d'informations supplémentaires
# sur le calcul.
```

######## Article R822-10 | LEGIARTI000041419390

Lorsque le bénéficiaire est accédant à la propriété et qu'il est une personne
seule assumant une charge familiale telle que définie à l'article R. 823-4 ,
il est opéré sur ses ressources un abattement de :

1° 901 euros pour le bénéficiaire ayant une ou deux personnes à charge ;

2° 1 350 euros dès trois personnes à charge.

```catala
champ d'application RessourcesAidesPersonnelleLogement:
  définition abattement_r_822_10 égal à
    soit abattement égal à
      si nombre de personnes_à_charge = 0 alors 0 €
      sinon (si nombre de personnes_à_charge <= 2 alors 901 €
      sinon 1 350 €)
    dans selon situation_familiale sous forme
    -- Célibataire: abattement
    -- CélibataireSéparéDeFait: abattement
    -- n'importe quel: 0 €
```

######## Article R822-11 | LEGIARTI000041419384

Il n'est pas tenu compte, à partir du premier jour du mois civil suivant celui
au cours duquel survient l'événement ou le changement de situation, sous réserve
que la preuve en soit apportée :

1° Des ressources du conjoint du bénéficiaire :

a) Soit décédé ;

b) Soit absent du domicile, en raison d'une décision de justice prononçant le
divorce ou d'une convention de divorce par consentement mutuel conclue en
application de l' article 229-1 du code civil ;

c) Soit absent du domicile, en raison d'une décision de justice autorisant
la résidence séparée ou homologuant, en cas de demande conjointe en divorce,
la convention temporaire passée à ce sujet entre les époux ;

d) Soit absent du domicile, en raison d'une séparation de fait des époux ;

2° Des revenus d'activité professionnelle ou des indemnités de chômage perçus
par le conjoint du bénéficiaire :

a) Soit détenu, les ressources du conjoint étant toutefois prises en
considération s'il est placé sous le régime de la semi-liberté ;

b) Soit cessant toute activité professionnelle pour se consacrer à un enfant
de moins de trois ans ou à plusieurs enfants.

```catala
# Nous choissisons par manque de ressources (sic) de développement de laisser
# le calcul des ressources à l'utilisateur du programme qui devra suivre
# les instructions de cet article et de ceux auxquels il se réfère pour
# correctement évaluer leur montant.
```

######## Article R822-12 | LEGIARTI000041419379

Lorsque l'une des situations mentionnées à l'article R. 822-11 prend fin,
il est tenu compte :

1° Des ressources perçues par le conjoint du bénéficiaire à partir du premier
jour du mois au cours duquel la vie commune est reprise ;

2° Des revenus d'activité professionnelle ou des indemnités de chômage, à
partir du premier jour du mois au cours duquel :

a) Soit la période de détention expire ;
b) Soit les conditions relatives à l'âge ou au nombre d'enfants auxquels
l'intéressé se consacre ne sont plus remplies ;

c) Soit l'intéressé reprend une activité professionnelle.

```catala
# Nous choissisons par manque de ressources (sic) de développement de laisser
# le calcul des ressources à l'utilisateur du programme qui devra suivre
# les instructions de cet article et de ceux auxquels il se réfère pour
# correctement évaluer leur montant.
```

######## Article R822-13 | LEGIARTI000041419372

Lorsque le bénéficiaire ou son conjoint apporte la preuve de la cessation de
son activité professionnelle et de son admission au bénéfice d'une pension
de retraite ou d'invalidité ou d'une rente d'accident de travail ou de
l'allocation aux adultes handicapés, les revenus d'activité professionnelle
et les indemnités de chômage inclus dans les ressources de l'intéressé sont
affectés d'un abattement de 30 %.

Cette mesure s'applique à partir du premier jour du mois civil suivant celui
au cours duquel survient le changement de situation.

Lorsque le bénéficiaire ou son conjoint reprend une activité professionnelle
rémunérée, l'abattement prévu au premier alinéa est supprimé à partir du
premier jour du mois civil au cours duquel intervient la reprise d'activité.

NOTA :

Se reporter aux conditions d'application prévues à l'article 25 du décret
n° 2019-1574 du 30 décembre 2019. Conformément à l'article 1 du décret
n° 2020-451 du 20 avril 2020 l'article susvisé a été modifié : Ces
dispositions sont applicables au calcul des droits, d'une part, à l'aide
personnalisée au logement prévue pour les accédants à la propriété par
l'article L. 831-1 du code de la construction et de l'habitation, d'autre
part, aux autres aides personnelles au logement prévues par le même code,
à compter des mois respectifs fixés par arrêté des ministres en charge du
logement, de la santé et du budget et, au plus tard, du 1er janvier 2021.

Aux termes de l'article 2 du décret n° 2020-1816 du 29 décembre 2020,
l'article 25 du décret n° 2019-1574 du 30 décembre 2019 a été modifié :
Ces dispositions sont applicables au calcul des droits à l’aide personnelle
au logement à compter du mois de janvier 2021 ou, pour le calcul des droits
à l’aide personnalisée au logement prévue pour les accédants à la propriété
par l’article L. 831-1 du code de la construction et de l’habitation, à
compter du mois de mai 2021.

```catala
# Nous choissisons par manque de ressources (sic) de développement de laisser
# le calcul des ressources à l'utilisateur du programme qui devra suivre
# les instructions de cet article et de ceux auxquels il se réfère pour
# correctement évaluer leur montant.
```

######## Article R822-14 | LEGIARTI000041419358

Lorsque le bénéficiaire ou son conjoint se trouve, depuis au moins deux mois
consécutifs, à la date d'effet de la demande ou pendant au moins deux mois
consécutifs au cours de la période de paiement, en chômage total et qu'il
perçoit l'allocation d'assurance prévue à l'article L. 5422-1 du code du
travail ou lorsqu'il se trouve en chômage partiel et qu'il perçoit l'allocation
spécifique prévue à l'article L. 5122-1 du même code, ou perçoit l'allocation
des travailleurs indépendants mentionnée à l'article L. 5424-25 du même code,
les revenus d'activité professionnelle dont bénéficie l'intéressé sont affectés
d'un abattement de 30 %.

Cette mesure s'applique à partir du premier jour du deuxième mois civil suivant
celui au cours duquel est intervenu le changement de situation.

Le nombre minimal d'heures de chômage partiel requis pour bénéficier de cet
abattement de 30 % est de quarante heures sur une période de deux mois
consécutifs.

La rémunération dont bénéficient les personnes relevant des conventions conclues
en application de l'article L. 1233-68 du code du travail est assimilée,
pendant la durée de la formation et pour l'application des dispositions du
deuxième alinéa du présent article, à l'allocation de chômage à laquelle elle
s'est substituée lors de l'entrée en formation.

Lorsque l'intéressé reprend une activité professionnelle rémunérée,
l'abattement est supprimé à partir du premier jour du mois civil au cours
duquel intervient la reprise d'activité.

NOTA :

Se reporter aux conditions d'application prévues à l'article 25 du décret
n° 2019-1574 du 30 décembre 2019. Conformément à l'article 1 du décret
n° 2020-451 du 20 avril 2020 l'article susvisé a été modifié : Ces dispositions
sont applicables au calcul des droits, d'une part, à l'aide personnalisée au
logement prévue pour les accédants à la propriété par l'article L. 831-1 du
code de la construction et de l'habitation, d'autre part, aux autres aides
personnelles au logement prévues par le même code, à compter des mois respectifs
fixés par arrêté des ministres en charge du logement, de la santé et du budget
et, au plus tard, du 1er janvier 2021.

Aux termes de l'article 2 du décret n° 2020-1816 du 29 décembre 2020,
l'article 25 du décret n° 2019-1574 du 30 décembre 2019 a été modifié : Ces
dispositions sont applicables au calcul des droits à l’aide personnelle au
logement à compter du mois de janvier 2021 ou, pour le calcul des droits à
l’aide personnalisée au logement prévue pour les accédants à la propriété
par l’article L. 831-1 du code de la construction et de l’habitation, à compter
du mois de mai 2021.

```catala
# Nous choissisons par manque de ressources (sic) de développement de laisser
# le calcul des ressources à l'utilisateur du programme qui devra suivre
# les instructions de cet article et de ceux auxquels il se réfère pour
# correctement évaluer leur montant.
```

######## Article R822-15 | LEGIARTI000041419346

Il n'est tenu compte ni des revenus d'activité professionnelle, ni des
indemnités de chômage de l'intéressé lorsque celui-ci ou son conjoint est
en chômage total depuis au moins deux mois consécutifs à la date d'effet de
la demande ou pendant au moins deux mois consécutifs au cours de la période
de paiement et s'il se trouve dans l'une des situations suivantes :

1° Il ne bénéficie pas ou ne bénéficie plus d'une indemnisation dans les
conditions mentionnées par l'article R. 822-14 ;

2° Son indemnisation a atteint le montant minimum prévu par l'accord mentionné
à l' article L. 5422-20 du code du travail , après application du taux dégressif
prévu à l'article L. 5422-3 du même code ;

3° Il perçoit l'allocation de solidarité spécifique prévue par les articles
L. 5423-1 à L. 5423-3 du code du travail.

Les droits sont examinés sur cette nouvelle base à compter du premier jour du
mois civil suivant celui au cours duquel sont intervenus le changement de
situation, la cessation du versement ou la diminution du montant de l'allocation
d'assurance, ou l'admission à l'allocation de solidarité spécifique.

Lorsque l'intéressé reprend une activité professionnelle rémunérée, il est tenu
compte de ses ressources à partir du premier jour du mois civil au cours duquel
intervient la reprise d'activité.

NOTA :

Se reporter aux conditions d'application prévues à l'article 25 du décret
n° 2019-1574 du 30 décembre 2019. Conformément à l'article 1 du décret
n° 2020-451 du 20 avril 2020 l'article susvisé a été modifié : Ces dispositions
sont applicables au calcul des droits, d'une part, à l'aide personnalisée au
logement prévue pour les accédants à la propriété par l'article L. 831-1 du
code de la construction et de l'habitation, d'autre part, aux autres aides
personnelles au logement prévues par le même code, à compter des mois respectifs
fixés par arrêté des ministres en charge du logement, de la santé et du budget
et, au plus tard, du 1er janvier 2021.

Aux termes de l'article 2 du décret n° 2020-1816 du 29 décembre 2020, l'article
25 du décret n° 2019-1574 du 30 décembre 2019 a été modifié : Ces dispositions
sont applicables au calcul des droits à l’aide personnelle au logement à compter
du mois de janvier 2021 ou, pour le calcul des droits à l’aide personnalisée au
logement prévue pour les accédants à la propriété par l’article L. 831-1 du code
de la construction et de l’habitation, à compter du mois de mai 2021.

```catala
# Nous choissisons par manque de ressources (sic) de développement de laisser
# le calcul des ressources à l'utilisateur du programme qui devra suivre
# les instructions de cet article et de ceux auxquels il se réfère pour
# correctement évaluer leur montant.
```

######## Article R822-16 | LEGIARTI000041419340

Lorsque le bénéficiaire ou son conjoint justifie d'une interruption de travail
supérieure à six mois, dans les conditions mentionnées à l' article R. 324-1
du code de la sécurité sociale , il est procédé, à compter du premier jour du
mois civil suivant celui au cours duquel est intervenu le changement de
situation, à un abattement de 30 % sur les revenus d'activité professionnelle
et les indemnités de chômage perçus par l'intéressé.

Cette mesure s'applique jusqu'au dernier jour du mois civil précédant celui
au cours duquel la situation considérée prend fin.

NOTA :

Se reporter aux conditions d'application prévues à l'article 25 du décret
n° 2019-1574 du 30 décembre 2019. Conformément à l'article 1 du décret
n° 2020-451 du 20 avril 2020 l'article susvisé a été modifié : Ces dispositions
sont applicables au calcul des droits, d'une part, à l'aide personnalisée au
logement prévue pour les accédants à la propriété par l'article L. 831-1 du
code de la construction et de l'habitation, d'autre part, aux autres aides
personnelles au logement prévues par le même code, à compter des mois
respectifs fixés par arrêté des ministres en charge du logement, de la santé
et du budget et, au plus tard, du 1er janvier 2021.

Aux termes de l'article 2 du décret n° 2020-1816 du 29 décembre 2020,
l'article 25 du décret n° 2019-1574 du 30 décembre 2019 a été modifié :
Ces dispositions sont applicables au calcul des droits à l’aide personnelle
au logement à compter du mois de janvier 2021 ou, pour le calcul des droits à
l’aide personnalisée au logement prévue pour les accédants à la propriété par
l’article L. 831-1 du code de la construction et de l’habitation, à compter du
mois de mai 2021.

```catala
# Nous choissisons par manque de ressources (sic) de développement de laisser
# le calcul des ressources à l'utilisateur du programme qui devra suivre
# les instructions de cet article et de ceux auxquels il se réfère pour
# correctement évaluer leur montant.
```

######## Article R822-17 | LEGIARTI000041419333

Lorsque le bénéficiaire ou son conjoint perçoit le revenu de solidarité
active mentionné à l' article L. 262-2 du code de l'action sociale et des
familles , il n'est tenu compte ni des revenus d'activité professionnelle, ni
des indemnités de chômage dont bénéficie l'intéressé, à compter du premier
jour du mois civil suivant celui au cours duquel ces conditions sont réunies
et jusqu'au dernier jour du mois civil au cours duquel ces conditions cessent
d'être réunies.

NOTA :

Se reporter aux conditions d'application prévues à l'article 25 du décret
n° 2019-1574 du 30 décembre 2019. Conformément à l'article 1 du décret
n° 2020-451 du 20 avril 2020 l'article susvisé a été modifié : Ces dispositions
sont applicables au calcul des droits, d'une part, à l'aide personnalisée au
logement prévue pour les accédants à la propriété par l'article L. 831-1 du
code de la construction et de l'habitation, d'autre part, aux autres aides
personnelles au logement prévues par le même code, à compter des mois respectifs
fixés par arrêté des ministres en charge du logement, de la santé et du budget
et, au plus tard, du 1er janvier 2021.

Aux termes de l'article 2 du décret n° 2020-1816 du 29 décembre 2020,
l'article 25 du décret n° 2019-1574 du 30 décembre 2019 a été modifié :
Ces dispositions sont applicables au calcul des droits à l’aide personnelle
au logement à compter du mois de janvier 2021 ou, pour le calcul des droits
à l’aide personnalisée au logement prévue pour les accédants à la propriété
par l’article L. 831-1 du code de la construction et de l’habitation, à
compter du mois de mai 2021.

######## Article R822-18 | LEGIARTI000043616221

Lorsque le bénéficiaire, son conjoint ou une personne à charge est ou a été
titulaire d'un contrat de professionnalisation mentionné à l'article L. 6325-1
du code du travail, les revenus mensuels perçus par l'intéressé dans le cadre
de ce contrat sont diminués d'un abattement égal à leur montant sans pouvoir
excéder le montant mensuel du salaire minimum de croissance.

NOTA :

Se reporter aux conditions d'application prévues à l'article 3 du décret
n° 2021-720 du 4 juin 2021.

```catala
# Nous choissisons par manque de ressources (sic) de développement de laisser
# le calcul des ressources à l'utilisateur du programme qui devra suivre
# les instructions de cet article et de ceux auxquels il se réfère pour
# correctement évaluer leur montant.
```

####### Sous-section 3 : Montant forfaitaire de ressources applicable aux étudiants

######## Article R822-20 | LEGIARTI000041419188

Lorsque à la date de la demande de l'aide personnelle au logement ou du
réexamen du droit à cette aide, le demandeur ou l'allocataire occupe un
logement à usage locatif, qu'il satisfait les conditions d'âge fixées pour
l'attribution d'une bourse d'enseignement supérieur sur critères sociaux et
poursuit des études, les ressources du bénéficiaire ou du ménage sont réputées
égales à un montant forfaitaire.

Ce montant est minoré lorsque le demandeur ou l'allocataire est titulaire
d'une bourse de l'enseignement supérieur qui n'est pas assujettie à l'impôt
sur le revenu.

```catala
champ d'application RessourcesAidesPersonnelleLogement:
  exception définition ressources_prises_en_compte sous condition
     mode_occupation sous forme Locataire ou
     mode_occupation sous forme RésidentLogementFoyer et
     condition_âge_bourse_enseignement_supérieur et
     demandeur_poursuit_des_études
  conséquence égal à ressources_forfaitaires_r822_20

# Le montant minoré sera défini plus tard, et donc constituera une exception
# au cas de base de ressources_forfaitaires_r822_20
```

NOTA :

Se reporter aux conditions d'application prévues à l'article 25 du décret
n° 2019-1574 du 30 décembre 2019. Conformément à l'article 1 du décret
n° 2020-451 du 20 avril 2020 l'article susvisé a été modifié : Ces
dispositions sont applicables au calcul des droits, d'une part, à
l'aide personnalisée au logement prévue pour les accédants à la propriété
par l'article L. 831-1 du code de la construction et de l'habitation,
d'autre part, aux autres aides personnelles au logement prévues par le
même code, à compter des mois respectifs fixés par arrêté des ministres
en charge du logement, de la santé et du budget et, au plus tard, du 1er
janvier 2021.

Aux termes de l'article 2 du décret n° 2020-1816 du 29 décembre 2020,
l'article 25 du décret n° 2019-1574 du 30 décembre 2019 a été modifié :
Ces dispositions sont applicables au calcul des droits à l’aide personnelle
au logement à compter du mois de janvier 2021 ou, pour le calcul des droits
à l’aide personnalisée au logement prévue pour les accédants à la propriété
par l’article L. 831-1 du code de la construction et de l’habitation, à compter
du mois de mai 2021.

######## Article D822-21 | LEGIARTI000041407871

Les montants mentionnés à l'article R. 822-20 sont fixés par arrêté des ministres
chargés du logement, du budget, de la sécurité sociale et de l'agriculture.
Ils évoluent, le 1er janvier de chaque année, comme l'indice de référence des
loyers défini à l' article 17-1 de la loi n° 89-462 du 6 juillet 1989 tendant à
améliorer les rapports locatifs et portant modification de la loi n° 86-1290 du
23 décembre 1986 . Ils sont arrondis à la centaine d'euros la plus proche.

NOTA :

Se reporter aux conditions d'application prévues à l'article 25 du décret
n° 2019-1574 du 30 décembre 2019. Conformément à l'article 1 du décret
n° 2020-451 du 20 avril 2020 l'article susvisé a été modifié : Ces dispositions
sont applicables au calcul des droits, d'une part, à l'aide personnalisée au
logement prévue pour les accédants à la propriété par l'article L. 831-1 du code
de la construction et de l'habitation, d'autre part, aux autres aides personnelles
au logement prévues par le même code, à compter des mois respectifs fixés par
arrêté des ministres en charge du logement, de la santé et du budget et, au plus
tard, du 1er janvier 2021.

Aux termes de l'article 2 du décret n° 2020-1816 du 29 décembre 2020, l'article
25 du décret n° 2019-1574 du 30 décembre 2019 a été modifié : Ces dispositions
sont applicables au calcul des droits à l’aide personnelle au logement à compter
du mois de janvier 2021 ou, pour le calcul des droits à l’aide personnalisée au
logement prévue pour les accédants à la propriété par l’article L. 831-1 du code
de la construction et de l’habitation, à compter du mois de mai 2021.

```catala
# Cet article régit la manière dont les montants forfaitaires de ressources
# évoluent d'année en année mais nous ne cherchons pas à formaliser cela
# dans ce calculateur.
```

####### Sous-section 4 : Prise en compte du patrimoine

######## Article R822-22 | LEGIARTI000041419223

Le montant prévu au premier alinéa de l'article L. 822-5 est fixé à 30 000 euros.

```catala
champ d'application ÉligibilitéAidesPersonnelleLogement:
  définition seuil_l822_5_patrimoine égal à 30 000 €
```

Il est appliqué à la somme de la valeur du patrimoine mobilier financier et de
la valeur estimée de l'ensemble du patrimoine immobilier, à l'exception de la
résidence principale et des biens à usage professionnel.

Lorsque la valeur du patrimoine est supérieure à 30 000 euros, seul le patrimoine
n'ayant pas produit, au cours de la période de référence définie au 3° de
l'article R. 822-3, de revenus retenus pour l'établissement de l'impôt sur
le revenu au titre des revenus nets catégoriels mentionnés à l'article
R. 822-4 est pris en compte pour le calcul de l'aide.

```catala
champ d'application ÉligibilitéAidesPersonnelleLogement:
  étiquette r822_22_3 exception l822_5_1
  définition patrimoine_pris_en_compte sous condition
    patrimoine_total_demandeur >=€ seuil_l822_5_patrimoine
  conséquence égal à
    demandeur.patrimoine.ne_produisant_pas_revenu_période_r822_3_3_r822_4
```

Ce patrimoine est considéré comme procurant un revenu annuel égal à 50 % de
sa valeur locative s'il s'agit d'immeubles bâtis, à 80 % de cette valeur s'il
s'agit de terrains non bâtis et à 3 % du montant des capitaux.

La valeur estimée du patrimoine est déterminée sur la base de la dernière valeur
déclarée par l'allocataire, qui est tenu de porter à la connaissance du service
instructeur les changements substantiels de sa situation patrimoniale.

La dernière valeur déclarée s'entend :

1° Pour le patrimoine financier, de la valeur figurant sur les derniers relevés
bancaires reçus par le bénéficiaire de l'aide personnelle au logement ;

2° Pour le patrimoine immobilier, de la valeur locative figurant sur le dernier
avis d'imposition à la taxe d'habitation ou à la taxe foncière reçu par le
bénéficiaire.

```catala
# Nous choissisons par manque de ressources (sic) de développement de laisser
# le calcul des ressources à l'utilisateur du programme qui devra suivre
# les instructions de cet alinéa et des précédents et de ceux auxquels il
# se réfère pour correctement évaluer leur montant.
```

NOTA :

Se reporter aux conditions d'application prévues à l'article 25 du décret
n° 2019-1574 du 30 décembre 2019. Conformément à l'article 1 du décret
n° 2020-451 du 20 avril 2020 l'article susvisé a été modifié : Ces dispositions
sont applicables au calcul des droits, d'une part, à l'aide personnalisée au
logement prévue pour les accédants à la propriété par l'article L. 831-1 du
code de la construction et de l'habitation, d'autre part, aux autres aides
personnelles au logement prévues par le même code, à compter des mois respectifs
fixés par arrêté des ministres en charge du logement, de la santé et du budget
et, au plus tard, du 1er janvier 2021.

Aux termes de l'article 2 du décret n° 2020-1816 du 29 décembre 2020, l'article
25 du décret n° 2019-1574 du 30 décembre 2019 a été modifié : Ces dispositions
sont applicables au calcul des droits à l’aide personnelle au logement à compter
du mois de janvier 2021 ou, pour le calcul des droits à l’aide personnalisée au
logement prévue pour les accédants à la propriété par l’article L. 831-1 du code
de la construction et de l’habitation, à compter du mois de mai 2021.

###### Section 3 : Conditions relatives au logement

####### Article R822-23 | LEGIARTI000038878949

Est considéré comme résidence principale, pour l'application du premier alinéa
du II de l'article L. 822-2 , le logement effectivement occupé soit par le
bénéficiaire de l'aide personnelle au logement, soit par son conjoint, soit
par une des personnes à charge au sens de l'article R. 823-4 , au moins huit
mois par an, sauf obligation professionnelle, raison de santé ou cas de force
majeure.

```catala
# Nous avons déjà asserté à l'article L821-2 qu'il fallait que le logement
# soit la résidence principale du ménage. Les conditions de détermination
# de la résidence principale dans cet article ne sont pas très intéressantes à
# formaliser, donc nous choissisons de ne pas les inclure dans le programme.
# Par contre il serait utile de les rappeler dans l'interface du formulaire
# de déclaration.
```

####### Article R822-24 | LEGIARTI000038878947


Le logement au titre duquel le droit à l'aide personnelle au logement est ouvert
doit répondre aux caractéristiques de décence définies par le décret n° 2002-120
du 30 janvier 2002 relatif aux caractéristiques du logement décent.

```catala
# Décence du logement déjà prise en compte dans L822-9
```

####### Article R822-25 | LEGIARTI000038878945

Le logement au titre duquel le droit à l'aide personnelle au logement est
ouvert doit présenter une surface habitable globale au moins égale à neuf
mètres carrés pour une personne seule, seize mètres carrés pour un ménage
sans enfant ou deux personnes, augmentée de neuf mètres carrés par personne
en plus, dans la limite de soixante-dix mètres carrés pour huit personnes et
plus.

```catala
champ d'application ÉligibilitéAidesPersonnelleLogement:
  règle condition_logement_surface sous condition
    soit nombre_personnes_logement égal à
      (selon ménage.situation_familiale sous forme
      -- Célibataire: 1
      -- Mariés: 2
      -- Pacsés: 2
      -- Concubins: 2
      -- CélibataireSéparéDeFait: 1
      -- ConcubinageDontSéparéDeFait: 2) +
      ménage.nombre_autres_occupants_logement +
      nombre de ménage.personnes_à_charge
    dans
    soit condition_logement_surface_minimale_sans_seuil_m_carrés égal à
      (selon ménage.situation_familiale sous forme
      -- Célibataire: 9
      -- Mariés: 16
      -- Pacsés: 16
      -- Concubins: 16
      -- CélibataireSéparéDeFait: 9
      -- ConcubinageDontSéparéDeFait: 16) +
      (ménage.nombre_autres_occupants_logement +
      nombre de ménage.personnes_à_charge) * 9
    dans si
      condition_logement_surface_minimale_sans_seuil_m_carrés >= 70 et
      nombre_personnes_logement >= 8
    alors
      (ménage.logement.surface_m_carrés >= 70)
    sinon
      (ménage.logement.surface_m_carrés >=
        condition_logement_surface_minimale_sans_seuil_m_carrés)
  conséquence rempli
```

##### Chapitre III : Modalités de liquidation et de versement

###### Section 1 : Calcul, liquidation et versement des aides

####### Article R823-1 | LEGIARTI000038878939

Les aides personnelles au logement et les primes de déménagement sont liquidées
et payées par la caisse d'allocations familiales compétente au regard de
la résidence du bénéficiaire ou, lorsque le bénéficiaire relève d'un régime
de protection sociale des professions agricoles, par la caisse de la mutualité
sociale agricole compétente.

```catala
# Pas pertinent pour le calcul du montant.
```

####### Article R823-2 | LEGIARTI000038878937

Les aides personnelles au logement sont attribuées sur la demande de l'intéressé
déposée auprès de l'organisme payeur mentionné à l'article R. 823-1 dont il
relève. Cette demande est conforme à un modèle type.

Elle est assortie de pièces justificatives définies par arrêté des ministres
chargés du logement, du budget, de la sécurité sociale et de l'agriculture.
Le même arrêté définit le modèle-type de la demande et précise celles de ces
pièces justificatives qui doivent être produites chaque année et, parmi
celles-ci, celles dont le défaut de présentation avant la date qu'il fixe
entraîne la suspension du paiement de l'aide.

Le fait que le logement réponde aux conditions de décence mentionnées à l'article
R. 822-24 est justifié par une attestation du bailleur.

La personne de nationalité étrangère qui demande à bénéficier des aides
personnelles au logement justifie, en outre, de la régularité de son séjour
par la production d'un des titres de séjour ou documents prévus à l' article
D. 512-1 du code de la sécurité sociale .

```catala
# Pas pertinent pour le calcul du montant.
```

####### Article R823-3 | LEGIARTI000038878935

Les changements survenus, au cours de la période de paiement de l'aide, dans
la situation du bénéficiaire ou du ménage font l'objet de justifications
fournies avec la demande de révision du montant de l'aide.

```catala
# Pas pertinent pour le calcul du montant.
```


####### Article R823-4 | LEGIARTI000038878933

Sont considérés comme personnes à charge, sous réserve qu'ils vivent
habituellement au foyer :

```catala
champ d'application ÉligibilitéAidesPersonnelleLogement:
  définition personnes_à_charge_prises_en_compte égal à
    filtre pour personne_à_charge dans ménage.personnes_à_charge de
      prise_en_compte_personne_à_charge de personne_à_charge
```

1° Les enfants de moins de vingt et un ans et considérés comme à charge au
sens des 1° et 2° de l'article L. 512-3 du code de la sécurité sociale et de
l'article L. 823-2 du présent code ;

```catala
champ d'application ÉligibilitéAidesPersonnelleLogement:
  règle prise_en_compte_personne_à_charge de personne_à_charge sous condition
    selon personne_à_charge sous forme
    -- EnfantÀCharge de enfant:
      enfant.date_de_naissance +@ 21 an >@ date_courante
    -- AutrePersonneÀCharge de parent: faux
  conséquence rempli
```

2° Les ascendants du bénéficiaire ou de son conjoint dont les ressources
déterminées dans les conditions prévues aux articles R. 822-3 à R. 822-6
n'excèdent pas le plafond individuel prévu à l' article L. 815-9 du code de
la sécurité sociale , en vigueur au 31 décembre de l'année de référence
multiplié par 1,25 :

a) Ayant au moins l'âge prévu par le 1° de l'article L. 351-8 du code de la
sécurité sociale ou, s'ils sont titulaires de l'allocation de solidarité aux
personnes âgées, soixante-cinq ans ;

b) Ayant au moins l'âge prévu par l' article L. 161-17-2 du code de la sécurité
sociale et bénéficiaires des articles L. 161-19 , L. 351-8 ou L. 643-3 du même
code ;

```catala
champ d'application ÉligibilitéAidesPersonnelleLogement:
  règle condition_2_r823_4 de personne_à_charge sous condition
    selon personne_à_charge sous forme
      -- EnfantÀCharge de enfant: faux
      -- AutrePersonneÀCharge de parent:
        parent.parenté = Ascendant et
        parent.ressources <=€
          plafond_individuel_l815_9_sécu *€ 1,25 et
        (
          (parent.date_naissance +@
            âge_l351_8_1_sécu <=@ date_courante ou
              (parent.titulaire_allocation_personne_âgée et
                parent.date_naissance +@ 65 an <=@
                  date_courante)
          ) ou
          (parent.date_naissance +@
            âge_l161_17_2_sécu <=@ date_courante et
              parent.bénéficiaire_l161_19_l351_8_l643_3_sécu)
        )
  conséquence rempli

  étiquette r823_4_2
  règle prise_en_compte_personne_à_charge de personne_à_charge sous condition
    condition_2_r823_4 de personne_à_charge
  conséquence rempli
```

3° Les ascendants, descendants ou collatéraux au deuxième ou au troisième degré
du bénéficiaire ou de son conjoint dont l'incapacité permanente est au moins
égale à 80 % ou qui présentent, compte tenu de leur handicap, une restriction
substantielle et durable dans l'accès à l'emploi au sens de l' article L. 821-2
du code de la sécurité sociale reconnue par la commission des droits et de
l'autonomie des personnes handicapées prévue par l' article L. 146-9 du code
de l'action sociale et des familles , et dont les ressources déterminées dans
les conditions prévues aux articles R. 822-3 à R. 822-6 n'excèdent pas le plafond
individuel prévu à l'article L. 815-9 du code de la sécurité sociale en vigueur
au 31 décembre de l'année de référence multiplié par 1,25.

```catala
champ d'application ÉligibilitéAidesPersonnelleLogement:
  exception r823_4_2
  règle prise_en_compte_personne_à_charge de personne_à_charge sous condition
   selon personne_à_charge sous forme
      -- EnfantÀCharge de enfant: faux
      -- AutrePersonneÀCharge de parent:
        # Pas besoin de préciser parenté car inclusif ici
        parent.incapacité_80_pourcent_ou_restriction_emploi et
        parent.ressources <=€
          plafond_individuel_l815_9_sécu *€ 1,25
  conséquence rempli
```

####### Article R823-5 | LEGIARTI000038878931

Pour l'application de l'article L. 823-2 , en cas de résidence alternée, les
modalités de prise en compte de l'enfant à charge pour le calcul de l'aide
ne peuvent être remises en cause par les parents qu'au bout d'un an, sauf
modification, avant cette échéance, des modalités de résidence de l'enfant.

```catala
# Cet article exprime un délai durant lequel le calcul de l'aide, dans le cadre
# d'une résidence alternée, ne peut être remis en cause. Il pose également une
# exception : la modification des modalités de résidence de l'enfant
# avant échéance.
```

####### Article R823-6 | LEGIARTI000041419232

Le montant mensuel de l'aide personnelle au logement est calculé pour une
période de trois mois à compter de la date à laquelle les conditions
d'ouverture du droit à cette aide sont réunies, sous réserve des cas prévus
aux articles R. 822-7 à R. 822-17 , R. 823-7 , R. 823-10 à R. 823-14 ainsi
que, le cas échéant, R. 832-9 .

Il est calculé sur la base du loyer effectivement payé pour le mois de
juillet de l'année précédente ou, en cas d'accession à la propriété, sur
la base de la mensualité acquittée au titre des charges.

Le droit à l'aide personnelle au logement et son montant mensuel sont
réexaminés tous les trois mois sous réserve des cas prévus au premier
alinéa, y compris, pendant une période qui ne peut dépasser neuf mois
consécutifs, à la suite d'un réexamen aboutissant à un versement nul ou
inférieur au seuil de versement, sans qu'il soit nécessaire à l'allocataire
de déposer une nouvelle demande d'aide.

A l'expiration du délai de neuf mois, une nouvelle demande d'aide doit être
déposée pour qu'il soit procédé à un nouvel examen du droit et que l'aide
puisse être à nouveau versée.

```catala
# Les délais d'examen des demandes définis dans cet article ne sont pas
# nécessairement pertinents à formaliser pour le moment.
# En revanche, l'alinéa 2 qui définit une base de calcul fixe pour deux cas:
# le loyer du mois de juillet de l'année précédente pour la location et la
# mensualité acquittée au titre des charges pour la propriété peut être
# intéressant à traiter (peut-être complexité au niveau du renvoi à un mois
# et non pas une date précise au niveau du calcul de jours ?).
```

NOTA :

Se reporter aux conditions d'application prévues à l'article 25 du décret
n° 2019-1574 du 30 décembre 2019. Conformément à l'article 1 du décret n°
2020-451 du 20 avril 2020 l'article susvisé a été modifié : Ces dispositions
sont applicables au calcul des droits, d'une part, à l'aide personnalisée au
logement prévue pour les accédants à la propriété par l'article L. 831-1 du
code de la construction et de l'habitation, d'autre part, aux autres aides
personnelles au logement prévues par le même code, à compter des mois
respectifs fixés par arrêté des ministres en charge du logement, de la santé
et du budget et, au plus tard, du 1er janvier 2021.

Aux termes de l'article 2 du décret n° 2020-1816 du 29 décembre 2020,
l'article 25 du décret n° 2019-1574 du 30 décembre 2019 a été modifié :
Ces dispositions sont applicables au calcul des droits à l’aide personnelle
au logement à compter du mois de janvier 2021 ou, pour le calcul des droits
à l’aide personnalisée au logement prévue pour les accédants à la propriété
par l’article L. 831-1 du code de la construction et de l’habitation,
à compter du mois de mai 2021.

####### Article R823-6-1 | LEGIARTI000042932258

Lorsqu'au moment du réexamen trimestriel du droit à l'aide personnelle au
logement il est constaté que le bénéficiaire perçoit également le revenu
de solidarité active défini à l'article L. 262-2 du code de l'action sociale
et des familles, ou la prime d'activité définie à l'article L. 841-1 du code
de la sécurité sociale ou l'allocation aux adultes handicapés définie à
l'article L. 821-1 du même code, l'échéance trimestrielle de son droit à l'aide
personnelle au logement est avancée pour coïncider avec le prochain réexamen
trimestriel de celle de ces aides dont il bénéficie.

La réduction de la durée de la période mentionnée au premier alinéa s'applique
également lorsque le conjoint du bénéficiaire ou tout autre membre du foyer a
droit à une prestation attribuée sous une condition de ressources calculée
trimestriellement.

Cet alignement de la période de droit à l'aide s'effectue par ordre de priorité
sur le revenu de solidarité active, la prime d'activité et l'allocation aux
adultes handicapés.

```catala
# Pas pertinent pour le moment.
```

NOTA :

Se reporter aux conditions d'application prévues à l'article 25 du décret
n° 2019-1574 du 30 décembre 2019. Conformément à l'article 1 du décret
n° 2020-451 du 20 avril 2020 l'article susvisé a été modifié : Ces dispositions
sont applicables au calcul des droits, d'une part, à l'aide personnalisée au
logement prévue pour les accédants à la propriété par l'article L. 831-1 du
code de la construction et de l'habitation, d'autre part, aux autres aides
personnelles au logement prévues par le même code, à compter des mois respectifs
fixés par arrêté des ministres en charge du logement, de la santé et du budget et,
au plus tard, du 1er janvier 2021.

Aux termes de l'article 2 du décret n° 2020-1816 du 29 décembre 2020, l'article 25
du décret n° 2019-1574 du 30 décembre 2019 a été modifié : Ces dispositions sont
applicables au calcul des droits à l’aide personnelle au logement à compter du
mois de janvier 2021 ou, pour le calcul des droits à l’aide personnalisée au
logement prévue pour les accédants à la propriété par l’article L. 831-1 du
code de la construction et de l’habitation, à compter du mois de mai 2021.

####### Article R823-7 | LEGIARTI000038878927

Lorsque le bénéficiaire s'installe dans un nouveau logement au cours de la
période de paiement, le montant de l'aide personnelle au logement est révisé
sur la base du loyer principal effectivement payé pour le premier mois de
location du nouveau logement.

Indépendamment de tout changement de logement, le montant de l'aide personnalisée
au logement est révisé en cours de période de paiement, lorsqu'en application
d'un avenant à la convention, un nouveau loyer est notifié au bénéficiaire.

```catala
# Il peut être intéressant de définir comment, en cas de loyer différent
# (dû à un déménagement ou à un changement de prix), le calcul évolue.
```

####### Article R823-8 | LEGIARTI000038878925

Les aides personnelles au logement sont versées mensuellement à terme échu,
dans les conditions définies par les conventions mentionnées à l'article L. 812-2 .

```catala
# Pas pertinent pour le moment.
```

####### Article D823-9 | LEGIARTI000038878923

Les modalités de liquidation et de versement des aides personnelles au
logement sont fixées :

1° Pour les ménages occupant un logement dont ils sont locataires ou
sous-locataires, y compris un logement dans une résidence universitaire
définie à l'article L. 631-12 et dans une résidence-service définie à
l'article L. 631-13, par les règles communes figurant aux articles
D. 823-16 à D. 823-19, et, en outre, pour les allocations de logement,
par les règles particulières figurant aux articles D. 842-1 à D. 842-4 ;

2° Pour les ménages résidant dans un logement-foyer défini à l'article
L. 633-1 et conventionné en application du 5° de l'article L. 831-1 , par
les règles figurant aux articles R. 832-20 à R. 832-23 et D. 832-24 à
D. 832-28 ;

3° Pour les ménages résidant dans un logement-foyer défini à l'article
L. 633-1 autre que celui mentionné au 2° du présent article et dans les
chambres des résidences gérées par les centres régionaux des œuvres
universitaires et scolaires mentionnées au 1° de l'article R. 822-29 du
code de l'éducation, assimilées, au sens et pour l'application du titre
IV du présent livre, à des logements-foyers, par les règles figurant
aux articles R. 842-14 et D. 842-15 à D. 842-18 du présent code ;

4° Pour les ménages propriétaires occupant un logement relevant du 1°
de l'article L. 831-1 et les occupants titulaires de contrats de
location-accession relevant du 6° du même article, par les règles
figurant aux articles R. 832-5 à R. 832-9 et D. 832-10 à D. 832-19 ;

5° Pour les autres ménages occupant un logement dont ils sont
propriétaires, ou pour lequel ils sont titulaires d'un contrat de
location-accession, par les règles figurant aux articles R. 842-5
et D. 842-6 à D. 842-13 .

```catala
champ d'application CalculAllocationLogement:
  définition catégorie_calcul_apl égal à
    selon mode_occupation sous forme
    -- Locataire de location: Location contenu location
    -- SousLocataire de location: Location contenu location
    -- RésidentLogementFoyer de logementfoyer:
      CatégorieCalculAPL.LogementFoyer contenu logementfoyer
    -- AccessionPropriétéLocalUsageExclusifHabitation de propriétaire:
      AccessionPropriété contenu propriétaire
    -- LocationAccession de propriétaire:
      AccessionPropriété contenu propriétaire

  définition aide_finale_formule égal à
    sous_calcul_traitement.aide_finale_formule
  définition traitement_aide_finale de arg égal à
    (sous_calcul_traitement.
      Traitement_formule_aide_finale.traitement_aide_finale de arg)

champ d'application CalculAidePersonnaliséeLogement:
  définition catégorie_calcul_apl égal à
    selon mode_occupation sous forme
    -- Locataire de location: Location contenu location
    -- SousLocataire de location: Location contenu location
    -- RésidentLogementFoyer de logementfoyer:
      CatégorieCalculAPL.LogementFoyer contenu logementfoyer
    -- AccessionPropriétéLocalUsageExclusifHabitation de propriétaire:
      AccessionPropriété contenu propriétaire
    -- LocationAccession de propriétaire:
      AccessionPropriété contenu propriétaire

  définition aide_finale_formule égal à
    sous_calcul_traitement.aide_finale_formule
  définition traitement_aide_finale de arg égal à
    (sous_calcul_traitement.
      Traitement_formule_aide_finale.traitement_aide_finale de arg)
```

```catala-metadata
  # Maintenant que nous avons déterminé comment calculer l'aide finale
  # en fonction du mode d'occupation, il nous faut donner à chacun des modes
  # de calcul les paramètres qu'il attend.
champ d'application CalculAidePersonnaliséeLogement:
  définition sous_calcul_traitement égal à
    selon catégorie_calcul_apl sous forme
    -- Location de location:
      (soit traitement_formule égal à
        CalculAidePersonnaliséeLogementLocatif de {
          -- ressources_ménage_arrondies: ressources_ménage
          -- nombre_personnes_à_charge: nombre_personnes_à_charge
          -- zone: zone
          -- date_courante: date_courante
          -- situation_familiale_calcul_apl: situation_familiale_calcul_apl
          -- loyer_principal: location.loyer_principal
          -- bénéficiaire_aide_adulte_ou_enfant_handicapés:
              location.bénéficiaire_aide_adulte_ou_enfant_handicapés
          -- logement_est_chambre: location.logement_est_chambre
          -- colocation: location.colocation
          -- âgées_ou_handicap_adultes_hébergées_onéreux_particuliers:
              location.âgées_ou_handicap_adultes_hébergées_onéreux_particuliers
          -- type_aide: type_aide
          -- réduction_loyer_solidarité:
              (selon location.bailleur sous forme
                 -- BailleurSocial de bailleur:
                     bailleur.réduction_loyer_solidarité_perçue
                 -- n'importe quel: 0€)
          -- logement_meublé_d842_2: location.logement_meublé_d842_2
        }
      dans Traitement_formule_aide_finale {
        -- aide_finale_formule:
          traitement_formule.
            CalculAidePersonnaliséeLogementLocatif.aide_finale_formule
        -- traitement_aide_finale:
          traitement_formule.
            CalculAidePersonnaliséeLogementLocatif.traitement_aide_finale
      })
    -- LogementFoyer de logement_foyer_:
      (soit traitement_formule égal à
        CalculAidePersonnaliséeLogementFoyer de {
          -- ressources_ménage_arrondies: ressources_ménage
          -- nombre_personnes_à_charge: nombre_personnes_à_charge
          -- zone: zone
          -- date_courante: date_courante
          -- situation_familiale_calcul_apl: situation_familiale_calcul_apl
          -- redevance: logement_foyer_.redevance
          -- type_logement_foyer: logement_foyer_.type
          -- date_conventionnement: logement_foyer_.date_conventionnement
        }
      dans Traitement_formule_aide_finale {
        -- aide_finale_formule:
          traitement_formule.
            CalculAidePersonnaliséeLogementFoyer.aide_finale_formule
        -- traitement_aide_finale:
          traitement_formule.
            CalculAidePersonnaliséeLogementFoyer.traitement_aide_finale
      })
    -- AccessionPropriété de propriétaire:
      (soit traitement_formule égal à
         CalculAidePersonnaliséeLogementAccessionPropriété de {
           -- ressources_ménage_arrondies: ressources_ménage
           -- nombre_personnes_à_charge: nombre_personnes_à_charge
           -- zone: zone
           -- date_courante: date_courante
           -- situation_familiale_calcul_apl: situation_familiale_calcul_apl
           -- mensualité_principale: propriétaire.mensualité_principale
           -- type_travaux_logement: propriétaire.type_travaux_logement_d832_15
           -- date_signature_prêt: propriétaire.prêt.date_signature
           -- local_habité_première_fois_bénéficiaire:
             propriétaire.local_habité_première_fois_bénéficiaire
           -- date_entrée_logement: propriétaire.date_entrée_logement
           -- copropriété: propriétaire.copropriété
           -- situation_r822_11_13_17: propriétaire.situation_r822_11_13_17
           -- type_prêt: propriétaire.prêt.type_prêt
           -- ancienneté_logement: propriétaire.ancienneté_logement
         }
      dans Traitement_formule_aide_finale {
        -- aide_finale_formule:
          traitement_formule.
            CalculAidePersonnaliséeLogementAccessionPropriété.
              aide_finale_formule
        -- traitement_aide_finale:
          traitement_formule.
            CalculAidePersonnaliséeLogementAccessionPropriété.
              traitement_aide_finale
      })

champ d'application CalculAllocationLogement:
  définition sous_calcul_traitement égal à
    selon mode_occupation sous forme
    -- Locataire de location:
      (soit traitement_formule égal à
        CalculAllocationLogementLocatif de {
          -- ressources_ménage_arrondies: ressources_ménage
          -- nombre_personnes_à_charge: nombre_personnes_à_charge
          -- zone: zone
          -- date_courante: date_courante
          -- situation_familiale_calcul_apl: situation_familiale_calcul_apl
          -- loyer_principal: location.loyer_principal
          -- bénéficiaire_aide_adulte_ou_enfant_handicapés:
              location.bénéficiaire_aide_adulte_ou_enfant_handicapés
          -- logement_est_chambre: location.logement_est_chambre
          -- colocation: location.colocation
          -- âgées_ou_handicap_adultes_hébergées_onéreux_particuliers:
              location.âgées_ou_handicap_adultes_hébergées_onéreux_particuliers
          -- type_aide: type_aide
          -- réduction_loyer_solidarité:
              (selon location.bailleur sous forme
                 -- BailleurSocial de bailleur:
                     bailleur.réduction_loyer_solidarité_perçue
                 -- n'importe quel: 0€)
          -- logement_meublé_d842_2: location.logement_meublé_d842_2
          -- changement_logement_d842_4: location.changement_logement_d842_4
        }
      dans Traitement_formule_aide_finale {
        -- aide_finale_formule:
          traitement_formule.aide_finale_formule
        -- traitement_aide_finale:
          traitement_formule.
            CalculAllocationLogementLocatif.
              traitement_aide_finale
      })
    -- SousLocataire de location: # Identique à Locataire
      (soit traitement_formule égal à
        CalculAllocationLogementLocatif de {
          -- ressources_ménage_arrondies: ressources_ménage
          -- nombre_personnes_à_charge: nombre_personnes_à_charge
          -- zone: zone
          -- date_courante: date_courante
          -- situation_familiale_calcul_apl: situation_familiale_calcul_apl
          -- loyer_principal: location.loyer_principal
          -- bénéficiaire_aide_adulte_ou_enfant_handicapés:
              location.bénéficiaire_aide_adulte_ou_enfant_handicapés
          -- logement_est_chambre: location.logement_est_chambre
          -- colocation: location.colocation
          -- âgées_ou_handicap_adultes_hébergées_onéreux_particuliers:
              location.âgées_ou_handicap_adultes_hébergées_onéreux_particuliers
          -- type_aide: type_aide
          -- réduction_loyer_solidarité:
              (selon location.bailleur sous forme
                 -- BailleurSocial de bailleur:
                     bailleur.réduction_loyer_solidarité_perçue
                 -- n'importe quel: 0€)
          -- logement_meublé_d842_2: location.logement_meublé_d842_2
          -- changement_logement_d842_4: location.changement_logement_d842_4
        }
      dans Traitement_formule_aide_finale {
        -- aide_finale_formule:
          traitement_formule.
            CalculAllocationLogementLocatif.aide_finale_formule
        -- traitement_aide_finale:
          traitement_formule.
            CalculAllocationLogementLocatif.traitement_aide_finale
      })
    -- RésidentLogementFoyer de logement_foyer_:
      (soit traitement_formule égal à
        CalculAllocationLogementFoyer de {
          -- ressources_ménage_arrondies: ressources_ménage
          -- nombre_personnes_à_charge: nombre_personnes_à_charge
          -- zone: zone
          -- date_courante: date_courante
          -- situation_familiale_calcul_apl: situation_familiale_calcul_apl
          -- redevance: logement_foyer_.redevance
          -- catégorie_équivalence_loyer_d842_16:
            logement_foyer_.catégorie_équivalence_loyer_d842_16
          -- type_logement_foyer: logement_foyer_.type
          -- date_conventionnement: logement_foyer_.date_conventionnement
        }
      dans Traitement_formule_aide_finale {
        -- aide_finale_formule:
          traitement_formule.aide_finale_formule
        -- traitement_aide_finale:
          traitement_formule.
            CalculAllocationLogementFoyer.traitement_aide_finale
      })
    -- AccessionPropriétéLocalUsageExclusifHabitation de propriétaire:
      (soit traitement_formule égal à
         CalculAllocationLogementAccessionPropriété de {
           -- ressources_ménage_arrondies: ressources_ménage
           -- nombre_personnes_à_charge: nombre_personnes_à_charge
           -- zone: zone
           -- date_courante: date_courante
           -- situation_familiale_calcul_apl: situation_familiale_calcul_apl
           -- mensualité_principale: propriétaire.mensualité_principale
           -- charges_mensuelles_prêt: propriétaire.charges_mensuelles_prêt
           -- type_travaux_logement: propriétaire.type_travaux_logement_r842_5
           -- date_signature_prêt: propriétaire.prêt.date_signature
           -- local_habité_première_fois_bénéficiaire:
             propriétaire.local_habité_première_fois_bénéficiaire
           -- date_entrée_logement: propriétaire.date_entrée_logement
           -- copropriété: propriétaire.copropriété
           -- situation_r822_11_13_17: propriétaire.situation_r822_11_13_17
         }
      dans Traitement_formule_aide_finale {
        -- aide_finale_formule:
          traitement_formule.
            CalculAllocationLogementAccessionPropriété.aide_finale_formule
        -- traitement_aide_finale:
          traitement_formule.
            CalculAllocationLogementAccessionPropriété.traitement_aide_finale
      })
    -- LocationAccession de propriétaire:
      # Identique à AccessionPropriétéLocalUsageExclusifHabitation
      (soit traitement_formule égal à
         CalculAllocationLogementAccessionPropriété de {
           -- ressources_ménage_arrondies: ressources_ménage
           -- nombre_personnes_à_charge: nombre_personnes_à_charge
           -- zone: zone
           -- date_courante: date_courante
           -- situation_familiale_calcul_apl: situation_familiale_calcul_apl
           -- mensualité_principale: propriétaire.mensualité_principale
           -- charges_mensuelles_prêt: propriétaire.charges_mensuelles_prêt
           -- type_travaux_logement: propriétaire.type_travaux_logement_r842_5
           -- date_signature_prêt: propriétaire.prêt.date_signature
           -- local_habité_première_fois_bénéficiaire:
             propriétaire.local_habité_première_fois_bénéficiaire
           -- date_entrée_logement: propriétaire.date_entrée_logement
           -- copropriété: propriétaire.copropriété
           -- situation_r822_11_13_17: propriétaire.situation_r822_11_13_17
         }
      dans Traitement_formule_aide_finale {
        -- aide_finale_formule:
          traitement_formule.
            CalculAllocationLogementAccessionPropriété.aide_finale_formule
        -- traitement_aide_finale:
          traitement_formule.
            CalculAllocationLogementAccessionPropriété.traitement_aide_finale
      })
```

####### Sous-section 1 : Ouverture et extinction des droits

######## Article R823-10 | LEGIARTI000038878919

Les aides personnelles au logement sont dues à compter du premier
jour du mois civil suivant celui au cours duquel les conditions
d'ouverture du droit sont réunies.

Toutefois, lorsque ces conditions sont réunies antérieurement au
mois de la demande, l'aide est due à compter du premier jour du
mois au cours duquel la demande est déposée.

```catala
# Pas pertinent pour le moment.
```

######## Article R823-11 | LEGIARTI000038878917

Par dérogation à l'article R. 823-10 , l'aide est due à compter du premier
jour du mois civil au cours duquel les conditions d'ouverture du droit
sont réunies :

1° Aux personnes qui, hébergées par un organisme logeant à titre temporaire
des personnes défavorisées ou par une association agréée en application de
l' article L. 121-9 du code de l'action sociale et des familles et bénéficiant
de l'aide mentionnée à l' article L. 851-1 du code de la sécurité sociale ,
accèdent à un logement ouvrant droit à l'une des aides personnelles au logement ;

2° Aux personnes dont le logement a fait l'objet d'un arrêté d'insalubrité
ou de péril lorsque, dans les conditions définies au I de l'article L. 521-2
du présent code, elles reprennent le paiement du loyer ou de toute autre somme
versée en contrepartie de l'occupation de ce logement ou lorsqu'elles sont relogées.

```catala
# Pas pertinent pour le moment.
# Mais d'autres modes d'occupation peuvent peut-être être ajoutés?
```

######## Article R823-12 | LEGIARTI000038878915

Les aides personnelles au logement cessent d'être dues à partir du premier
jour du mois civil au cours duquel les conditions d'ouverture du droit
cessent d'être réunies.

Par dérogation à l'alinéa précédent, le droit aux aides personnelles au
logement est éteint à partir du premier jour du mois civil suivant celui
au cours duquel survient le décès du bénéficiaire.

```catala
# Cet article traite de l'extinction du droit à cette aide, ce n'est pas
# pertinent pour le moment.
```

######## Article R823-13 | LEGIARTI000038878913

Tout changement de nature à modifier les droits aux aides personnelles au
logement, en particulier tout changement de la composition familiale, prend
effet et cesse de produire ses effets selon les règles prévues pour
l'ouverture et pour l'extinction des droits définies, respectivement,
au premier alinéa de l'article R. 823-10 et au premier alinéa de l'article
R. 823-12, sauf en cas de décès du conjoint du bénéficiaire ou d'une personne
à charge, où le changement prend effet le premier jour du mois civil suivant
celui au cours duquel survient le décès.

Lorsqu'une séparation, telle que mentionnée à l'article R. 821-3 , intervient
en cours de période de paiement, le droit à l'aide du bénéficiaire est
réexaminé en fonction de la nouvelle situation et la révision du droit prend
effet le premier jour du mois civil suivant celui au cours duquel la séparation
a eu lieu.

```catala
# Pas pertinent pour le moment.
```

######## Article R823-14 | LEGIARTI000038878911

Les dispositions des articles R. 823-10 et R. 823-12 ne peuvent avoir pour effet
d'interrompre le droit aux aides personnelles au logement.

Par dérogation aux dispositions des articles R. 831-2, R. 832-6 et R. 832-8,
en cas de déménagement, de conclusion ou de résiliation des conventions mentionnées
à l'article L. 831-1 , le droit aux aides personnelles au logement :

1° Est ouvert à compter du premier jour du mois civil au cours duquel les conditions
d'ouverture du droit sont réunies ;

2° S'éteint le dernier jour du mois civil au cours duquel ces conditions cessent
d'être réunies.

```catala
# Cet article met en place des délais qu'il n'est pas nécessaire de codifier
# pour le moment.
```

######## Article D823-15 | LEGIARTI000038878909

Le signalement par le bailleur du déménagement du bénéficiaire et de la résiliation
de son bail a lieu dans un délai d'un mois à compter de la date de déménagement
ou de la résiliation du bail.

Ce délai peut être prolongé d'un mois supplémentaire si le bailleur apporte la
preuve qu'il n'était manifestement pas en mesure de signaler ces évènements dans
le délai d'un mois.

Le signalement par le prêteur du remboursement anticipé du prêt a lieu dans un délai
d'un mois à compter de la date de remboursement du solde du prêt.

```catala
# Cet article n'appelle pas nécessairement à codification mais il présente
# une petite difficulté : les informations dont il est question et qui
# peuvent potentiellement être appréhendées comme des variables à l'entrée
# dans le cadre du calcul ne sont pas renseignées par l'allocataire mais par
# son bailleur ou prêteur.
```

####### Sous-section 2 : Calcul de l'aide en secteur locatif

######## Article D823-16 | LEGIARTI000038878905

Pour les ménages mentionnés au 1° de l'article D. 823-9, le montant mensuel de l'aide
est calculé selon la formule suivante :

$$\textrm{Af} = \textrm{L} + \textrm{C}-\textrm{Pp}$$

```catala
champ d'application CalculAidePersonnaliséeLogementLocatif:
  définition aide_finale_formule égal à
    soit aide_finale égal à
      loyer_éligible +€ montant_forfaitaire_charges_d823_16 -€
      participation_personnelle
    dans
    si aide_finale <€ 0 € alors 0 € sinon aide_finale
```

où :

1° " Af " est l'aide mensuelle résultant de la formule de calcul ;

2° " L " est le loyer éligible, correspondant au loyer principal pris en compte dans
la limite d'un plafond fixé par arrêté en fonction de la zone géographique et, sauf
dans le cas où le logement occupé est une chambre, de la composition familiale ;

```catala
champ d'application CalculAidePersonnaliséeLogementLocatif:
  définition loyer_éligible égal à
    si loyer_principal >€ plafond_loyer_d823_16_2
    alors plafond_loyer_d823_16_2
    sinon loyer_principal
```

3° " C " est le montant forfaitaire au titre des charges, fixé par arrêté en fonction
de la composition familiale ;

```catala
# Ne donne pas précisément la formule mais uniquement le principe général,
# on ne formalise pas ici.
```

4° " Pp " est la participation personnelle du ménage calculée selon les dispositions
de l'article D. 823-17.

```catala
# Ne donne pas précisément la formule mais uniquement le principe général,
# on ne formalise pas ici.
```

Le montant ainsi calculé est diminué lorsque le loyer principal dépasse un plafond
de dégressivité. Il décroît proportionnellement au dépassement de ce plafond, de telle
sorte qu'il soit nul lorsqu'il atteint un plafond de suppression. Le montant de ces
plafonds est obtenu par l'application de coefficients multiplicateurs, fixés par
arrêté en fonction de la zone géographique, au montant du plafond de loyer mentionné
au 2°. Le plafond de dégressivité ne peut être inférieur à ce plafond de loyer
multiplié par 2,5. Toutefois, cette diminution ne s'applique pas lorsque le demandeur
ou son conjoint est bénéficiaire de l'allocation aux adultes handicapés prévue à
l' article L. 821-1 du code de la sécurité sociale ou de l'allocation d'éducation de
l'enfant handicapé prévue à l'article L. 541-1 du même code.

```catala
champ d'application CalculAidePersonnaliséeLogementLocatif:
  définition traitement_aide_finale de aide_finale état diminué égal à
    si loyer_principal >€ plafond_suppression_d823_16 alors
      0€
    sinon (si loyer_principal >€ plafond_dégressivité_d823_16 alors
        aide_finale -€ (aide_finale *€
          ((loyer_principal -€ plafond_dégressivité_d823_16) /€
            (plafond_suppression_d823_16 -€ plafond_dégressivité_d823_16)))
        # Faire un graphique pour se convaincre que la pente proportionnelle
        # a bien cette expression.
    sinon aide_finale)

  exception définition traitement_aide_finale de aide_finale
  état diminué
  sous condition
    bénéficiaire_aide_adulte_ou_enfant_handicapés
  conséquence égal à aide_finale

  assertion plafond_dégressivité_d823_16 >=€ plafond_loyer_d823_16_2 *€ 2,5
```

Le résultat ainsi obtenu est minoré d'un montant fixé forfaitairement par arrêté.

```catala
champ d'application CalculAidePersonnaliséeLogementLocatif:
  définition traitement_aide_finale de aide_finale
  état minoration_forfaitaire
  égal à
    soit aide_finale égal à traitement_aide_finale de aide_finale dans
    si
      aide_finale -€ montant_forfaitaire_d823_16 >=€ 0€
    alors
      aide_finale -€ montant_forfaitaire_d823_16
    sinon 0€
```

Le montant qui en résulte est diminué d'un montant représentatif des contributions
sociales qui s'y appliquent, arrondi à l'euro inférieur, puis majoré de ce montant
représentatif.

```catala
champ d'application CalculAidePersonnaliséeLogementLocatif:
  définition traitement_aide_finale de aide_finale
  état contributions_sociales_arrondi
  égal à
    soit aide_finale égal à traitement_aide_finale de aide_finale dans
    soit crds égal à
      contributions_sociales.montant de aide_finale
    dans
    soit aide_finale_moins_crds_arrondie égal à
      arrondi_argent de ((aide_finale -€ crds) -€ 0,50€)
    dans
    si
      aide_finale_moins_crds_arrondie +€ crds >=€ 0€
    alors
      aide_finale_moins_crds_arrondie +€ crds
    sinon 0€
```

Pour les locataires qui bénéficient de la réduction de loyer de solidarité en application
de l'article L. 442-2-1 , ce résultat est réduit d'un montant égal à 98 % de la réduction
de loyer de solidarité.

```catala
champ d'application CalculAidePersonnaliséeLogementLocatif:
  définition fraction_l832_3 égal à 98%
```

Lorsque ce dernier résultat, calculé selon les dispositions précédentes, est inférieur à
un montant fixé par arrêté, selon celle des trois aides dont le ménage bénéficie, il
n'est pas procédé à son versement.

```catala
champ d'application CalculAidePersonnaliséeLogementLocatif:
  définition traitement_aide_finale de aide_finale état montant_minimal égal à
    soit aide_finale égal à traitement_aide_finale de aide_finale dans
    si
      aide_finale <€ montant_minimal_aide_d823_16
    alors 0 € sinon aide_finale
```

######## Article D823-17 | LEGIARTI000041419255

La participation personnelle du ménage, mentionnée au 4° de l'article D. 823-16, est
la somme d'une participation minimale et d'une participation au titre des ressources
du ménage, calculée selon la formule suivante :

$$\textrm{Pp} = \textrm{P0} + \textrm{Tp}\times (\textrm{R}-\textrm{R0})$$

```catala
champ d'application CalculAidePersonnaliséeLogementLocatif:
  définition participation_personnelle égal à
    soit participation_ressources égal à
       (ressources_ménage_arrondies -€ abattement_forfaitaire_d823_17) *€
        taux_prise_compte_ressources
    dans
    soit participation_ressources égal à
      si participation_ressources <€ 0€ alors 0€ sinon participation_ressources
    dans
    participation_minimale +€ participation_ressources
```

où :

1° " Pp " est la participation personnelle du ménage ;

2° " P0 " est la participation minimale calculée selon des modalités précisées par
arrêté et qui ne peut être inférieure à un montant minimum défini par arrêté ;


```catala
# Ne donne pas précisément la formule mais uniquement le principe général,
# on ne formalise pas ici.
```

3° " Tp " est le taux de prise en compte des ressources du ménage. Il est égal à la
somme d'un premier taux en fonction de la composition familiale et d'un second taux
en fonction du loyer éligible défini au 2° de l'article D. 823-16. Le second taux est
obtenu par l'application de taux progressifs à des tranches successives du loyer éligible,
exprimé en proportion d'un loyer de référence en fonction de la composition familiale.
Les valeurs du premier taux, les modalités de calcul du second taux et les valeurs des
loyers de référence sont fixées par arrêté ;

```catala
champ d'application CalculAidePersonnaliséeLogementLocatif:
  définition taux_prise_compte_ressources égal à
    taux_composition_familiale +. taux_loyer_éligible
```

4° " R " représente les ressources du ménage, appréciées selon les modalités prévues
à la section 2 du chapitre II du présent titre et arrondies à la centaine d'euros supérieure ;

```catala
champ d'application CalculAidePersonnaliséeLogement:
  définition ressources_ménage état avec_arrondi égal à
    # Cette formule arrondit à la centaine d'euros supérieure. Essayez quelques
    # exemples pour vous en convaincre, dont 100 et 150.
    arrondi_argent de ((ressources_ménage *€ 1%) +€ 0,49€) *€ 100,0
```

5° “ R0 ” est un abattement forfaitaire appliqué aux ressources du ménage. Il est fixé
par arrêté en fonction de la composition familiale et est revalorisé au 1er janvier de
chaque année, en fonction de l'évolution de l'indice des prix à la consommation des
ménages hors tabac. Cette évolution est appréciée entre le 1er octobre de l'avant-dernière
année précédant la revalorisation et le 1er octobre de l'année précédant la revalorisation.
Il est arrondi à l'euro inférieur.

NOTA :

Se reporter aux conditions d'application prévues à l'article 25 du décret n° 2019-1574
du 30 décembre 2019. Conformément à l'article 1 du décret n° 2020-451 du 20 avril 2020
l'article susvisé a été modifié : Ces dispositions sont applicables au calcul des droits,
d'une part, à l'aide personnalisée au logement prévue pour les accédants à la propriété
par l'article L. 831-1 du code de la construction et de l'habitation, d'autre part, aux
autres aides personnelles au logement prévues par le même code, à compter des mois
respectifs fixés par arrêté des ministres en charge du logement, de la santé et du
budget et, au plus tard, du 1er janvier 2021.

Aux termes de l'article 2 du décret n° 2020-1816 du 29 décembre 2020, l'article 25 du
décret n° 2019-1574 du 30 décembre 2019 a été modifié : Ces dispositions sont applicables
au calcul des droits à l’aide personnelle au logement à compter du mois de janvier 2021
ou, pour le calcul des droits à l’aide personnalisée au logement prévue pour les accédants
à la propriété par l’article L. 831-1 du code de la construction et de l’habitation,
à compter du mois de mai 2021.

######## Article D823-18 | LEGIARTI000041477261

Les arrêtés fixant les plafonds de loyer mentionnés au 2° de l'article D. 823-16 et
les montants forfaitaires de charges mentionnés au 3° du même article peuvent fixer
des montants spécifiques pour les ménages colocataires ou dans les cas de sous-location
partielle du logement autorisés à l'article L. 822-4 . Les paramètres de calcul de l'aide
sont déterminés pour chaque foyer en fonction de sa propre composition familiale.

Pour le calcul de l'aide du locataire sous-louant une partie du logement, le loyer
principal pris en compte correspond au loyer résiduel après déduction des loyers
provenant de la sous-location, hormis pour les contrats d'accueils familiaux mentionnés
à l' article L. 442-1 du code de l'action sociale et des familles pour lesquels le loyer
principal correspond à l'intégralité du loyer acquitté.

Pour le calcul de l'aide du sous-locataire en sous-location partielle, le loyer
principal pris en compte correspond au loyer acquitté.

Lorsque le logement est loué ou sous-loué en meublé, les dispositions de l'article
D. 842-2 s'appliquent le cas échéant pour la détermination des loyers pris en compte.

```catala
# Nous choissisons par manque de ressources (sic) de développement de laisser
# le calcul du loyer principal à l'utilisateur du programme qui devra suivre
# les instructions de cet article et de ceux auxquels il se réfère pour
# correctement évaluer leur montant.
```

######## Article D823-19 | LEGIARTI000038878899

Les arrêtés pris pour l'application de la présente sous-section sont pris par les
ministres chargés du logement, du budget, de la sécurité sociale et de l'agriculture.

Le zonage géographique est fixé par arrêté des ministres chargés du logement et du budget.

```catala
# Pas pertinent pour le moment.
```

###### Section 2 : Prime de déménagement

####### Article D823-20 | LEGIARTI000038878895

La prime de déménagement est attribuée aux personnes ou aux ménages ayant à charge
au moins trois enfants nés ou à naître et qui s'installent dans un nouveau logement
ouvrant droit à l'une des aides personnelles au logement au cours d'une période comprise
entre le premier jour du mois civil suivant le troisième mois de grossesse au titre
d'un enfant de rang trois ou plus et le dernier jour du mois précédant celui au cours
duquel cet enfant atteint son deuxième anniversaire.

Cette prime est due si le droit à l'aide est ouvert dans un délai de six mois à compter
de la date d'emménagement.

```catala
champ d'application ÉligibilitéPrimeDeDéménagement:
  règle condition_rang_enfant sous condition
    (nombre pour personne_à_charge dans ménage.personnes_à_charge de
      (selon personne_à_charge sous forme
      -- EnfantÀCharge: vrai
      -- AutrePersonneÀCharge: faux)
    ) +
    informations.nombre_enfants_à_naître_après_troisième_mois_grossesse >= 3
  conséquence rempli

  règle condition_période_déménagement sous condition
    (selon
      informations.date_naissance_troisième_enfant_ou_dernier_si_plus
    sous forme
    -- MoinsDeTroisEnfants: faux
    -- PlusDeTroisEnfants de date_naissance_ou_grossesse:
      (selon date_naissance_ou_grossesse sous forme
      -- AvantPremierJourMoisCivilTroisièmeMoisDeGrossesse: faux
      -- AprèsPremierJourMoisCivilTroisièmeMoisDeGrossesse: vrai
      -- DateDeNaissance de date_naissance:
        date_courante <=@
          ((premier_jour_du_mois de (date_naissance +@ 2 an))) +@ (-1 jour))
    )
  conséquence rempli

  définition délai_après_emménagement_l823_8_2 égal à 6 mois
```

####### Article D823-21 | LEGIARTI000038878893

La demande de prime, accompagnée des pièces justificatives, est déposée auprès de
l'organisme payeur de l'aide personnelle au logement, six mois au plus tard après
la date de l'emménagement dans la nouvelle résidence. Elle est conforme à un modèle-type.

Le modèle-type de la demande et la liste des pièces justificatives à fournir sont
fixés par arrêté des ministres chargés du logement, du budget, de la sécurité sociale
et de l'agriculture.

```catala
# Pas pertinent pour le moment.
```

####### Article D823-22 | LEGIARTI000038878891

Le montant de la prime de déménagement est égal aux dépenses justifiées réellement
engagées par le bénéficiaire dans la limite d'un plafond fixé, en fonction de la
composition de la famille, par arrêté des ministres chargés du logement, du budget,
de la sécurité sociale et de l'agriculture.

```catala
champ d'application ÉligibilitéPrimeDeDéménagement:
  définition montant_prime_déménagement égal à
    si dépenses_justifiées_réellement_engagées <=€ plafond_d823_22 alors
      dépenses_justifiées_réellement_engagées
    sinon
      plafond_d823_22
```

###### Section 3 : Recouvrement des sommes indûment versées

####### Article R823-23 | LEGIARTI000038878887

Dans le cas où le bailleur ou l'établissement habilité justifie qu'il a,
conformément aux articles L. 832-2 et L. 842-1 , déduit les sommes d'aide
personnelle au logement du montant du loyer et des dépenses accessoires de
logement ou de celui des charges de remboursement et que le locataire ou
l'emprunteur ne conteste pas le caractère indu de ces sommes, celles-ci
sont recouvrées, suivant le cas, auprès du locataire ou de l'emprunteur,
dans les conditions fixées à l' article L. 553-2 du code de la sécurité sociale .

```catala
# Pas pertinent pour le moment.
```

####### Article R823-24 | LEGIARTI000038878885

Les dispositions des articles R. 133-9-2 , D. 553-1 , D. 553-2 , D. 553-4 et
D. 553-5 du code de la sécurité sociale sont applicables au recouvrement des indus.

```catala
# Pas pertinent pour le moment.
```

####### Article D823-25 | LEGIARTI000038878883

Les organismes payeurs sont autorisés à abandonner la mise en recouvrement des
sommes indûment payées, dans les conditions définies à l' article D. 133-2 du code
de la sécurité sociale .

```catala
# Pas pertinent pour le moment.
```

####### Article D823-26 | LEGIARTI000042385635

Pour la mise en œuvre de la saisie des aides personnelles au logement prévue au
3° de l'article L. 821-6, les prélèvements mensuels d'exécution de la saisie sont
déterminés selon les dispositions des articles D. 553-1 et D. 553-2 du code
de la sécurité sociale

NOTA :

Conformément au II de l'article 3 du décret n° 2020-1202 du 30 septembre 2020,
ces dispositions s'appliquent à compter du 1er juin 2021.

```catala
# Pas pertinent pour le moment.
```

##### Chapitre IV : Impayés de dépenses de logement

###### Section 1 : Seuils de constitution d'un impayé

####### Article R824-1 | LEGIARTI000038878877

Dans le secteur locatif, lorsque l'aide personnelle au logement est versée au
bénéficiaire, l'impayé de dépense de logement, comprenant le loyer et, le cas
échéant, les charges locatives, est constitué quand le locataire est débiteur
à l'égard du bailleur d'une somme au moins égale à deux fois le montant mensuel
brut du loyer et des charges.

```catala
champ d'application ImpayéDépenseLogement
  sous condition mode_occupation_impayé sous forme ImpayéLoyer:
  définition montant_impayé égal à
    si montant_dette >=€ seuil_impayé_dépense_de_logement
    alors montant_dette sinon 0€

  définition seuil_impayé_dépense_de_logement
    sous condition aide_versée sous forme Bénéficiaire
    conséquence égal à
      selon dépense_logement_brute sous forme
      -- Loyer de loyer_brut: (loyer_brut +€ montant_charges) *€ 2,0
      -- Mensualité: 0€ # ne devrait pas arriver
      -- TotalAnnuelÉchéances: 0€ # ne devrait pas arriver
```

Lorsque l'aide personnelle au logement est versée entre les mains du bailleur,
l'impayé est constitué quand le locataire est débiteur à l'égard du bailleur
d'une somme au moins égale à deux fois le montant mensuel net du loyer et des charges.

```catala
champ d'application ImpayéDépenseLogement
  sous condition mode_occupation_impayé sous forme ImpayéLoyer:
  définition seuil_impayé_dépense_de_logement
    sous condition aide_versée sous forme Bailleur
    conséquence égal à
      selon dépense_logement_nette sous forme
      -- Loyer de loyer_net: (loyer_net +€ montant_charges) *€ 2,0
      -- Mensualité: 0€ # ne devrait pas arriver
      -- TotalAnnuelÉchéances: 0€ # ne devrait pas arriver
```

Le montant mensuel brut du loyer correspond au loyer figurant dans le bail.

```catala
# Il n'y a pas de montant de charge brut/net
champ d'application ImpayéDépenseLogement
  sous condition mode_occupation_impayé sous forme ImpayéLoyer:
  définition dépense_logement_brute égal à dépense_logement
```

Le montant mensuel net du loyer correspond à ce même loyer, déduction faite du
montant de l'aide personnelle au logement.

```catala
champ d'application ImpayéDépenseLogement
  sous condition mode_occupation_impayé sous forme ImpayéLoyer:
  définition dépense_logement_nette égal à
    selon dépense_logement_brute sous forme
    -- Loyer de montant_loyer: Loyer contenu (montant_loyer -€ montant_apl)
    -- Mensualité de mensualité: Mensualité contenu mensualité
    # Ne devrait pas arriver
    -- TotalAnnuelÉchéances de total: TotalAnnuelÉchéances contenu total
    # Ne devrait pas arriver
```

####### Article R824-2 | LEGIARTI000038878875

```catala
# Cet article précise les cas où un impayé est constitué dans le cadre de
# l'accession à la propriété.
```
```catala
champ d'application ImpayéDépenseLogement sous condition
  mode_occupation_impayé sous forme ImpayéPrêt:

  définition montant_impayé égal à
    si montant_dette >=€ seuil_impayé_dépense_de_logement
    alors montant_dette sinon 0€
```

Dans le secteur de l'accession à la propriété :

1° Lorsque l'aide personnelle au logement est versée au bénéficiaire,
l'impayé est constitué :

a) En cas de périodicité mensuelle du prêt, lorsque l'emprunteur est débiteur
à l'égard de l'établissement habilité d'une somme au moins égale à deux
échéances de prêt brutes ;

b) En cas de périodicité autre que mensuelle du prêt, lorsque l'emprunteur est
débiteur à l'égard de l'établissement habilité d'une somme au moins égale à un
sixième du total annuel des échéances de prêt brutes ;

```catala
champ d'application ImpayéDépenseLogement sous condition
  mode_occupation_impayé sous forme ImpayéPrêt:

  définition seuil_impayé_dépense_de_logement
    sous condition
      aide_versée sous forme Bénéficiaire
    conséquence égal à
      selon dépense_logement_brute sous forme
      -- Loyer: 0€ # Ne devrait pas arriver
      -- Mensualité de mensualité_brute:
        mensualité_brute *€ 2,0
      -- TotalAnnuelÉchéances de échéance_prêt_brute:
        échéance_prêt_brute *€ (1,0 /. 6,0)
```

2° Lorsque l'aide personnelle au logement est versée directement auprès de
l'établissement habilité, l'impayé est constitué :

a) En cas de périodicité mensuelle du prêt, lorsque l'emprunteur est débiteur
à l'égard de l'établissement habilité d'une somme au moins égale à deux échéances
de prêt nettes ;

b) En cas de périodicité autre que mensuelle, lorsque l'emprunteur est débiteur
à l'égard de l'établissement habilité d'une somme au moins égale à un sixième du
total annuel des échéances de prêt nettes.


```catala
champ d'application ImpayéDépenseLogement sous condition
  mode_occupation_impayé sous forme ImpayéPrêt:

  définition seuil_impayé_dépense_de_logement
    sous condition
      aide_versée sous forme ÉtablissementHabilité

    conséquence égal à
      selon dépense_logement_nette sous forme
      -- Loyer: 0€ # ne devrait pas arriver
      -- Mensualité de mensualité_nette: mensualité_nette *€ 2,0
      -- TotalAnnuelÉchéances de échéance_prêt_nette:
        échéance_prêt_nette *€ (1,0 /. 6,0)
```


L'échéance de prêt brute correspond à celle figurant dans le contrat de prêt.

```catala
champ d'application ImpayéDépenseLogement sous condition
  mode_occupation_impayé sous forme ImpayéPrêt:
  définition dépense_logement_brute égal à dépense_logement
```

L'échéance de prêt nette correspond à cette même échéance, déduction faite de l'aide
personnelle au logement.

```catala
champ d'application ImpayéDépenseLogement sous condition
  mode_occupation_impayé sous forme ImpayéPrêt:
  définition dépense_logement_nette égal à
    selon dépense_logement sous forme
    -- Loyer de loyer: Loyer contenu loyer # ne devrait pas arriver
    -- Mensualité de mensualité:
      Mensualité contenu (mensualité -€ montant_apl)
    -- TotalAnnuelÉchéances de total_échéances:
      TotalAnnuelÉchéances contenu (total_échéances -€ (montant_apl *€ 12,0))
```


####### Article R824-3 | LEGIARTI000038878873

Pour l'application du présent chapitre, les redevances prévues par le contrat
de location-accession ou par le contrat d'occupation en logement-foyer mentionné
à l'article L. 633-1 sont assimilées, respectivement, au montant du loyer et des
charges ou à une échéance.

```catala
# Est ce qu'un sous-locataire est considéré comme un locataire pour les
# impayés ?
# Oui car le deuxième alinéa de L822-4 s'applique ici, comme rappelé par
# DGALN/DHUP/FE4 le 25/05/2022.
champ d'application ImpayéDépenseLogement:
  définition mode_occupation_impayé égal à
    selon mode_occupation sous forme
    -- Locataire: ImpayéLoyer
    -- AccessionPropriétéLocalUsageExclusifHabitation: ImpayéPrêt
    -- RésidentLogementFoyer: ImpayéPrêt
    -- LocationAccession: ImpayéLoyer
    -- SousLocataire:  ImpayéLoyer
```

###### Section 2 : Secteur locatif

####### Sous-section 1 : Signalement et mise en place d'un plan d'apurement en cas d'impayé

######## Article R824-4 | LEGIARTI000038878867

Lorsque le bénéficiaire de l'aide personnelle au logement ne règle pas la part
de dépense de logement restant à sa charge, sa situation est soumise à l'organisme
payeur par le bailleur percevant l'aide personnelle au logement pour son compte,
dans un délai de deux mois après la constitution de l'impayé défini à l'article
R. 824-1 , sauf si la somme due a été, entre-temps, réglée en totalité.

Le bailleur doit justifier qu'il poursuit par tous les moyens possibles le
recouvrement de sa créance.

```catala
# Sur le délai de déclaration de l'impayé de loyer par le bailleur,
# pas nécessairement utile pour le moment
```

######## Article R824-5 | LEGIARTI000038878865

L'organisme payeur se saisit de toute situation d'impayé définie à l'article R. 824-1
dont il a connaissance.

```catala
# Pas pertinent car n'implique pas de calcul.
```

######## Article R824-6 | LEGIARTI000038878863

Si le bailleur ne signale pas l'impayé à l'organisme payeur dans le délai et les
conditions mentionnés à l'article R. 824-4 ou s'il n'apporte pas les justifications
prévues au même article, il est fait application des dispositions de l'article L. 114-17
du code de la sécurité sociale .

```catala
# L'article L. 114-17 du code de la sécurité sociale énonce les différents
# cas où une pénalité peut être prononcée par le directeur de l'organisme
# chargé de la gestion des prestations familiales.
```

######## Article R824-7 | LEGIARTI000038878861

Lorsque le bénéficiaire de l'aide est en situation d'impayé de dépense
de logement, l'organisme payeur informe la commission de coordination
des actions de prévention des expulsions locatives et met en œuvre les
mesures prévues au présent article.

Pour se prononcer sur le maintien de l'aide, l'organisme payeur choisit,
en fonction de la situation du bénéficiaire, de recourir à l'une ou à
l'autre des procédures définies au 1° et au 2°.

1° L'organisme payeur peut choisir de renvoyer le dossier au bailleur
afin que ce dernier établisse, dans un délai de six mois au plus,
un plan d'apurement de la dette. Sous réserve de la reprise du paiement
de la dépense courante de logement, du respect du plan d'apurement et de
son approbation par l'organisme payeur, ce dernier maintient le versement
de l'aide personnelle au logement.

A défaut de réception du plan d'apurement dans le délai mentionné au 1° et
après mise en demeure du bailleur, l'organisme payeur saisit le fonds départemental
de solidarité pour le logement mentionné à l' article 6 de loi n° 90-449 du 31 mai 1990
visant à la mise en œuvre du droit au logement, qui dispose d'un délai de trois mois
pour établir un dispositif d'apurement. L'organisme payeur tient la commission de
coordination des actions de prévention des expulsions locatives informée de l'évolution
de la situation du bénéficiaire.

En cas de mauvaise exécution du plan ou du dispositif d'apurement ou de non-reprise du
paiement de la dépense courante de logement, l'organisme payeur suspend le versement de
l'aide personnelle au logement, sous réserve des dispositions de l'article R. 824-28 .

2° L'organisme payeur peut choisir de saisir directement le fonds départemental de
solidarité pour le logement ou tout autre organisme à vocation analogue, en lui demandant
de faire connaître son dispositif d'apurement dans un délai de six mois. L'organisme payeur
informe la commission de coordination de la situation du bénéficiaire de l'aide. Le bailleur,
informé de cette saisine par l'organisme payeur, peut faire part de ses propositions au
fonds départemental ou à l'organisme à vocation analogue.

Après réception du dispositif d'apurement, l'organisme payeur poursuit le versement de l'aide
personnelle au logement sous réserve, de la reprise du paiement de la dépense courante de logement.

En cas de mauvaise exécution du plan ou du dispositif d'apurement ou de non-reprise du paiement
de la dépense courante de logement, l'organisme payeur suspend le versement de l'aide personnelle
au logement, sous réserve des dispositions de l'article R. 824-28.

```catala
# Cet article explicite les procédures à suivre pour l'organisme payeur en
# cas d'impayés. Nous choissisons par manque de ressources (sic) de
# développement de laisser à l'organisme payeur le soin de suivre les
# instructions de cet article et de ceux auxquels il se réfère
# afin de connaître la bonne procédure à suivre.
```

######## Article R824-8 | LEGIARTI000038878859

Si le fonds départemental de solidarité pour le logement ou un organisme à vocation analogue
n'a pas fait connaître le dispositif qu'il a retenu dans les délais prévus au 1° ou au 2° de
l'article R. 824-7 , l'organisme payeur met en demeure le bénéficiaire de reprendre le paiement
de la dépense courante de logement et d'apurer l'intégralité de sa dette en remboursant chaque
mois au bailleur 1/ 36e de sa dette pendant trente-six mois à compter du mois suivant la mise
en demeure.

En cas de mauvaise exécution du plan d'apurement ou de non-reprise du paiement de la dépense
courante de logement, l'organisme payeur suspend le versement de l'aide personnelle au logement,
sous réserve des dispositions de l'article R. 824-28 .

```catala
# Idem
```

######## Article R824-9 | LEGIARTI000038878857

La bonne exécution du plan ou du dispositif d'apurement est vérifiée, au moins tous les six
mois, par l'organisme payeur.

```catala
# Pas pertinent pour le moment
```

######## Article R824-10 | LEGIARTI000038878855

Pour les impayés d'un montant égal ou inférieur à cent euros, l'organisme payeur peut proposer
au bailleur et au bénéficiaire de l'aide de recourir à une procédure de traitement de l'impayé
selon des modalités précisées par arrêté des ministres chargés du logement, de la sécurité sociale
et de l'agriculture. Cet arrêté précise le cadre dans lequel l'organisme payeur élabore un plan
d'apurement.

```catala
# Procédure alternative pour les impayés < ou = à 100€
# Mais la procédure n'est pas explicitée.
```

Cette procédure de traitement de l'impayé prend fin lorsque le plan proposé par l'organisme payeur
n'est pas approuvé par le bailleur et par le bénéficiaire dans le délai imparti ou en cas de
non-respect de ce plan. Dans ce cas, l'organisme payeur applique la procédure de droit commun
définie à l'article R. 824-7 , les délais fixés aux 1° et 2° de cet article et au deuxième alinéa
de l'article R. 824-20 étant alors divisés par deux.

```catala
# Pas pertinent pour le moment.
```

####### Sous-section 2 : Saisine directe du fonds de solidarité pour le logement ou d'un organisme à vocation analogue

######## Article R824-11 | LEGIARTI000038878851

Si le fonds départemental de solidarité pour le logement ou un organisme à vocation analogue
a été saisi en même temps que l'organisme payeur, il en informe, sans délai, l'organisme
payeur. Ce dernier maintient le versement de l'aide personnelle au logement pour une durée
de six mois à compter de cette saisine.

A défaut de réception d'un dispositif d'apurement dans le délai mentionné au premier alinéa,
et après mise en demeure du fonds départemental de solidarité pour le logement ou d'un organisme
à vocation analogue, l'organisme payeur saisit le bailleur, afin de mettre en place un plan
d'apurement dans un délai de trois mois à compter de cette saisine. Il en informe simultanément
la commission de coordination des actions de prévention des expulsions locatives.

```catala
# Pas pertinent pour le moment.
```

######## Article R824-12 | LEGIARTI000038878849

A défaut de réception de ce plan d'apurement dans le délai de trois mois, l'organisme payeur
met en demeure le bénéficiaire de reprendre le paiement de la dépense courante de logement
et d'apurer l'intégralité de sa dette en remboursant chaque mois au bailleur 1/36e de sa dette,
pendant trente-six mois à compter du mois suivant la mise en demeure.

```catala
# Pas pertinent pour le moment.
```

######## Article R824-13 | LEGIARTI000038878847

Dans chacune des situations définies aux articles R. 824-11 et R. 824-12, en cas de mauvaise
exécution du plan ou du dispositif d'apurement ou de non-reprise du paiement de la dépense
courante de logement, l'organisme payeur suspend le versement de l'aide personnelle au logement,
sous réserve des dispositions de l'article R. 824-28 .

```catala
# Pas pertinent pour le moment.
```

####### Sous-section 3 : Modalités d'attribution des aides personnelles au logement en cas de résiliation du bail

######## Article R824-14 | LEGIARTI000038878843

Lorsque le bail a été résilié et que l'occupant du logement s'acquitte d'une indemnité d'occupation
et des charges fixées par le juge, le versement de l'aide est maintenu durant la période où l'occupant
s'acquitte de l'indemnité et des charges fixées, et jusqu'au départ effectif de l'occupant.

```catala
# Pas pertinent pour le moment.
```

######## Article D824-15 | LEGIARTI000038878841

Pour qu'elle ouvre droit au bénéfice de l'aide, la signature du protocole d'accord, conclu en
application des articles L. 353-15-2 , L. 442-6-5 et du sixième alinéa de l'article L. 442-8-2 ,
est subordonnée à l'approbation préalable du plan d'apurement par l'organisme payeur.

Si un protocole d'accord est signé, l'organisme payeur fixe les modalités du versement du rappel
de l'aide pendant la période comprise entre l'interruption du versement de l'aide et la signature
du protocole.

Ces modalités tiennent compte de la situation financière du bénéficiaire de l'aide et du plan de
résorption de la dette établi avec le bailleur. A ce titre, l'organisme payeur décide du versement
du rappel d'aide :

1° Soit en une seule fois si le montant du rappel ou de la dette est inférieur à quatre cent
cinquante euros ;

2° Soit par versements semestriels échelonnés sur la durée du plan d'apurement et sous réserve
de sa bonne exécution. Dans ce cas, le premier versement est réalisé trois mois après la reprise
du paiement par l'occupant des échéances prévues par le protocole.

```catala
# Là encore, il s'agit d'une procédure qu'il n'est peut-être pas pertinent
# pour le moment de traduire en catala. L'alinéa 3, en revanche, peut être
# intéressant à traiter. Mais là encore, il faut poser le cadre de ce rappel
# d'aide avant de préciser comment est apprécié ce rappel d'aide.
```

######## Article R824-16 | LEGIARTI000038878839

Si l'occupant ne respecte pas les engagements contenus dans le protocole, le bailleur est tenu
d'en informer l'organisme payeur qui suspend le versement du rappel, sous réserve des dispositions
de l'article R. 824-28 .

Sous réserve de la reprise du paiement de la dépense courante de logement, l'organisme payeur
maintient l'aide personnelle au logement pendant une durée qui ne peut excéder trois mois,
pour permettre la négociation d'un nouveau plan d'apurement entre le bailleur et l'occupant.

Ce nouveau plan fait l'objet d'un avenant au protocole.

```catala
# Pas pertinent pour le moment.
```

######## Article R824-17 | LEGIARTI000038878837

Si l'organisme payeur ne reçoit pas le plan d'apurement dans le délai mentionné au deuxième
alinéa de l'article R. 824-16 ou s'il ne l'approuve pas, il met en demeure le bénéficiaire
de reprendre, sans délai, le paiement de la dépense courante de logement et d'apurer
l'intégralité de sa dette en remboursant chaque mois au bailleur 1/ 36e de sa dette,
pendant trente-six mois.

```catala
# Disposition traitant d'une procédure.
# Pas pertinent pour le moment.
```

######## Article R824-18 | LEGIARTI000038878835

En cas de non-reprise du paiement de la dépense courante de logement ou de refus de
s'engager sur ce plan d'apurement ou de mauvaise exécution de ce plan, le versement
de l'aide personnelle au logement est suspendu, sous réserve des dispositions de
l'article R. 824-28 .

```catala
# Suspension du versement de l'aide. Il est important de définir lorsque
# le bénéficiaire ne peut plus bénéficier de l'aide, mais cela implique le
# traitement des procédures préliminaires.
```

######## Article R824-19 | LEGIARTI000038878832

L'exécution régulière du plan ou du dispositif d'apurement est vérifiée tous les six mois
par l'organisme payeur.

```catala
# Pas pertinent pour le moment.
```

####### Sous-section 4 : Versement en tiers payant en cas de situation d'impayé

######## Article R824-20 | LEGIARTI000038878828

Dans le cas où un bénéficiaire qui perçoit directement l'aide personnalisée au logement se
trouve dans la situation d'impayé définie à l'article R. 824-1 , l'organisme payeur demande
au bailleur de lui indiquer s'il souhaite en obtenir le versement entre ses mains, en lieu
et place du bénéficiaire.

Le bailleur dispose d'un délai de deux mois pour faire connaître sa position à l'organisme
payeur. Ce délai est inclus dans les délais prévus aux 1° et 2° de l'article R. 824-7 . Le
silence du bailleur à l'expiration de ce délai vaut décision de refus.

Les dispositions du présent article sont applicables aux allocations de logement, sauf dans
le cas prévu au troisième alinéa de l'article L. 842-1 .

```catala
# Demande de modification de la personne qui perçoit les aides pour
# lesquelles le bénéficiaire est éligible. + Procédure que le bailleur doit
# mettre en place pour procéder à ce changement.
```

######## Article R824-21 | LEGIARTI000038878826

Sans préjudice de l'application des articles R. 824-7 à R. 824-13 et R. 824-27 à R. 824-29 ,
à réception de l'accord du bailleur, l'organisme payeur lui verse l'aide et en informe le
bénéficiaire.

```catala
# Résultat de la demande traitée à l'article R824-20.
```

######## Article R824-22 | LEGIARTI000038878824

En cas de refus du bailleur, dans les cas limitativement prévus à l'article D. 832-2 , de
percevoir directement l'aide personnalisée au logement, le versement de l'aide est maintenu
dans les conditions prévues aux articles R. 824-7 à R. 824-13 et R. 824-27 à R. 824-29 .

Ces dispositions s'appliquent également aux allocations de logement, en cas de refus du
bailleur et dans le cas prévu au troisième alinéa de l'article L. 842-1 .

Toutefois, s'il est fait application de la procédure prévue au 1° de l'article R. 824-7,
le délai de six mois accordé au bailleur pour mettre en place un plan d'apurement est réduit
à deux mois. Il est décompté à partir de la date du refus du bailleur mentionné à l'article
R. 824-20 .

```catala
# Pas forcément pertinent pour le moment.
```

####### Sous-section 5 : Articulation de la procédure d'impayé avec la procédure de surendettement

######## Article R824-23 | LEGIARTI000038878820

Lorsqu'une procédure de surendettement a été engagée devant la commission prévue à l' article
L. 712-1 du code de la consommation , préalablement ou parallèlement à l'engagement des
procédures prévues aux sous-sections 1 à 4 et à la sous-section 6 de la présente section,
le versement de l'aide personnelle au logement est maintenu pendant le délai prévu à l' article
R. 721-4 du code de la consommation pour décider de l'orientation du dossier de surendettement.

```catala
# Conséquence du cumul de procédures sur le versement de l'aide personnelle
# au logement.
```

######## Article R824-24 | LEGIARTI000038878818

Lorsqu'elle est rétablie dans les conditions prévues à l'article L. 824-3 , l'aide est versée
entre les mains du bailleur :

1° Sans préjudice des dispositions de l'article L. 832-1 pour l'aide personnalisée au logement ;

2° Sauf en cas de refus du bailleur et dans le cas prévu au troisième alinéa de l'article
L. 842-1 pour les allocations de logement.

```catala
# Définition des cas où l'aide est versée directement entre les mains
# du bailleur.
```

######## Article R824-25 | LEGIARTI000038878816

Lorsque l'aide était versée à l'allocataire avant l'engagement de la procédure prévue à
l'article R. 824-23 , il est fait application de l'article R. 824-20 .

```catala
# Pas pertinent pour le moment.
```

######## Article R824-26 | LEGIARTI000038878814

A réception des plans, mesures ou jugements mettant fin à la procédure, l'organisme payeur
maintient le versement de l'aide personnelle au logement, sous réserve de la reprise du
paiement du loyer et du respect des conditions prévues par la commission de surendettement,
par le plan conventionnel ou par le jugement.

L'exécution régulière de la mesure ou du jugement est vérifiée tous les six mois par
l'organisme payeur.
Si le versement de l'aide a été suspendu avant l'engagement de la procédure de surendettement,
l'organisme payeur décide des modalités de versement du rappel de l'aide correspondant à la
période de suspension, dès lors que la dette n'a pas été annulée.

```catala
# Pas pertinent pour le moment
```

####### Sous-section 6 : Dispositions communes

######## Article R824-27 | LEGIARTI000038878810

La suspension du versement de l'aide personnelle au logement, en cas d'impayé, ne fait pas
obstacle à la récupération d'un indu.

```catala
# Conséquences de la suspension du versement de l'aide sur la récupération
# de l'indu.
```

######## Article R824-28 | LEGIARTI000038878808

L'organisme payeur peut décider du maintien du versement de l'aide personnelle au logement :

1° Si l'allocataire s'acquitte du paiement de la dépense courante de logement ;

2° S'il se trouve dans une situation sociale difficile et qu'il s'acquitte du paiement de
la moitié au moins de la dépense courante de logement, déduction faite de l'aide.



######## Article R824-29 | LEGIARTI000038878806

Lorsque le juge décide d'un plan d'apurement, notamment dans le cas prévu au V de l'article
24 de la loi n° 89-462 du 6 juillet 1989 tendant à améliorer les rapports locatifs et portant
modification de la loi n° 86-1290 du 23 décembre 1986 , l'aide personnelle au logement est
maintenue ou rétablie, sous réserve du respect de ce plan d'apurement, dans les conditions
prévues à l'article R. 824-26 .



######## Article R824-30 | LEGIARTI000038878804

Pour l'application du II du même article 24 de la loi du 6 juillet 1989 mentionnée à l'article
R. 824-29 , la première information par l'organisme payeur de la commission de coordination des
actions de prévention des expulsions locatives vaut saisine de cette commission.

Pour l'application de l' article 7-2 de la loi n° 90-449 du 31 mai 1990 visant à la mise en œuvre
du droit au logement, la première information par l'organisme payeur de la commission de
coordination des actions de prévention des expulsions locatives vaut alerte de cette commission.



###### Section 3 : Logements-foyers

####### Article R824-31 | LEGIARTI000038878800

Lorsque le bénéficiaire en situation d'impayé se trouve en logement-foyer, le versement de
l'aide personnelle au logement est maintenu, conformément aux modalités prévues à la section 2
du présent chapitre

Dans ce cas, le gestionnaire est substitué au bailleur et la redevance au loyer.



###### Section 4 : Accession à la propriété

####### Article R824-32 | LEGIARTI000038878796

Lorsque le bénéficiaire en situation d'impayé est accédant à la propriété, le versement de
l'aide personnelle au logement est maintenu, conformément aux dispositions prévues aux
articles R. 824-4 à R. 824-13 et R. 824-27 à R. 824-29 .

Dans ce cas, l'établissement habilité est substitué au bailleur, l'échéance de prêt au
loyer et, à l'exception de la phase locative des contrats de location-accession mentionnés
au 6° de l'article L. 831-1 , le comité responsable du plan local d'action pour le logement
et l'hébergement des personnes défavorisées mentionné à l' article 3 de la loi n° 90-449
du 31 mai 1990 visant à la mise en œuvre du droit au logement est substitué à la commission
de coordination des actions de prévention des expulsions locatives.



####### Article R824-33 | LEGIARTI000038878794

Lorsque le bénéficiaire se trouve dans la situation d'impayé définie aux articles R. 824-2
et R. 824-3, l'organisme payeur demande au prêteur s'il souhaite le versement de l'aide
entre ses mains en lieu et place du bénéficiaire conformément aux dispositions prévues aux
articles R. 824-20 à R. 824-22 , l'établissement habilité étant substitué au bailleur.



####### Article D824-34 | LEGIARTI000038878792

Les articles R. 824-23 à R. 824-26 sont applicables aux accédants à la propriété en situation
d'impayé, l'échéance d'emprunt étant assimilée au loyer.



#### Titre III : Aide personnalisée au logement

##### Chapitre Ier : Champ d'application

###### Article R831-1 | LEGIARTI000038878776

```catala
# Cet article vient seulement préciser le II. du L822-2, mais n'apporte
# pas d'information supplémentaire quand au calcul de l'égibilité ou
# du montant.
```

L'aide personnalisée au logement mentionnée au 1° de l'article L. 821-1 est
attribuée, pour leur résidence principale, aux personnes qui occupent :

1° Soit le logement dont elles sont propriétaires et qui a été construit, ou
amélioré, ou acquis et amélioré dans les conditions définies par le 1° de
l'article L. 831-1 ;

2° Soit un logement à usage locatif, faisant l'objet d'une convention conclue
en application des 2°, 3° ou 4° de l'article L. 831-1 ;

3° Soit un logement faisant l'objet d'un contrat de location-accession conclu
dans les conditions prévues par la loi n° 84-595 du 12 juillet 1984 définissant
la location-accession à la propriété immobilière et qui a été construit ou acquis
dans les conditions définies par le 6° de l'article L. 831-1 ;

4° Soit un local privatif dans un logement-foyer tel que défini à l'article
L. 633-1 , faisant l'objet d'une convention conclue en application du 5° de
l'article L. 831-1.

Pour l'application du présent titre, sont assimilés à des propriétaires les
titulaires d'un contrat leur donnant vocation à l'attribution à terme de la
propriété du logement qu'ils occupent, ainsi que les porteurs de parts ou
d'actions de sociétés donnant vocation à l'attribution en propriété du
logement qu'ils occupent.

```catala
# Le dernier alinéa apport des informations utiles à la conception du
# formulaire administratif: le cas du viager rentre dans la case de
# l'accession à la propriété par exemple.
```

##### Chapitre II : Modalités de liquidation et de versement de l'aide personnalisée au logement

###### Section 1 : Modalités du versement en tiers payant

####### Article D832-1 | LEGIARTI000038878766

I.-L'aide personnalisée au logement est versée selon les modalités précisées
par les conventions nationales prévues à l'article L. 812-2 :

1° Au bailleur ou au gestionnaire agréé en application du 3° de l'article 8
du décret n° 77-934 du 27 juillet 1977 fixant les conditions d'octroi de prêts
aidés par l'Etat pour la construction, l'amélioration et l'acquisition des
logements locatifs, lorsque le bénéficiaire est locataire ;

2° A l'établissement habilité à cette fin, lorsque le bénéficiaire est
propriétaire du logement ;

3° Au gestionnaire du logement-foyer, lorsque le bénéficiaire est résident
d'un logement-foyer.

II.-L'établissement habilité mentionné au 2° du I est :

1° L'établissement prêteur, lorsque le bénéficiaire est un propriétaire qui
a contracté un prêt unique entrant dans le champ d'application de l'article R. 832-5 ;

2° Lorsque le propriétaire a contracté plusieurs prêts, l'établissement qui a
accordé le prêt principal répondant aux critères de l'article R. 832-5 ;

3° Un autre établissement que celui mentionné au 2° du II, si le propriétaire
lui a donné mandat et qu'il répond à des caractéristiques définies par arrêté
des ministres chargés du logement, du budget, de la sécurité sociale et de
l'agriculture.

```catala
# Ne touche ni à l'égibilité ni au calcul du montant donc nous ne formaliserons
# pas pour l'instant.
```

####### Article D832-2 | LEGIARTI000041477269

Lorsque le bénéficiaire est locataire d'un logement compris dans un patrimoine
conventionné comportant moins de dix logements, l'aide personnalisée est versée
au bailleur ou au gestionnaire, s'il en fait la demande. Faute d'une telle demande,
elle est versée au locataire.

Dans les cas de sous-location prévus aux articles L. 353-20 et L. 442-8-1 , l'aide
personnalisée au logement est versée, sur leur demande, aux personnes morales locataires.

En outre, sauf si le bailleur ou l'établissement habilité en demande le versement
entre ses mains, elle est versée directement :

1° Au locataire ou à l'occupant de bonne foi, dans le cas prévu par l'article L. 353-9 ;

2° Au propriétaire qui a contracté plusieurs prêts et qui n'a pas donné mandat à un
établissement habilité, lorsqu'aucune des mensualités de remboursement de ces prêts
n'est supérieure ou égale, pour la première année, au montant de l'aide personnalisée ;

3° Aux personnes sous-locataires mentionnées au premier alinéa de l'article
L. 822-4 . Dans ce cas, le locataire titulaire du bail principal et sous-louant
une partie de son logement est assimilé au bailleur.

```catala
# Ne touche ni à l'égibilité ni au calcul du montant donc nous ne formaliserons
# pas pour l'instant.
```

####### Article D832-3 | LEGIARTI000038878762

Pour l'application des articles D. 832-1 et D. 832-2, est considéré comme un établissement
habilité :

1° Le vendeur, en cas de vente à terme, ou de location-accession ;

2° Les sociétés faisant l'objet des dispositions des articles L. 212-1 à L. 212-13 et
L. 213-1 à L. 213-15 , lorsqu'elles ont été bénéficiaires du prêt principal.

```catala
# Ne touche ni à l'égibilité ni au calcul du montant donc nous ne formaliserons
# pas pour l'instant.
```

####### Artcile D832-4 | LEGIARTI000038878760

Lorsque le bénéficiaire est un propriétaire ou un accédant à la propriété titulaire
d'un contrat de location-accession, l'aide personnalisée est versée :

1° A l'établissement habilité, en cas de prêt unique ;

2° A l'établissement habilité ou au bénéficiaire, lorsqu'il y a différents contrats de prêts.

```catala
# Ne touche ni à l'égibilité ni au calcul du montant donc nous ne formaliserons
# pas pour l'instant.
```

###### Section 2 : Accession à la propriété

####### Article R832-5 | LEGIARTI000039048901

```catala
# TODO juridique: est-ce que cet article correspond réellement à la définition
# des caractéristiques et conditions d'octroi du 1°) de L831-1 ?
# TODO juridique: vérifier que les conditions sont différentes entre les
# prêts
```

L'aide personnalisée au logement est accordée au propriétaire qui est titulaire et supporte
les charges correspondantes d'un :

1° Prêt aidé par l'Etat pour la construction, l'acquisition et l'amélioration des logements
en accession à la propriété défini à l'article D. 331-32 ;

2° Prêt conventionné défini à l'article D. 331-63 , dans les conditions précisées
par l'article D. 331-64.

```catala
champ d'application ÉligibilitéAidePersonnaliséeLogement:
  règle caractéristiques_prêt_l831_1_1 de prêt sous condition
    prêt.titulaire_prêt sous forme Demandeur et (
    prêt.type_prêt sous forme D331_32 ou
    prêt.type_prêt sous forme D331_63_64)
  conséquence rempli
```

####### Article R832-6 | LEGIARTI000038878754

Sous réserve des dispositions de l'article R. 823-14, l'aide est versée :

1° Soit, si le propriétaire occupe le logement à la date de la première échéance due au titre de ce prêt :

a) En cas de périodicité mensuelle, à compter du premier jour du mois civil suivant celui de cette échéance ;

b) En cas de périodicité supérieure au mois, à compter du premier jour du mois civil suivant le premier mois
de la période couverte par cette échéance ;

2° Soit, si le propriétaire n'occupe pas le logement à la date de la première échéance due au titre de ce prêt
ou si l'entrée dans les lieux intervient au cours de la période couverte par cette échéance, à compter du
premier jour du mois civil suivant celui au cours duquel se situe l'entrée dans les lieux.

```catala
# Ne touche ni à l'égibilité ni au calcul du montant donc nous ne formaliserons
# pas pour l'instant.
```

####### Article R832-7 | LEGIARTI000039048899

L'aide personnalisée est accordée à l'accédant à la propriété titulaire d'un contrat de location-accession,
lorsque le vendeur est titulaire d'un prêt défini par les articles D. 331-59-8 et suivants ou d'un prêt
défini par les articles D. 331-76-1 et suivants et que ce dernier supporte les charges correspondantes.

```catala
champ d'application ÉligibilitéAidePersonnaliséeLogement:
  règle caractéristiques_prêt_l831_1_1 de prêt sous condition
    prêt.titulaire_prêt sous forme
      VendeurQuandDemandeurAContratLocationAccession et (
    prêt.type_prêt sous forme D331_59_8 ou
    prêt.type_prêt sous forme D331_76_1)
  conséquence rempli
```

####### Article R832-8 | LEGIARTI000039048897

Sous réserve des dispositions de l'article R. 823-14 , l'aide est versée :

1° Soit, si l'accédant occupe le logement à la date de la première échéance due au titre du contrat
de location-accession :

a) En cas de périodicité mensuelle, à compter du premier jour du mois civil suivant celui de cette
première échéance ;

b) En cas de périodicité supérieure au mois, à compter du premier jour du mois civil suivant le premier
mois de la période couverte par cette échéance ;

2° Soit, si l'accédant n'occupe pas le logement à la date de la première échéance due au titre du
contrat de location-accession ou si l'entrée dans les lieux intervient au cours de la période couverte
par cette échéance, à compter du premier jour du mois civil suivant celui au cours duquel se situe
l'entrée dans les lieux.

Est pris en considération, pour le calcul de l'aide personnalisée, le montant de la redevance définie
au premier alinéa de l'article D. 331-59-16 et au II de l'article D. 331-76-5-1 .

```catala
# Ne touche ni à l'égibilité ni au calcul du montant donc nous ne formaliserons
# pas pour l'instant.
```

####### Article R832-9 | LEGIARTI000039048895

Le montant de l'aide personnalisée versée au propriétaire occupant bénéficiaire est révisé en
cours de période de paiement :

1° Lorsque la période de remboursement du prêt ouvrant droit à l'aide personnalisée fait
suite à une période de différé d'amortissement ;

2° Lors de chaque révision de la redevance, lorsque l'accédant est titulaire d'un contrat
de location-accession ;

3° Lors de chaque révision des charges de remboursement, lorsque le propriétaire est titulaire
d'un prêt aidé par l'Etat à taux révisable défini à l'article D. 331-54-1 ou d'un prêt
conventionné à taux révisable défini à l'article D. 331-75 .

```catala
# Ne touche ni à l'égibilité ni au calcul du montant donc nous ne formaliserons
# pas pour l'instant.
```

####### Article D832-10 | LEGIARTI000038878746

Pour les propriétaires bénéficiant d'un prêt ouvrant droit à l'aide personnalisée au logement
ou les titulaires d'un contrat de location-accession conclu dans les conditions prévues
au 6° de l'article L. 831-1 , le montant mensuel de l'aide est calculé selon la formule et
les modalités suivantes :

$$\textrm{Af} = \textrm{K} \times (\textrm{L} + \textrm{C}-\textrm{L0})$$

```catala
champ d'application CalculAidePersonnaliséeLogementAccessionPropriété:
  définition aide_finale_formule égal à
    soit aide_finale égal à
      (mensualité_éligible +€ montant_forfaitaire_charges_d832_10 -€
      mensualité_minimale) *€ coefficient_prise_en_charge_d832_10
    dans
    si aide_finale <€ 0 € alors 0 € sinon aide_finale
```

où :

1° " Af " est l'aide mensuelle issue de la formule de calcul ;

2° " K " est le coefficient de prise en charge, calculé selon les dispositions de l'article D. 832-11 ;

3° " L " est la mensualité éligible, déterminée selon les dispositions de l'article D. 832-12,
prise en compte dans la limite d'un plafond fixé par arrêté en fonction de la zone géographique,
de la composition familiale et de la finalité de l'opération ;

```catala
champ d'application CalculAidePersonnaliséeLogementAccessionPropriété:
  définition mensualité_éligible égal à
    si mensualité_principale >€ plafond_mensualité_d832_10_3
    alors plafond_mensualité_d832_10_3
    sinon mensualité_principale
```

4° " C " est le montant forfaitaire au titre des charges, fixé par arrêté en fonction de
la composition familiale ;

```catala
# Ne donne pas précisément la formule mais uniquement le principe général,
# on ne formalise pas ici.
```

5° " L0 " est la mensualité minimale, calculée selon les dispositions de l'article D. 832-15.

```catala
# Ne donne pas précisément la formule mais uniquement le principe général,
# on ne formalise pas ici.
```

Le montant ainsi calculé est minoré d'un montant fixé forfaitairement par arrêté.

```catala
champ d'application CalculAidePersonnaliséeLogementAccessionPropriété:
  définition traitement_aide_finale de aide_finale
  état minoration_forfaitaire
  égal à
    si aide_finale -€ montant_forfaitaire_d832_10 >=€ 0€
    alors
      aide_finale -€ montant_forfaitaire_d832_10
    sinon 0€
```

Ce résultat est minoré d'un abattement au titre de la dépense nette minimale, dont les modalités
de calcul sont fixées par l'article D. 832-17.

```catala
champ d'application CalculAidePersonnaliséeLogementAccessionPropriété:
  définition traitement_aide_finale de aide_finale état abattement égal à
    soit aide_finale égal à traitement_aide_finale de aide_finale dans
    soit aide_finale égal à
      aide_finale -€ abattement_dépense_nette_minimale_d832_10 de aide_finale
    dans
    si aide_finale >=€ 0€ alors aide_finale sinon 0€
```

Ce dernier résultat, obtenu par application des dispositions précédentes, est diminué d'un montant
représentatif des contributions sociales qui s'y appliquent, arrondi à l'euro inférieur,
puis majoré de ce montant représentatif.

```catala
champ d'application CalculAidePersonnaliséeLogementAccessionPropriété:
  définition traitement_aide_finale de aide_finale
  état contributions_sociales_arrondi
  égal à
    soit aide_finale égal à traitement_aide_finale de aide_finale dans
    soit crds égal à contributions_sociales.montant de aide_finale dans
    soit aide_finale_moins_crds_arrondie égal à
      arrondi_argent de ((aide_finale -€ crds) -€ 0,50€)
    dans
    si
      aide_finale_moins_crds_arrondie +€ crds >=€ 0€
    alors
      aide_finale_moins_crds_arrondie +€ crds
    sinon 0€
```

Lorsque le montant mensuel de l'aide, ainsi calculé, est inférieur à un montant fixé par arrêté,
il n'est pas procédé à son versement.

```catala
champ d'application CalculAidePersonnaliséeLogementAccessionPropriété:
  définition traitement_aide_finale de aide_finale état montant_minimal égal à
    soit aide_finale égal à traitement_aide_finale de aide_finale dans
    si aide_finale <€ montant_minimal_aide_d832_10
    alors 0 €
    sinon aide_finale
```

####### Article D832-11 | LEGIARTI000038878744

Le coefficient " K ", mentionné au 2° de l'article D. 832-10, est ainsi calculé selon la formule
et les modalités suivantes :

$$\textrm{K} =  0,95 - \frac{\textrm{R}}{\textrm{cm} \times \textrm{N}}$$

```catala
champ d'application CalculAidePersonnaliséeLogementAccessionPropriété:
  définition coefficient_prise_en_charge_d832_10 état formule égal à
    0,95 -.
      (ressources_ménage_arrondies /€
        (coefficient_multiplicateur_d832_11 *€ n_nombre_parts_d832_11)
      )
  # Attention: ici coefficient_multiplicateur_d832_11 doit être un montant
  # d'argent sinon problème d'homogénéité dans la formule.
```

où :

1° " K " est le coefficient de prise en charge, arrondi à deux décimales par défaut.
Lorsque le calcul le porte à une valeur supérieure à 0,95, il est considéré comme égal à 0,95 ;

```catala
champ d'application CalculAidePersonnaliséeLogementAccessionPropriété:
  définition coefficient_prise_en_charge_d832_10 état arrondi égal à
    (arrondi_décimal de ((coefficient_prise_en_charge_d832_10 -. 0,005)
      *. 100,0)) /. 100,0
  définition coefficient_prise_en_charge_d832_10 état seuil égal à
    si coefficient_prise_en_charge_d832_10 >=. 0,95 alors 0,95 sinon
      coefficient_prise_en_charge_d832_10
```

2° " R " représente la limite supérieure de l'intervalle dans lequel se situent les ressources
du ménage, appréciées conformément aux dispositions de la section 2 du chapitre II du titre
II du présent livre et arrondies à la centaine d'euros supérieure ;

```catala
# Pourquoi nous parle-t-on de limite supérieure d'intervalle ici ?
# Il n'y a aucun intervalle de ressources définis à la section 2 du chapitre
# II du présent livre. Est-ce que c'est juste redondant avec l'arrondi à la
# centaine d'euros supérieure (intervalles de 100 €) ?
# Réponse de DGALN/DHUP/FE4 le 25/05/2022:
# "L’intervalle correspond bien à l’arrondi de l’assiette ressources à la
# centaine d’euros supérieure. La rédaction, issue de la recodification,
# est en effet une reprise peut-être maladroite et perfectible de l’ancien
# article (avant recodification, abrogé depuis) R. 351-19 qui est plus clair
# sur cette notion d’intervalle :
#   « Le coefficient K, au plus égal à 0,95, est déterminé pour chaque
#   intervalle de ressources de 100 euros en appliquant la formule suivante :
#     K = 0,95-R/ CM x N
#   dans laquelle :
#     R représente la limite supérieure de l'intervalle dans lequel se situent
#     les ressources appréciées conformément à l'article R. 351-5 ; […] »
# La manière dont nous interprétons cette réponse est la suivante : toutes
# ces circonvolutions autour des limites supérieures des tranches reviennent
# bien à faire un arrondi à la centaine d'euros supérieure.
```

3° " cm " est un coefficient multiplicateur fixé par arrêté ;

4° " N " représente le nombre de parts déterminé selon le barème suivant :

------------------------------------------------------------ ---
bénéficiaire isolé                                           1,4
ménage sans personne à charge                                1,8
bénéficiaire isolé ou ménage ayant une personne à charge     2,5
bénéficiaire isolé ou ménage ayant deux personnes à charge   3,0
bénéficiaire isolé ou ménage ayant trois personnes à charge  3,7
bénéficiaire isolé ou ménage ayant quatre personnes à charge 4,3
majoration par personne à charge supplémentaire              0,5
------------------------------------------------------------ ---

```catala
champ d'application CalculNombrePartsAccessionPropriété:
  définition n_nombre_parts_d832_11 égal à
    si nombre_personnes_à_charge = 0 alors
      selon situation_familiale_calcul_apl sous forme
      -- PersonneSeule: 1,4
      -- Couple: 1,8
    sinon (si nombre_personnes_à_charge = 1 alors
      2,5
    sinon (si nombre_personnes_à_charge = 2 alors
      3,0
    sinon (si nombre_personnes_à_charge = 3 alors
      3,7
    sinon (si nombre_personnes_à_charge = 4 alors
      4,3
    sinon
      ( 4,3 +. (0,5 *. (entier_vers_décimal de
        (nombre_personnes_à_charge - 4))))
    ))))

champ d'application CalculAidePersonnaliséeLogementAccessionPropriété:
  définition n_nombre_parts_d832_11 égal à
    calcul_nombre_parts.n_nombre_parts_d832_11
  définition calcul_nombre_parts.nombre_personnes_à_charge égal à
    nombre_personnes_à_charge
  définition calcul_nombre_parts.situation_familiale_calcul_apl égal à
    situation_familiale_calcul_apl
```

####### Article D832-12 | LEGIARTI000039048893

I.-La mensualité " L ", définie au 3° de l'article D. 832-10 , est déterminée selon
les modalités suivantes, sur une base mensuelle :

La mensualité est la somme :

1° Des charges d'intérêts, ou des charges d'intérêts et d'amortissement et des charges
accessoires au principal de la dette afférente aux prêts définis par les articles
D. 331-32 et suivants et aux prêts complémentaires définis par arrêté. Ces prêts
doivent avoir fait l'objet, pour chacun d'entre eux, d'un certificat daté et notifié
au demandeur par les organismes prêteurs, précisant les modalités ainsi que la périodicité
des paiements, présenté à l'appui de la demande d'aide personnalisée au logement.

Lorsque le prêt ouvrant droit à l'aide personnalisée au logement est un prêt aidé par
l'État en accession à la propriété accordé pour l'agrandissement ou un prêt conventionné
accordé pour l'amélioration du logement, le prêt souscrit antérieurement aux fins de
construction ou d'acquisition de ce logement est assimilé à un prêt complémentaire ;

2° Des primes d'assurance accessoires aux contrats de prêts, notamment de l'assurance décès,
de l'assurance couvrant les risques d'invalidité, de l'assurance pour pertes pécuniaires
résultant du report d'échéances de sommes dues en cas de chômage, contractées par le
bénéficiaire en garantie de l'exécution des engagements souscrits ;

II.-Pour les titulaires d'un contrat de location-accession conclu dans les conditions
prévues au 6° de l'article L. 831-1 , la redevance de location-accession, définie au
premier alinéa de l'article D. 331-59-16 et au II de l'article D. 331-76-5-1 , est
assimilée à la mensualité.

```catala
# Cet article précise ce qu'il faut prendre en compte pour la mensualité du
# prêt mais il ne nous paraît pas nécessaire de le transcrire en Catala
# à ce stade, puisque l'on peut laisser à l'utilisateur de rentrer
# correctement la mensualité + prime d'assurance sur le formulaire d'entrée.
```

####### Article D832-13 | LEGIARTI000038878740

Lorsque le prêt ouvrant droit à l'aide personnalisée est un prêt conventionné
accordé pour l'amélioration du logement et que le prêt souscrit antérieurement pour
l'acquisition de ce logement est assimilé à un prêt complémentaire dans les conditions
prévues au 1° du I de l'article D. 832-12 , la mensualité de référence est celle prévue
pour les logements acquis et améliorés à l'aide d'un prêt conventionné.

```catala
# Cet article précise comment remplir la case "mensualité" du formulaire
# pour un cas particulier, pas pertinent à traduire en Catala.
```

####### Article D832-14 | LEGIARTI000038878738

Pour le calcul de la mensualité " L ", le plafond mensuel mentionné au 3° de l'article
D. 832-10 est applicable pour la période au titre de laquelle le certificat prévu à
l'article D. 832-12 a été établi.

```catala
# Voir l'article 18 de l'arrêté du 27 septembre 2019 qui précise le sens de
# cet alinéa en termes de calcul.
champ d'application CalculAidePersonnaliséeLogementAccessionPropriété:
  définition plafond_mensualité_d832_10_3 état base égal à
    calcul_plafond_mensualité_d832_10_3 de date_signature_prêt
```

Le plafond ainsi retenu ne peut, en aucun cas, être inférieur à celui applicable au
moment de l'entrée dans les lieux, sous la réserve qu'il s'agisse d'un local habité
pour la première fois par le bénéficiaire.

```catala
# Ce plafond est défini par l'article 18 de l'arrêté du 27 septembre 2019.
# Sauf que cet arrêté définit le plafond en fonction de la date de signature du
# prêt. La manière dont nous interprétons le deuxième alinéa ci-dessus est la
# suivante : puisque le plafond dépend d'une date (normalement de signature
# du prêt), et qu'en fonction de la date ce plafond varie à la hausse ou à la
# baisse, l'alinéa donne une petite faveur aux propriétaires puisqu'il leur
# permet de choisir la date la plus avantageuse pour calculer le plafond
# entre la date de signature du prêt et celle d'entrée dans le logement si
# le local est habité pour la première fois par le bénéficiaire.
champ d'application CalculAidePersonnaliséeLogementAccessionPropriété:
  exception définition plafond_mensualité_d832_10_3 état base sous condition
    local_habité_première_fois_bénéficiaire
  conséquence égal à
    soit plafond_signature égal à
      calcul_plafond_mensualité_d832_10_3 de date_signature_prêt
    dans
    soit plafond_entrée égal à
      calcul_plafond_mensualité_d832_10_3 de date_entrée_logement
    dans
    si plafond_signature <€ plafond_entrée
    alors plafond_entrée
    sinon plafond_signature
```

####### Article D832-15 | LEGIARTI000039621211

La mensualité minimale " L0 ", mentionnée au 5° de l'article D. 832-10 est calculée :

1° Pour les logements construits, agrandis, aménagés à partir de locaux non destinés
à l'habitation, acquis et améliorés, occupés par leur propriétaire ou par l'accédant
titulaire d'un contrat de location-accession, par l'application de pourcentages à des
tranches de ressources dont les limites inférieures et supérieures sont multipliées
par le nombre de parts " N " défini au 4° de l'article D. 832-11. Ce résultat est
divisé par douze. Les pourcentages et les tranches sont fixés par arrêté selon la
date de signature du contrat de prêt ou de location-accession. Les ressources sont
appréciées selon les modalités prévues à la section 2 du chapitre II du titre II du
présent livre et arrondies à la centaine d'euros supérieure. Les pourcentages et le
coefficient " N " sont appliqués à la limite supérieure de l'intervalle dans lequel
se situent les ressources ;

```catala
# Que veut dire la phrase "Les pourcentages
# et le coefficient " N " sont appliqués à la limite supérieure de
# l'intervalle dans lequel se situent les ressources" ? Parce que pour la
# tranche la plus haute il n'y a pas de limite supérieure, donc qu'est-ce
# qu'on fait ? Est-ce qu'on prend comme revenu la limite supérieure de
# la tranche sauf pour la dernière ? Ambiguité.
# Réponse de DGALN/DHUP/FE4 le 25/05/2022:
# "Comme pour le D. 832-11, l’intervalle fait référence à l’arrondi de R à la
#  centaine d’euros supérieure (sinon cela n’aurait effectivement pas de sens
#  pour la dernière tranche).
#  La précision de la notion d’intervalle apparaissait dans les articles avant
#  recodification :
#  - ex R. 351-21 (pour le D. 832-15)
#  « […] Le loyer minimum L0 est déterminé pour chaque intervalle de ressources
#    de 100 euros mentionné à l'article R. 351-19. Les pourcentages et le
#    coefficient N prévus au premier alinéa du présent article sont appliqués
#    à la limite supérieure de l'intervalle dans lequel se situent les
#    ressources appréciées conformément à l'article R. 351-5. »
#  - ex R. 351-62 (pour le D. 832-26)
#  « […] L'équivalence de loyer et de charges minima est déterminée pour chaque
#    intervalle de ressources de 100 euros mentionné à l'article R. 351-61.
#    Les pourcentages et le coefficient N prévus au premier alinéa du présent
#    article sont appliqués à la limite supérieure de l'intervalle dans lequel
#    se situent les ressources appréciées conformément à l'article R. 351-5. »"
# La manière dont nous interprétons cette réponse est la suivante : toutes
# ces circonvolutions autour des limites supérieures des tranches reviennent
# bien à faire un arrondi à la centaine d'euros supérieure.

champ d'application CalculAidePersonnaliséeLogementAccessionPropriété:
  définition mensualité_minimale sous condition
    type_travaux_logement sous forme TravauxPourAcquisitionD832_15_1 ou
    # Comment interpetéter le R832-5 dans
    # le cas d'un prêt conventionné D331-63 alinéa 3° où le prêt finance
    # l'acquisition d'un logement existant sans travaux et donc sans
    # amélioration; le demandeur est-il éligible à l'APL de ce cas et si oui,
    # peut-on nous confirmer que le montant de la mensualité minimale est bien
    # calculée selon l'alinéa 1° de D832-15 ?
    # Réponse de DGALN/DHUP/FE4 le 25/05/2022:
    # "Le demandeur est éligible à l’APL qui est calculée selon l’alinéa 1°
    #  du D832-15. Le 2° ne concerne que les travaux d’amélioration."
    # La réponse à notre question est bien oui pour les deux items,
    # d'où la ligne ci-dessous.
    type_travaux_logement sous forme TypeTravauxLogementD832_15.PasDeTravaux
  conséquence égal à
    # Selon un mail de DGALN/DHUP/FE4 du 19/07/2022, il nest nécessaire pour le
    # calcul de cette mensualité minimale de garder une précision infinie
    # durant toutes les étapes du calcul. En Catala, nous utilisons donc le type
    # décimal plutôt que argent qui arrondi systématiquement au centime près
    # à chaque étape de calcul.
    soit ressources_ménage_arrondies égal à
      argent_vers_décimal de ressources_ménage_arrondies
    dans
    soit montant_limite_tranches_d832_15_1 égal à
      argent_vers_décimal de montant_limite_tranches_d832_15_1
    dans
    décimal_vers_argent de (
      ((si
        # Pour la tranche supérieure
        ressources_ménage_arrondies >=.
          (montant_limite_tranches_d832_15_1 *. n_nombre_parts_d832_11)
      alors
        ((ressources_ménage_arrondies -.
          (montant_limite_tranches_d832_15_1 *. n_nombre_parts_d832_11)) *.
            taux_tranche_supérieure_d832_15_1)
      sinon 0,0) +.
      (si
        # Pour la tranche inférieure
        ressources_ménage_arrondies <=.
          montant_limite_tranches_d832_15_1 *. n_nombre_parts_d832_11
      alors
        ressources_ménage_arrondies *. taux_tranche_inférieure_d832_15_1
      sinon
        ((montant_limite_tranches_d832_15_1 *. n_nombre_parts_d832_11) *.
            taux_tranche_inférieure_d832_15_1))) /. 12,0)
# PS: les ressources ont déjà été arrondies, voir D823-17.
```

2° Pour les logements existants améliorés et occupés par leur propriétaire, selon
les dispositions du premier alinéa de l'article D. 832-26 dans lesquelles la
mensualité minimale " L0 " se substitue à l'équivalence minimale de loyer et de
charges " E0 ".

```catala
champ d'application CalculAidePersonnaliséeLogementAccessionPropriété:
  définition mensualité_minimale sous condition
    type_travaux_logement sous forme TravauxSurLogementDéjàAcquisD832_15_2
  conséquence égal à
    calcul_équivalence_loyer_minimale.montant

  # Mettre la condition à "faux" permet d'activer le premier alinéa de D832-26
  définition calcul_équivalence_loyer_minimale.condition_2_du_832_25 égal à faux
  définition calcul_équivalence_loyer_minimale.ressources_ménage_arrondies
    égal à ressources_ménage_arrondies
  définition calcul_équivalence_loyer_minimale.n_nombre_parts_d832_25 égal à
    n_nombre_parts_d832_11
```

####### Article D832-16 | LEGIARTI000041477280

Dans le cas du calcul de l'aide personnalisée des copropriétaires prévu à l'article R. 821-4 :

1° La mensualité " L " représente le quotient des mensualités par le nombre de copropriétaires,
cotitulaires du prêt ouvrant droit à l'aide personnalisée, le résultat étant pris en compte dans
la limite de la mensualité plafond prévue au 3° de l'article D. 832-10 qui correspond à
la situation familiale de chacun des intéressés ;

2° Il est fait application à chaque personne ou ménage concerné du coefficient " N " défini
au 4° de l'article D. 832-11 et de l'élément " C " défini au 4° de l'article D. 832-10,
qui correspondent à sa situation familiale.

Les arrêtés fixant les plafonds de mensualité mentionnés au 3° de l'article D. 832-10
et les montants forfaitaires au titre des charges mentionnées au 4° du même article
peuvent fixer des montants spécifiques pour les ménages copropriétaires.

```catala
# Dans R821-4, nous avons déjà dit qu'il s'agissait simplement pour tous les
# co-propriétaires de remplir séparément des formulaires de demande APL.
# Cet article précise simplement qu'il faut diviser la mensualité selon
# le nombre de copropriétaires mais cela semble logique et nous laissons
# donc ce pré-calcul aux utilisateurs.
```

####### Article D832-17 | LEGIARTI000038878732

Pour les contrats de prêts et contrats de location-accession signés après le 30 juin 1999,
la dépense nette minimale mentionnée au dixième alinéa de l'article D. 832-10 est obtenue
en déduisant de la mensualité déclarée, majorée du montant forfaitaire des charges,
le montant mensuel de l'aide calculé selon les dispositions des deuxième à neuvième
alinéas du même article.

```catala
champ d'application CalculAidePersonnaliséeLogementAccessionPropriété:
  exception
  définition dépense_nette_minimale_d832_10 de aide_calculée_2_9_d832_10
  sous condition
    date_signature_prêt >=@ |1999-06-30|
  conséquence égal à
    mensualité_principale +€ montant_forfaitaire_charges_d832_10 -€
      aide_calculée_2_9_d832_10
```

Dans les autres cas, cette dépense nette est obtenue en déduisant de la seule mensualité
déclarée le montant mensuel de l'aide calculé de la même façon.

```catala
champ d'application CalculAidePersonnaliséeLogementAccessionPropriété:
  définition dépense_nette_minimale_d832_10 de aide_calculée_2_9_d832_10 égal à
    mensualité_principale -€ aide_calculée_2_9_d832_10
```

Lorsque la dépense nette ainsi calculée est inférieure au produit des ressources et d'un
coefficient fixé par arrêté, selon la date de signature du contrat et la finalité de
l'opération, il est appliqué un abattement sur le montant mensuel de l'aide égal à la
différence constatée.

```catala
champ d'application CalculAidePersonnaliséeLogementAccessionPropriété:
  définition abattement_dépense_nette_minimale_d832_10
    de aide_calculée_2_9_d832_10
  égal à
    soit dépense_nette_minimale égal à
      dépense_nette_minimale_d832_10 de aide_calculée_2_9_d832_10
    dans
    si dépense_nette_minimale<=€
      ressources_ménage_avec_d832_18 *€ coefficient_multiplicateur_d832_17_3
    alors
      ressources_ménage_avec_d832_18 *€ coefficient_multiplicateur_d832_17_3 -€
        dépense_nette_minimale
    sinon
      0€
```

Les ressources sont appréciées selon les modalités prévues à la section 2 du chapitre
II du titre II du présent livre et, le cas échéant, à l'article D. 832-18 , et arrondies
à la centaine d'euros supérieure.

####### Article D832-18 | LEGIARTI000038878730

Si les ressources du bénéficiaire et de son conjoint déterminées selon les modalités
prévues à la section 2 du chapitre II du titre II du présent livre sont inférieures
à un montant déterminé par le produit d'un coefficient, fixé par arrêté, et des
mensualités déclarées, les ressources sont réputées égales à ce montant, sauf lorsque
les intéressés se trouvent dans l'une des situations mentionnées aux articles R. 822-11
et R. 822-13 à R. 822-17 .

```catala
# Les ressources mentionnées ici prennent-elles en compte
# les ressources des personnes à charge autre que le bénéficiaire et son
# conjoint ?
# Non, selon réponse de DGALN/DHUP/FE4 du 25/05/2022.
champ d'application CalculAidePersonnaliséeLogementAccessionPropriété:
  définition ressources_ménage_avec_d832_18 égal à
    si ressources_ménage_arrondies <=€ mensualité_principale *€
      coefficient_multiplicateur_d832_18
    alors
      mensualité_principale *€ coefficient_multiplicateur_d832_18
    sinon ressources_ménage_arrondies

  exception définition ressources_ménage_avec_d832_18 sous condition
    situation_r822_11_13_17
  conséquence égal à ressources_ménage_arrondies
```

####### Article D832-19 | LEGIARTI000038878728

Les arrêtés mentionnés dans la présente section sont pris par les ministres chargés du
logement, du budget, de la sécurité sociale et de l'agriculture.

Le zonage géographique est fixé par arrêté des ministres chargés du logement et du budget.

```catala
# Pas pertinent pour le calcul.
```

###### Section 3 : Logements-foyers

####### Sous-Section 1 : Conditions d'assimilation des logements-foyers aux logements à usage locatif

######## Article R832-20 | LEGIARTI000038878722

La présente section ne s'applique qu'à ceux des logements-foyers mentionnés à l'article
L. 633-1 qui fournissent, le cas échéant, diverses prestations annexes telles que
blanchissage, service de soins ou services socio-éducatifs moyennant une redevance.

Ces logements-foyers sont :

1° Les logements-foyers accueillant, à titre principal, des personnes handicapées ou des
personnes âgées ;

2° Les résidences sociales ;

3° Les logements-foyers accueillant, à titre principal, des jeunes travailleurs ou des
travailleurs migrants et ayant fait l'objet d'une convention, prévue à l'article L. 353-2 ,
signée avant le 1er janvier 1995.

```catala-metadata
déclaration énumération TypeLogementFoyer:
  -- LogementPersonnesÂgéesOuHandicapées
  -- RésidenceSociale
  -- FoyerJeunesTrvailleursOuMigrantsConventionnéL353_2Avant1995
  -- Autre # TODO juridique: confirmer la présence de ce variant autre
           # qui correspond aux logement mentionnés au R832-21, bien que
           # la formulation de R832-20 semble impliquer que seuls les 3
           # variants du dessus existent.
```

######## Article R832-21 | LEGIARTI000039048891

Pour l'application du 5° de l'article L. 831-1 , peuvent être assimilés à des logements
à usage locatif :

1° Les logements-foyers existants dont la construction a été financée :

a) Soit dans les conditions prévues par les articles L. 313-1 , L. 411-1 , R. 311-1 et
R. 431-49 sous réserve que, lorsque des crédits collectés au titre de la participation
des employeurs à l'effort de construction ont constitué le seul financement entrant dans
le champ d'application des articles précités, le montant de ces crédits doit représenter
au moins 20 % du coût de la construction ;

b) Soit au moyen des subventions accordées sur le budget du ministre chargé de la santé,
représentant au moins 20 % du coût de la construction ;

2° Les logements-foyers existants dont l'amélioration ou l'acquisition suivie d'une
amélioration est financée :

a) Soit dans les conditions prévues par la section 1 du chapitre Ier du titre III du livre III ;

b) Soit dans les conditions prévues par la section 1 du chapitre III du titre II du livre III ;

c) Soit au moyen de crédits collectés au titre de la participation des employeurs à l'effort
de construction dans les conditions prévues par le troisième alinéa de l'article L. 313-1
dans le cadre d'une opération ayant fait l'objet d'un agrément des ministres chargés du
logement et des travailleurs immigrés ;

d) Soit au moyen de subventions accordées sur le budget du ministre chargé de la santé ou
de la caisse nationale de solidarité pour l'autonomie représentant au moins 20 % du coût
des travaux d'amélioration pouvant faire l'objet d'une subvention ou du coût de l'opération
d'acquisition-amélioration.

Il en est de même des immeubles améliorés ou acquis et améliorés aux fins de transformation
en logements-foyers avec le bénéfice des financements mentionnés ci-dessus ;

3° Les logements-foyers neufs dont la construction est financée :

a) Soit dans les conditions prévues par la section 1 du chapitre Ier du titre III du livre III ;

b) Soit au moyen de subventions accordées sur le budget du ministère chargé de la santé
représentant au moins 20 % du coût de la construction ;

4° Les établissements d'hébergement mentionnés au III de l'article R. 321-12 , dès lors que
leurs caractéristiques techniques respectent celles des logements-foyers prévues par l'arrêté
mentionné à l'article D. 331-8 et que la convention prévue au II de l'article R. 321-20 est
remplacée par la convention prévue au III de l'article R. 353-159 .

```catala
champ d'application ÉligibilitéAidePersonnaliséeLogement:
  règle condition_logement_bailleur sous condition
    selon ménage.logement.mode_occupation sous forme
      -- RésidentLogementFoyer de logement_foyer:
        logement_foyer.remplit_conditions_r832_21
        # On a regroupé toutes les conditions au dessus dans une seule
        # variable, le calcul n'étant pas ici pertinent.
      -- n'importe quel: faux
  conséquence rempli
```

######## Article R832-22 | LEGIARTI000038878718

Sont assimilés à des logements locatifs en application du 5° de l'article L. 831-1 ,
les logements-foyers qui répondent à l'une des conditions fixées aux articles R. 832-20
et R. 832-21 et font l'objet d'une convention passée dans les conditions prévues à
la section 7 du chapitre III du titre V du livre III.

```catala
# Article redondant, rien à coder ici.
```

####### Sous-Section 2 : Conditions d'octroi de l'aide personnalisée au logement aux personnes résidant dans un logement-foyer

######## Article R832-23 | LEGIARTI000038878714

Par dérogation à l'article R. 823-10 , l'aide est due à l'occupant d'un logement-foyer mentionné
aux 2° et 3° de l'article R. 832-20 à partir du premier jour du premier mois civil pour lequel
cet occupant acquitte l'intégralité de la redevance mensuelle prévue par le titre d'occupation,
sous réserve que les autres conditions d'ouverture du droit soient réunies à cette date.

```catala
# On ne cherche pas à coder pour l'instant le calcul du calendrier d'ouverture
# des droits dans les différentes situations.
```

######## Article D832-24 | LEGIARTI000038878712

Pour les ménages résidant dans un logement-foyer mentionné à l'article R. 832-22 , le montant
mensuel de l'aide est calculé selon la formule et les modalités suivantes :

$$\textrm{Af} = \textrm{K} \times (\textrm{E}-\textrm{E0})$$

```catala
champ d'application CalculAidePersonnaliséeLogementFoyer:
  définition aide_finale_formule égal à
    soit aide_finale égal à
      (équivalence_loyer_éligible -€ équivalence_loyer_minimale) *€
        coefficient_prise_en_charge_d832_25
    dans
    si aide_finale <€ 0 € alors 0 € sinon aide_finale
```

où :

1° " Af " est l'aide mensuelle issue de la formule de calcul ;

2° " K " est le coefficient de prise en charge ; il est calculé selon les dispositions de
l'article D. 832-25 ;

3° " E " est l'équivalence de loyer et de charges locatives éligible, définie aux articles
R. 353-156 à R. 353-160 , et prise en compte dans la limite d'un plafond fixé par arrêté en fonction
de la zone géographique et de la composition familiale ;

```catala
champ d'application CalculAidePersonnaliséeLogementFoyer:
  définition équivalence_loyer_éligible égal à
    si redevance >=€ plafond_équivalence_loyer_éligible alors
      plafond_équivalence_loyer_éligible
    sinon
      redevance
```

4° " E0 " est l'équivalence de loyer et de charges locatives minimale ; elle est calculée selon
les dispositions de l'article D. 832-26.

```catala
champ d'application CalculAidePersonnaliséeLogementFoyer:
  définition équivalence_loyer_minimale égal à
    calcul_équivalence_loyer_minimale.montant
  définition calcul_équivalence_loyer_minimale.condition_2_du_832_25 égal à
    condition_2_du_832_25
  définition calcul_équivalence_loyer_minimale.ressources_ménage_arrondies
    égal à ressources_ménage_arrondies
  définition calcul_équivalence_loyer_minimale.n_nombre_parts_d832_25 égal à
    n_nombre_parts_d832_25
```

Le montant ainsi calculé est minoré d'un montant fixé forfaitairement par arrêté.

```catala
champ d'application CalculAidePersonnaliséeLogementFoyer:
  définition traitement_aide_finale de aide_finale
  état minoration_forfaitaire
  égal à
    si aide_finale -€ montant_forfaitaire_d832_24 >=€ 0€
    alors
      aide_finale -€ montant_forfaitaire_d832_24
    sinon 0€
```

Ce résultat est minoré d'un abattement au titre de la dépense nette minimale, dont les modalités
de calcul sont fixées à l'article D. 832-27.

```catala
champ d'application CalculAidePersonnaliséeLogementFoyer:
  définition traitement_aide_finale de aide_finale état abattement égal à
    soit aide_finale égal à traitement_aide_finale de aide_finale dans
    soit aide_finale égal à
      aide_finale -€  abattement_dépense_nette_minimale_d832_27 de aide_finale
    dans
    si aide_finale >=€ 0€ alors aide_finale sinon 0€
```

Ce dernier résultat est diminué d'un montant représentatif des contributions sociales qui
s'y appliquent, arrondi à l'euro inférieur, puis majoré de ce montant représentatif.


```catala
champ d'application CalculAidePersonnaliséeLogementFoyer:
  définition traitement_aide_finale de aide_finale
  état contributions_sociales_arrondi
  égal à
    soit aide_finale égal à traitement_aide_finale de aide_finale dans
    soit crds égal à
      contributions_sociales.montant de aide_finale
    dans
    soit aide_finale_moins_crds_arrondie égal à
       arrondi_argent de ((aide_finale -€ crds) -€ 0,50€)
    dans si
      aide_finale_moins_crds_arrondie +€ crds >=€ 0€
    alors
      aide_finale_moins_crds_arrondie +€ crds
    sinon 0€
```

Lorsque le montant mensuel de l'aide, calculé selon les modalités précédentes, est inférieur
à un montant fixé par arrêté, il n'est pas procédé à son versement.

```catala
champ d'application CalculAidePersonnaliséeLogementFoyer:
  définition traitement_aide_finale de aide_finale état montant_minimal égal à
    soit aide_finale égal à traitement_aide_finale de aide_finale dans
    si aide_finale <€ montant_minimal_aide_d823_24
    alors 0 € sinon aide_finale
```

######## Article D832-25 | LEGIARTI000038878710

Le coefficient " K ", défini au 2° de l'article D. 832-24, est calculé selon la formule et
les modalités précisées au 1° du présent article.

Toutefois, pour les logements-foyers de jeunes travailleurs conventionnés après le 30 septembre 1990
et pour les résidences sociales conventionnées après le 31 décembre 1994, en application du 5°
de l'article L. 831-1 , et mentionnés au 1° de l'article R. 832-21 , le coefficient " K " est calculé
selon la formule et les modalités précisées au 2° du présent article.

```catala
champ d'application CalculAidePersonnaliséeLogementFoyer:
  définition condition_2_du_832_25 égal à
    selon type_logement_foyer sous forme
    -- FoyerJeunesTrvailleursOuMigrantsConventionnéL353_2Avant1995:
      date_conventionnement >=@ |1990-09-30|
    -- RésidenceSociale:
      date_conventionnement >=@ |1994-12-31|
    -- n'importe quel: faux
```

1°

$$\textrm{K} = 0,95 - \frac{\textrm{R} - \textrm{r} \times \textrm{N}}{\textrm{cm} \times \textrm{N}}$$

```catala
champ d'application CalculAidePersonnaliséeLogementFoyer:
  définition coefficient_prise_en_charge_d832_25 état formule égal à
    soit dénominateur égal à ressources_ménage_arrondies -€
        coefficient_r_d832_25 *€ n_nombre_parts_d832_25
    dans
    soit dénominateur égal à
      si dénominateur <€ 0€ alors 0€ sinon dénominateur
    dans
    0,95 -.
      (dénominateur /€
        (coefficient_multiplicateur_d832_25 *€ n_nombre_parts_d832_25)
      )
```

où :

a) " K " est le coefficient de prise en charge, arrondi à deux décimales par défaut. Lorsque le
calcul le porte à une valeur supérieure à 0,95, il est considéré égal à 0,95 ;


```catala
champ d'application CalculAidePersonnaliséeLogementFoyer:
  définition coefficient_prise_en_charge_d832_25 état arrondi égal à
    (arrondi_décimal de ((coefficient_prise_en_charge_d832_25 -. 0,005)
      *. 100,0)) /. 100,0
  définition coefficient_prise_en_charge_d832_25 état seuil égal à
    si coefficient_prise_en_charge_d832_25 >=. 0,95 alors 0,95 sinon
      coefficient_prise_en_charge_d832_25
```

b) " R " représente la limite supérieure de l'intervalle dans lequel se situent les ressources
du ménage, appréciées selon les modalités prévues à la section 2 du chapitre II du titre II du
présent livre et arrondies à la centaine d'euros supérieure ;

c) " r " est un coefficient fixé par arrêté ;

d) " cm " est un coefficient multiplicateur fixé par arrêté ;

e) " N " représente le nombre de parts déterminé selon le barème suivant :

------------------------------------------------------------  ---
bénéficiaire isolé                                            1,4
ménage sans personne à charge	                                1,8
bénéficiaire isolé ou ménage ayant une personne à charge	    2,5
bénéficiaire isolé ou ménage ayant deux personnes à charge	  3,0
bénéficiaire isolé ou ménage ayant trois personnes à charge	  3,7
bénéficiaire isolé ou ménage ayant quatre personnes à charge  4,3
majoration par personne à charge supplémentaire	              0,5
------------------------------------------------------------- ---

```catala
champ d'application CalculNombrePartLogementFoyer:
  définition n_nombre_parts_d832_25 égal à
    si nombre_personnes_à_charge = 0 alors
      selon situation_familiale_calcul_apl sous forme
      -- PersonneSeule: 1,4
      -- Couple: 1,8
    sinon (si nombre_personnes_à_charge = 1 alors
      2,5
    sinon (si nombre_personnes_à_charge = 2 alors
      3,0
    sinon (si nombre_personnes_à_charge = 3 alors
      3,7
    sinon (si nombre_personnes_à_charge = 4 alors
      4,3
    sinon
      ( 4,3 +. (0,5 *. (entier_vers_décimal de
        (nombre_personnes_à_charge - 4))))
    ))))

champ d'application CalculAidePersonnaliséeLogementFoyer:
  définition calcul_nombre_parts.nombre_personnes_à_charge égal à
    nombre_personnes_à_charge
  définition calcul_nombre_parts.situation_familiale_calcul_apl égal à
    situation_familiale_calcul_apl
  définition calcul_nombre_parts.condition_2_du_832_25 égal à
    condition_2_du_832_25
  définition n_nombre_parts_d832_25 égal à
    calcul_nombre_parts.n_nombre_parts_d832_25
```

2°

$$\textrm{K} = 0,90 - \frac{\textrm{R}}{\textrm{cm} \times \textrm{N}}$$

```catala
champ d'application CalculAidePersonnaliséeLogementFoyer:
  exception définition coefficient_prise_en_charge_d832_25 état formule
  sous condition
    condition_2_du_832_25
  conséquence égal à
    0,90 -.
      (ressources_ménage_arrondies /€
        (coefficient_multiplicateur_d832_25 *€ n_nombre_parts_d832_25)
      )
```
où :

a) " K " est le coefficient de prise en charge, arrondi à deux décimales par défaut ;
lorsque le calcul le porte à une valeur supérieure à 0,90, il est considéré égal à 0,90 ;


```catala
champ d'application CalculAidePersonnaliséeLogementFoyer:
  exception définition coefficient_prise_en_charge_d832_25 état arrondi
  sous condition
    condition_2_du_832_25
  conséquence égal à
    (arrondi_décimal de ((coefficient_prise_en_charge_d832_25 -. 0,005)
      *. 100,0)) /. 100,0
  exception définition coefficient_prise_en_charge_d832_25 état seuil
  sous condition
    condition_2_du_832_25
  conséquence égal à
    si coefficient_prise_en_charge_d832_25 >=. 0,90 alors 0,90 sinon
      coefficient_prise_en_charge_d832_25
```

b) " R " représente la limite supérieure de l'intervalle dans lequel se situent les
ressources du ménage, appréciées selon les modalités prévues à la section 2 du
chapitre II du titre II du présent livre et arrondies à la centaine d'euros supérieure ;

c) " cm " est un coefficient multiplicateur fixé par arrêté ;

d) " N " représente le nombre de parts déterminé selon le barème suivant :

------------------------------------------------------------ ---
bénéficiaire isolé	                                         1,2
ménage sans personne à charge	                               1,5
bénéficiaire isolé ou ménage ayant une personne à charge	   2,5
bénéficiaire isolé ou ménage ayant deux personnes à charge	 3,0
bénéficiaire isolé ou ménage ayant trois personnes à charge	 3,7
bénéficiaire isolé ou ménage ayant quatre personnes à charge 4,3
majoration par personne à charge supplémentaire	             0,5
------------------------------------------------------------ ---

```catala
champ d'application CalculNombrePartLogementFoyer:
  exception définition n_nombre_parts_d832_25 sous condition
    condition_2_du_832_25
  conséquence égal à
    si nombre_personnes_à_charge = 0 alors
      selon situation_familiale_calcul_apl sous forme
      -- PersonneSeule: 1,2
      -- Couple: 1,5
    sinon (si nombre_personnes_à_charge = 1 alors
      2,5
    sinon (si nombre_personnes_à_charge = 2 alors
      3,0
    sinon (si nombre_personnes_à_charge = 3 alors
      3,7
    sinon (si nombre_personnes_à_charge = 4 alors
      4,3
    sinon
      ( 4,3 +. (0,5 *. (entier_vers_décimal de
        (nombre_personnes_à_charge - 4))))
    ))))
```

######## Article D832-26 | LEGIARTI000038878708

L'équivalence de loyer et de charges minimale " E0 ", définie au 4° de l'article D. 832-24 ,
est obtenue par application de pourcentages à des tranches de ressources, dont les limites
inférieures et supérieures sont multipliées par le nombre de parts " N " défini au e du 1°
de l'article D. 832-25. Le résultat est majoré du produit d'un montant forfaitaire par le
nombre de parts " N ", le total étant divisé par douze.

```catala
# Selon un mail de DGALN/DHUP/FE4 du 19/07/2022, il nest nécessaire pour le
# calcul de cette équivalence de loyer de garder une précision infinie durant
# toutes les étapes du calcul. En Catala, nous utilisons donc le type
# décimal plutôt que argent qui arrondi systématiquement au centime près
# à chaque étape de calcul.
champ d'application CalculÉquivalenceLoyerMinimale:
  définition tranches_revenus_d832_26_multipliées égal à
    application pour tranche dans tranches_revenus_d832_26 de
      TrancheRevenuDécimal {
        -- haut: (selon tranche.haut sous forme
          -- LimiteTranche.Infini: LimiteTrancheDécimal.Infini
          -- LimiteTranche.Revenu de tranche_haut:
            LimiteTrancheDécimal.Revenu contenu
              ((argent_vers_décimal de tranche_haut) *. n_nombre_parts_d832_25))
        -- bas: argent_vers_décimal de tranche.bas *.
          n_nombre_parts_d832_25
        -- taux: tranche.taux
      }

  définition montant égal à
    soit ressources_ménage_arrondies égal à
      argent_vers_décimal de ressources_ménage_arrondies
    dans
    décimal_vers_argent de (
      (((somme décimal pour tranche dans tranches_revenus_d832_26_multipliées de
        (si
          ressources_ménage_arrondies <=. tranche.bas
        alors 0,0
        sinon
          (selon tranche.haut sous forme
          -- LimiteTrancheDécimal.Revenu de tranche_haut:
            (si
              ressources_ménage_arrondies >=. tranche_haut
            alors
              (tranche_haut -. tranche.bas) *.
                tranche.taux
            sinon
              ((ressources_ménage_arrondies -.
                tranche.bas) *.
                  tranche.taux))
          -- LimiteTrancheDécimal.Infini:
            (ressources_ménage_arrondies -. tranche.bas) *.
              tranche.taux)
        ))) +.
      argent_vers_décimal de montant_forfaitaire_d832_26 *.
        n_nombre_parts_d832_25) /. 12,0)
```

Toutefois, pour les logements-foyers de jeunes travailleurs et pour les résidences sociales
mentionnés au deuxième alinéa de l'article D. 832-25, " E0 " est obtenue par application de
pourcentages à des tranches de ressources dont les limites inférieures et supérieures sont
multipliées par le nombre de parts " N " défini au d du 2° de l'article D. 832-25. Le résultat
est majoré d'un montant forfaitaire, le total étant alors divisé par douze.

```catala
champ d'application CalculÉquivalenceLoyerMinimale:
  exception définition montant sous condition
    condition_2_du_832_25
  conséquence égal à
    soit ressources_ménage_arrondies égal à
      argent_vers_décimal de ressources_ménage_arrondies
    dans
    décimal_vers_argent de (
      (((somme décimal pour tranche dans tranches_revenus_d832_26_multipliées de
        (si
          ressources_ménage_arrondies <=. tranche.bas
        alors 0,0
        sinon
          (selon tranche.haut sous forme
          -- LimiteTrancheDécimal.Revenu de tranche_haut:
            (si
              ressources_ménage_arrondies >=. tranche_haut
            alors
              (tranche_haut -. tranche.bas) *.
                tranche.taux
            sinon
              ((ressources_ménage_arrondies -.
                tranche.bas) *.
                  tranche.taux))
          -- LimiteTrancheDécimal.Infini:
            (ressources_ménage_arrondies -. tranche.bas) *.
              tranche.taux)
        ))) +.
      argent_vers_décimal de montant_forfaitaire_d832_26) /. 12,0)
```

Les pourcentages et le coefficient " N " sont appliqués à la limite supérieure de l'intervalle
dans lequel se situent les ressources.

Les ressources sont appréciées selon les modalités prévues à la section 2 du chapitre II du
titre II du présent livre et arrondies à la centaine d'euros supérieure.

Les pourcentages, les montants forfaitaires et les bornes des tranches sont fixés par arrêté.

######## Article D832-27 | LEGIARTI000038878706

La dépense nette de logement, mentionnée au neuvième alinéa de l'article D. 832-24 , est égale
à la différence entre l'équivalence de loyer et de charges locatives éligibles " E " et le
montant mensuel de l'aide calculé selon les dispositions des premier à huitième alinéas du
même article. Lorsque cette dépense nette de logement est inférieure à un montant fixé par
arrêté selon le type de logements-foyer, l'abattement à effectuer sur le montant mensuel de
l'aide est égal à la différence constatée.

```catala
champ d'application CalculAidePersonnaliséeLogementFoyer:
  définition dépense_nette_minimale_d832_27 de aide_calculée_1_8_d832_24
  égal à
    équivalence_loyer_éligible -€ aide_calculée_1_8_d832_24

  définition abattement_dépense_nette_minimale_d832_27
    de aide_calculée_1_8_d832_24
  égal à
    soit dépense_nette_minimale égal à
      dépense_nette_minimale_d832_27 de aide_calculée_1_8_d832_24
    dans
    si dépense_nette_minimale <=€ montant_forfaitaire_d832_27
    alors montant_forfaitaire_d832_27 -€ dépense_nette_minimale
    sinon 0€

```
######## Article D832-28 | LEGIARTI000038878704

Les arrêtés mentionnés dans la présente section sont pris par les ministres chargés du logement,
du budget, de la sécurité sociale et de l'agriculture.

Le zonage géographique est fixé par arrêté des ministres chargés du logement et du budget.


#### Titre IV : Allocations de Logement

##### Chapitre 1 : Champ d'application

####### Article D841-1 | LEGIARTI000038878698

Pour l'application du 3° de l'article L. 841-1 , la durée pendant laquelle l'allocation est
due est fixée à cinq ans.

```catala
champ d'application ÉligibilitéAllocationLogement:
  définition durée_l841_1_3 égal à 5 an
```

####### Article R841-2 | LEGIARTI000038878696

Les allocataires sans personne à charge mentionnés au 6° de l'article L. 841-1 sont assimilés
aux ménages sans enfant, pour le calcul de leur allocation de logement familiale.

```catala
# Confirme qu'une femme enceinte est considérée comme une personne seule.
```

##### Chapitre 2 : Modalités de liquidation et de versement des allocations de logement

###### Section 1 : Secteur locatif ordinaire

####### Article D842-1 | LEGIARTI000038878687

Les dispositions de la section 1 du chapitre III du titre II du présent livre sont applicables
au calcul de l'allocation de logement versée en secteur locatif, sous réserve des dispositions
des articles D. 842-2 et D. 842-4.

```catala
# Rien à formaliser ici, voir le prologue sur la déclaration du
# champ d'application de calcul des allocations logement qui appelle
# directement le champ d'application de calcul de l'APL en secteur locatif.
```

####### Article D842-2 | LEGIARTI000038878685

En cas de logement en hôtel meublé ou en établissement assimilé, ou lorsque l'allocataire occupe
un logement loué en meublé, le prix du loyer est remplacé par les deux tiers du prix effectivement
payé dans la limite du loyer-plafond.

```catala
# D'après D823-18, cet article s'applique aussi pour l'aide personnalisée au
# logement. Puisque CalculAllocationLogementLocatif appelle toujours
# CalculAidePersonnaliséeLogementLocatif, il suffit de définir la manipulation
# dans CalculAidePersonnaliséeLogementLocatif.
champ d'application CalculAidePersonnaliséeLogementLocatif:
  définition loyer_principal état avec_réduction_meublé égal à
    si logement_meublé_d842_2 alors
      loyer_principal *€ (2,0 /. 3,0)
    sinon
      loyer_principal
```

Dans le cas d'un local à usage mixte d'habitation et professionnel, il n'est pas tenu compte
des majorations de loyers résultant de l'affectation d'une partie des lieux à l'exercice d'une
profession.

```catala
# Pas pertinent de le formaliser ici puisqu'il s'agit surtout d'une indication
# sur la manière de remplir la variable "loyer_principal" dans le formulaire.
```

Lorsque le logement est compris dans les locaux relevant du statut du fermage, lorsque le
logement est à usage mixte d'habitation et commercial ou lorsqu'il est loué à titre d'accessoire
du contrat de travail, l'évaluation du loyer est faite, en tant que de besoin, par référence
à celui de logements similaires dans la même commune ou dans des communes avoisinantes.


```catala
# Pas pertinent de le formaliser ici puisqu'il s'agit surtout d'une indication
# sur la manière de remplir la variable "loyer_principal" dans le formulaire.
```

####### Article D842-3 | LEGIARTI000041477287

En cas de colocation telle que définie à l'article R. 821-4 , le loyer principal retenu est le
résultat du quotient du loyer effectivement payé par le nombre de cotitulaires du bail ou de
l'engagement de location.

```catala
# Pas pertinent de le formaliser ici puisqu'il s'agit surtout d'une indication
# sur la manière de remplir la variable "loyer_principal" dans le formulaire.
```

####### Article D842-4 | LEGIARTI000038878681

Lorsqu'à la suite, soit d'un échange consenti pour libérer un logement dont la superficie
excède celle prévue à l'article R. 822-25 , soit d'une expropriation pour cause d'utilité
publique ou d'une opération d'aménagement ou de rénovation urbaine ou de résorption d'habitat
insalubre en application de l'article L. 522-1 , soit de la démolition d'un immeuble ayant
fait l'objet d'une déclaration d'insalubrité, d'une interdiction d'habiter ou d'un arrêté
de péril, soit d'une opération de restauration immobilière, les personnes mentionnées au 3°
de l'article D. 842-16 qui, au titre de leur ancien logement, bénéficiaient de l'allocation
de logement ou qui remplissaient les conditions pour pouvoir en bénéficier, ont été amenées,
de leur propre fait ou à l'initiative des pouvoirs publics, à occuper un logement locatif
présentant un loyer plus élevé que celui qu'elles payaient précédemment, l'allocation est
calculée de façon à couvrir la différence entre le loyer principal acquitté dans l'ancien
logement, déduction faite, le cas échéant, de l'allocation qui leur était octroyée et le
nouveau loyer principal qui leur est réclamé, dans la limite du plafond fixé en application
du 3° de l'article L. 823-1 .

```catala
champ d'application CalculAllocationLogementLocatif sous condition
  selon changement_logement_d842_4 sous forme
  -- Changement de infos:
    loyer_principal >=€ infos.ancien_loyer_principal
  -- PasDeChangement: faux:

  exception définition aide_finale_formule égal à
    selon changement_logement_d842_4 sous forme
    -- Changement de infos:
        loyer_principal -€
          (infos.ancien_loyer_principal -€ infos.ancienne_allocation_logement)
    -- n'importe quel: 0 € # N'arrive pas

  # TODO juridique: confirmer cette ligne
  exception définition traitement_aide_finale de aide_finale égal à
    aide_finale
```

###### Section 2 : Accession à la propriété

####### Article R842-5 | LEGIARTI000039048889

L'allocation de logement est accordée au titre de la résidence principale :

```catala
# Cet article dispose qu'en cas d'accession à la propriété, l'allocation
# logement est plus restrictive. On définit donc une variable qui va
# être vraie tout le temps sauf en cas d'accession propriété. Puis
# on définit des exceptions à cette règle pour les 1°, 2° et 3° de cet
# article.
# TODO juridique: vérifier si cela s'applique à la location-accession ou pas
champ d'application ÉligibilitéAllocationLogement:
  règle condition_accession_propriété sous condition
    selon ménage.logement.mode_occupation sous forme
    -- AccessionPropriétéLocalUsageExclusifHabitation: faux
    -- n'importe quel: vrai
  conséquence rempli
```

1° Aux personnes propriétaires du logement pendant la période au cours de laquelle elles se
libèrent de la dette contractée pour accéder à la propriété de ce logement et, le cas échéant,
de celle contractée en même temps pour réaliser des travaux permettant de remplir les
conditions de décence ;

```catala
# "le cas échéant, de celle contractée en même temps pour
# réaliser des travaux permettant de remplir les conditions de décence"
# Quel est le sens de cette précision, quel cas cela recouvre-t-il ?
# Quand il y a deux prêts différents, un pour le logement et un pour les
# travaux, donc les périodes de remboursement se chevauchent mais ne sont
# pas égales ? Les deux prêts doivent-t-il commencer en même temps?
# Réponse de DGALN/DHUP/FE4 le 20/05/2022:
# "Il s’agit de travaux pour la décence (cf. art 6 loi de 89 et décret décence
# du 30/01/2002) et non juste de travaux d’embellissement."
# Lorsqu’il y a deux prêts différents, un pour le logement et un pour
# les travaux et que les périodes de remboursement se chevauchent et ne sont
# pas égales, doit-on comprendre que les deux prêts doivent commencer en
# même temps pour que la disposition s’applique ?
# Réponse de DGALN/DHUP/FE4 du 25/05/2022:
# "Soit la demande est globale avec 2 prêts « en même temps » (cf. 1°), soit
# les travaux arrivent ultérieurement, dans ce cas c’est le 2°."
champ d'application ÉligibilitéAllocationLogement:
  exception règle condition_accession_propriété sous condition
    selon ménage.logement.mode_occupation sous forme
    -- AccessionPropriétéLocalUsageExclusifHabitation de propriétaire:
        propriétaire.prêt.titulaire_prêt sous forme Demandeur et
        (propriétaire.type_travaux_logement_r842_5 sous forme
          TypeTravauxLogementR842_5.PasDeTravaux ou
        propriétaire.type_travaux_logement_r842_5 sous forme
          ObjectifDécenceLogement)
    -- n'importe quel: faux
  conséquence rempli
```

2° Aux personnes qui se libèrent d'une dette contractée en vue de réaliser des travaux
figurant sur la liste mentionnée à l'article R. 321-15 ;

```catala
champ d'application ÉligibilitéAllocationLogement:
  exception règle condition_accession_propriété sous condition
    selon ménage.logement.mode_occupation sous forme
    -- AccessionPropriétéLocalUsageExclusifHabitation de propriétaire:
        propriétaire.prêt.titulaire_prêt sous forme Demandeur et
        propriétaire.type_travaux_logement_r842_5 sous forme
          PrévuDansListeR321_15
    -- n'importe quel: faux
  conséquence rempli
```

3° Aux personnes qui se libèrent d'une dette contractée en vue soit d'agrandir leur logement,
soit d'aménager à usage de logements des locaux non destinés à l'habitation, lorsque ces
travaux répondent aux normes techniques imposées pour le bénéfice des prêts conventionnés
mentionnés à l'article D. 331-63 .

```catala
champ d'application ÉligibilitéAllocationLogement:
  exception règle condition_accession_propriété sous condition
    selon ménage.logement.mode_occupation sous forme
    -- AccessionPropriétéLocalUsageExclusifHabitation de propriétaire:
       propriétaire.prêt.titulaire_prêt sous forme Demandeur et
       propriétaire.type_travaux_logement_r842_5 sous forme
          AgrandirOuRendreHabitableD331_63
    -- n'importe quel: faux
  conséquence rempli
```

####### Article D842-6 | LEGIARTI000038878675

Pour les ménages mentionnés à l'article R. 842-5 , le montant mensuel de l'allocation est
calculé selon la formule et les modalités suivantes :

$$\textrm{Af} = \textrm{K} \times (\textrm{L} + \textrm{C}-\textrm{L0})$$

```catala
champ d'application CalculAllocationLogementAccessionPropriété:
  définition aide_finale_formule égal à
    (
      mensualité_éligible +€ montant_forfaitaire_charges -€
        mensualité_minimale
    ) *€ coefficient_prise_en_charge
```

où :

1° " Af " est l'allocation mensuelle issue de la formule de calcul ;

2° " K " est le coefficient de prise en charge, calculé selon les dispositions du 2° de
l'article D. 832-25 ;

```catala
champ d'application CalculAllocationLogementAccessionPropriété:
  # En définissant cette variable de contexte, on s'assure que le chemin de code
  # pris dans le sous-champ d'application du calcul de l'APL en secteur logement
  # foyer prenne bien le 2° du D832-25 et non pas le 1°.
  définition calcul_apl_logement_foyer.condition_2_du_832_25 égal à
    vrai
  # Attention ! Le calcul de K dépend également du nombre de parts N, qui
  # est différent selon que l'on soit en secteur accession-propriété ou
  # en secteur logement-foyer. Ici, nous sommes en secteur accession
  # propriété donc il faut que le nombre de parts pris en compte pour le calcul
  # soit le nombre de parts accession-propriété.
  définition calcul_apl_logement_foyer.n_nombre_parts_d832_25 égal à
    calcul_nombre_parts.n_nombre_parts_d832_11

  définition coefficient_prise_en_charge égal à
    calcul_apl_logement_foyer.coefficient_prise_en_charge_d832_25
```

3° " L " est la mensualité éligible ; elle correspond à la mensualité principale,
déterminée selon les dispositions articles D. 842-7 à D. 842-10, prise en compte dans la
limite d'un plafond fixé par arrêté en fonction de la zone géographique et de la
composition familiale ;

```catala
champ d'application CalculAllocationLogementAccessionPropriété:
  définition mensualité_éligible égal à
    si mensualité_principale >€ plafond_mensualité_d842_6 alors
      plafond_mensualité_d842_6
    sinon
      mensualité_principale
```

4° " C " est le montant forfaitaire au titre des charges, fixé par arrêté en fonction de la
composition familiale ;

5° " L0 " est la mensualité minimale, calculée selon les dispositions du deuxième alinéa de
l'article D. 832-26, dans lesquelles elle se substitue à " E0 ".

```catala
champ d'application CalculAllocationLogementAccessionPropriété:
  définition mensualité_minimale égal à
    calcul_équivalence_loyer_minimale.montant

  # Mettre la condition à "faux" permet d'activer le deuxième alinéa de D832-26
  définition calcul_équivalence_loyer_minimale.condition_2_du_832_25 égal à vrai
  définition calcul_équivalence_loyer_minimale.ressources_ménage_arrondies
  égal à ressources_ménage_arrondies
  définition calcul_équivalence_loyer_minimale.n_nombre_parts_d832_25 égal à
    calcul_nombre_parts.n_nombre_parts_d832_11
  définition calcul_nombre_parts.nombre_personnes_à_charge égal à
    nombre_personnes_à_charge
  définition calcul_nombre_parts.situation_familiale_calcul_apl égal à
    situation_familiale_calcul_apl
```

Ce résultat, ainsi calculé, est minoré d'un montant fixé forfaitairement par arrêté.

```catala
champ d'application CalculAllocationLogementAccessionPropriété:
  définition traitement_aide_finale de aide_finale
  état minoration_forfaitaire
  égal à
    si aide_finale -€ montant_forfaitaire_d842_6 <€ 0€ alors
      0 €
    sinon
      (aide_finale -€ montant_forfaitaire_d842_6)
```

Le montant ainsi obtenu est minoré d'un abattement au titre de la dépense nette minimale,
dont les modalités de calcul sont précisées par l'article D. 842-11.

```catala
champ d'application CalculAllocationLogementAccessionPropriété:
  définition traitement_aide_finale de aide_finale état dépense_nette_minimale
  égal à
    soit aide_finale égal à traitement_aide_finale de aide_finale dans
    soit abattement égal à abattement_dépense_nette_minimale de aide_finale dans
    si aide_finale <€ abattement
    alors 0 €
    sinon aide_finale -€ abattement
```

Ce dernier résultat est diminué d'un montant représentatif des contributions sociales qui
s'y appliquent, arrondi à l'euro inférieur, puis majoré de ce montant représentatif.

```catala
champ d'application CalculAllocationLogementAccessionPropriété:
  définition traitement_aide_finale de aide_finale
  état contributions_sociales_arrondi
  égal à
    soit aide_finale égal à traitement_aide_finale de aide_finale dans
    soit crds égal à contributions_sociales.montant de aide_finale dans
    soit aide_finale_moins_crds_arrondie égal à
      arrondi_argent de ((aide_finale -€ crds) -€ 0,50€)
    dans
    si
      aide_finale_moins_crds_arrondie +€ crds >=€ 0€
    alors
      aide_finale_moins_crds_arrondie +€ crds
    sinon 0€
```

Lorsque le montant mensuel de l'allocation, ainsi calculé, est inférieur à un montant fixé
par arrêté, il n'est pas procédé à son versement.

```catala
champ d'application CalculAllocationLogementAccessionPropriété:
  définition traitement_aide_finale de aide_finale état montant_minimal égal à
    soit aide_finale égal à traitement_aide_finale de aide_finale dans
    si aide_finale <€ montant_minimal_aide_d842_6
    alors 0 € sinon aide_finale
```

####### Article D842-7 | LEGIARTI000038878673

Sous réserve des dispositions de l'article D. 842-8 , seuls sont pris en considération pour
le calcul de la mensualité " L " définie au 3° de l'article D. 842-6 :

1° Les charges d'intérêts et d'amortissements et les charges accessoires au principal de
la dette, afférentes aux emprunts contractés en vue de l'accession à la propriété d'un
logement et qui ont fait, pour chacun d'entre eux, l'objet d'un certificat daté, notifié
au demandeur par les organismes prêteurs précisant les modalités ainsi que la périodicité
des paiements, présenté à l'appui de la demande d'allocation de logement ;

2° Les charges d'intérêts et d'amortissements et les charges accessoires, afférentes aux
emprunts substitués à ceux qui sont mentionnés au 1°, dans la limite des sommes restant
dues en capital sur le montant du premier prêt ;

3° Les charges afférentes au paiement de tout ou partie du prix d'achat ou ayant pour
objet de financer les dépenses jugées indispensables à la délivrance du certificat de
conformité dans les conditions prévues au 1° ;

4° Le versement des primes de l'assurance-décès contractée par le bénéficiaire en
garantie de l'exécution des engagements souscrits ;

5° Les loyers payés en vertu d'un contrat de location-accession ou d'un bail à construction.

```catala
# Ceci donne des informations sur comment calculer à la main la variable
# d'entrée "mensualité_principale" mais nous ne détaillerons pas le calcul
# en Catala ici.
```

####### Article D842-8 | LEGIARTI000038878671

Ne sont, notamment, pas pris en considération :

1° Les remboursements effectués par le bénéficiaire en anticipation des obligations
résultant des contrats de prêts qu'il a souscrits ;

2° Les prêts constituant une obligation au porteur.
Toutefois, peuvent être pris en considération les prêts au logement consentis par les
banques ou les établissements financiers et en représentation desquels des billets à ordre
ont été créés en vue d'une mobilisation éventuelle de la créance, dès lors que, dans le
contrat de prêt, le créancier et le débiteur sont expressément désignés et que les paiements
sont effectifs.

```catala
# Ceci donne des informations sur comment calculer à la main la variable
# d'entrée "mensualité_principale" mais nous ne détaillerons pas le calcul
# en Catala ici.
```

####### Article D842-9 | LEGIARTI000038878669

Pour le calcul de la mensualité " L ", le plafond mensuel mentionné au 3° de l'article
D. 842-6 est applicable pour la période au titre de laquelle le certificat prévu à
l'article D. 842-7 a été établi.

```catala
champ d'application CalculAllocationLogementAccessionPropriété:
  définition plafond_mensualité_d842_6 égal à
    calcul_plafond_mensualité_d842_6 de date_signature_prêt
```

Le plafond ainsi retenu ne pourra, en aucun cas, être inférieur à celui applicable au
moment de l'entrée dans les lieux, sous la réserve qu'il s'agisse d'un local habité
pour la première fois par l'allocataire.

```catala
# La manière dont nous interprétons le deuxième alinéa ci-dessus est la
# suivante : puisque le plafond dépend d'une date (normalement de signature
# du prêt), et qu'en fonction de la date ce plafond varie à la hausse ou à la
# baisse, l'alinéa donne une petite faveur aux propriétaires puisqu'il leur
# permet de choisir la date la plus avantageuse pour calculer le plafond
# entre la date de signature du prêt et celle d'entrée dans le logement si
# le local est habité pour la première fois par le bénéficiaire.
champ d'application CalculAllocationLogementAccessionPropriété:
  exception définition plafond_mensualité_d842_6 sous condition
    local_habité_première_fois_bénéficiaire
  conséquence égal à
    soit plafond_signature égal à
      calcul_plafond_mensualité_d842_6 de date_signature_prêt
    dans
    soit plafond_entrée égal à
      calcul_plafond_mensualité_d842_6 de date_entrée_logement
    dans
    si
      plafond_signature <€ plafond_entrée
    alors plafond_entrée
    sinon plafond_signature
```

####### Article D842-10 | LEGIARTI000041477292

Lorsque plusieurs personnes ou ménages constituant des foyers distincts occupent le
même logement et qu'ils sont copropriétaires du logement et cotitulaires du prêt,
pour le calcul de l'allocation de logement :

1° La mensualité " L " représente le quotient de la somme prise en compte au titre
des charges mentionnées à l'article D. 842-7 par le nombre de copropriétaires,
cotitulaires du prêt ouvrant droit à l'allocation de logement, le résultat étant
pris en compte dans la limite de la mensualité plafond mentionnée au 3° de
l'article D. 842-6 qui correspond à la situation familiale de chacun des
intéressés ;

2° Il est fait application à chaque personne ou ménage concerné du coefficient
" N " prévu au d du 2° de l'article D. 832-25 et de l'élément " C " prévu au 4°
 de l'article D. 842-6 correspondant à sa situation familiale.

Les arrêtés fixant les plafonds de mensualité mentionnés au 3° de l'article
D. 842-6 et les montants forfaitaires au titre des charges mentionnées au 4°
du même article peuvent fixer des montants spécifiques pour les ménages
copropriétaires.

```catala
# Ceci donne des informations sur comment calculer à la main la variable
# d'entrée "mensualité_principale" mais nous ne détaillerons pas le calcul
# en Catala ici.
```

####### Article D842-11 | LEGIARTI000038878665

La dépense nette minimale mentionnée au dixième alinéa de l'article D. 842-6 est obtenue
en déduisant des charges mensuelles de prêts déclarés, majorées du montant forfaitaire
des charges, le montant mensuel de l'allocation calculé selon les dispositions des premier
à neuvième alinéas du même article.

```catala
champ d'application CalculAllocationLogementAccessionPropriété:
   définition dépense_nette_minimale de allocation_mensuelle égal à
      charges_mensuelles_prêt +€ montant_forfaitaire_charges -€
        allocation_mensuelle
```

Lorsque la dépense nette minimale est inférieure à un montant fixé par arrêté, il est
procédé à un abattement sur le montant mensuel de l'allocation égal à la différence
constatée.

```catala
champ d'application CalculAllocationLogementAccessionPropriété:
  définition abattement_dépense_nette_minimale de allocation_mensuelle
  égal à
    soit dépense_nette_minimale égal à
      dépense_nette_minimale de allocation_mensuelle
    dans si
      dépense_nette_minimale <€ seuil_minimal_dépense_nette_minimale
    alors
      seuil_minimal_dépense_nette_minimale -€ dépense_nette_minimale
    sinon
     0 €

  définition seuil_minimal_dépense_nette_minimale égal à
     montant_forfaitaire_d842_11
```

Toutefois dans les cas mentionnés au 1° et 3° de l'article R. 842-5, lorsque le contrat
de prêt a été signé après le 1er juillet 1999, la dépense nette minimale pour effectuer
l'abattement doit être inférieure au produit des ressources par un coefficient fixé
par arrêté.

```catala
champ d'application CalculAllocationLogementAccessionPropriété:
   exception définition seuil_minimal_dépense_nette_minimale sous condition
      (type_travaux_logement sous forme TypeTravauxLogementR842_5.PasDeTravaux
        ou type_travaux_logement sous forme ObjectifDécenceLogement ou
        type_travaux_logement sous forme AgrandirOuRendreHabitableD331_63) et
        date_signature_prêt >=@ |1999-07-01|
   conséquence égal à
      ressources_ménage_arrondies *€ coefficient_d842_11
```

Les ressources sont appréciées selon les modalités prévues à la section 2 du chapitre II
du titre II du présent livre et de l'article D. 842-12 puis arrondies à la centaine
d'euros supérieure.

####### Article D842-12 | LEGIARTI000038878663

Si les ressources de l'allocataire et de son conjoint déterminées selon les modalités
prévues à la section 2 du chapitre II du titre II du présent livre sont inférieures :

1° A un montant égal au produit d'un coefficient, fixé par arrêté, et de la mensualité
déclarée, s'agissant des prêts signés entre le 1er octobre 1992 et le 30 septembre 1994,
ou après le 30 septembre 1994, si l'allocation est accordée en application du 1° ou 3°
de l'article R. 842-5 ;

```catala
champ d'application CalculAllocationLogementAccessionPropriété:
  définition seuil_minimal_ressources_ménage sous condition
    ((date_signature_prêt >=@ |1992-09-30| et
      date_signature_prêt <=@ |1994-09-30|) ou
    (date_signature_prêt >@ |1994-09-30| et
      (type_travaux_logement sous forme TypeTravauxLogementR842_5.PasDeTravaux
      ou type_travaux_logement sous forme ObjectifDécenceLogement ou
      type_travaux_logement sous forme AgrandirOuRendreHabitableD331_63)
    ))
  conséquence égal à
    mensualité_principale *€ coefficient_d842_12
```

2° A un montant forfaitaire, fixé par arrêté, s'agissant des prêts signés après le
30 septembre 1994 et lorsque l'allocation est accordée en application du 2° de
l'article R. 842-5 ;

```catala
champ d'application CalculAllocationLogementAccessionPropriété:
  définition seuil_minimal_ressources_ménage sous condition
    date_signature_prêt >@ |1994-09-30| et
    type_travaux_logement sous forme PrévuDansListeR321_15
  conséquence égal à
    montant_forfaitaire_d842_12
```

Ces ressources sont portées à ce montant sauf lorsque les intéressés se trouvent
dans l'une des situations mentionnées aux articles R. 822-11 et R. 822-13 à R. 822-17 .

```catala
champ d'application CalculAllocationLogementAccessionPropriété:
  définition ressources_ménage_arrondies état seuil égal à
    si ressources_ménage_arrondies <=€ seuil_minimal_ressources_ménage alors
    # Cette formule arrondit à la centaine d'euros supérieure. Essayez quelques
    # exemples pour vous en convaincre, dont 100 et 150.
      (arrondi_argent de ((seuil_minimal_ressources_ménage +€ 49,99€) *€ 1%)) *€
        100,0
    sinon
      ressources_ménage_arrondies

  exception définition ressources_ménage_arrondies état seuil sous condition
    situation_r822_11_13_17
  conséquence égal à
    ressources_ménage_arrondies
```

####### Article D842-13 | LEGIARTI000038878661

Les arrêtés prévus par la présente section sont pris par les ministres chargés
du logement, du budget, de la sécurité sociale et de l'agriculture.

Le zonage géographique est fixé par arrêté des ministres chargés du logement et du budget.

```catala
# Pas pertinent ici
```

###### Section 3 : Logements-foyers

####### Article R842-14 | LEGIARTI000038878657

Les conditions relatives à la résidence principale et à la superficie du logement
prévues aux articles R. 822-23 et R. 822-25 sont réputées remplies lorsque le
bénéficiaire réside dans un logement-foyer de jeunes travailleurs construit en
application du III de l'article 12 de la loi n° 57-908 du 7 août 1957 tendant
à favoriser la construction de logements et les équipements collectifs.

```catala
champ d'application ÉligibilitéAllocationLogement sous condition
  selon ménage.logement.mode_occupation sous forme
    -- RésidentLogementFoyer de logement_foyer:
      logement_foyer.construit_application_loi_1957_12_III
    -- n'importe quel: faux:

  règle éligibilité_commune.condition_logement_résidence_principale rempli
  règle éligibilité_commune.condition_logement_surface rempli
```

####### Article D842-15 | LEGIARTI000038878655

Pour les bénéficiaires résidant dans un logement-foyer, ou assimilé, mentionné
au 3° de l'article D. 823-9, à l'exception des étudiants logés dans un studio
d'un logement-foyer défini à l'article L. 633-1 pour lesquels le montant mensuel
de l'aide est calculé selon les modalités précisées au 1° de l'article D. 823-9,
le montant mensuel de l'aide est calculé selon la formule et les modalités suivantes :

$$\textrm{Af} = \textrm{K} \times (\textrm{L} + \textrm{C}-\textrm{L0})$$

```catala
champ d'application CalculAllocationLogementFoyer:
  définition aide_finale_formule égal à
    (équivalence_loyer +€
      montant_forfaitaire_charges -€ loyer_minimal) *€
    coefficient_prise_en_charge
```

où :

1° " Af " est l'aide mensuelle issue de la formule de calcul ;

2° " K " est le coefficient de prise en charge ; il est calculé selon les dispositions
du 2° de l'article D. 832-25 ;

```catala
champ d'application CalculAllocationLogementFoyer:
  # En définissant cette variable de contexte, on s'assure que le chemin de code
  # pris dans le sous-champ d'application du calcul de l'APL en secteur logement
  # foyer prenne bien le 2° du D832-25 et non pas le 1°.
  définition calcul_apl_logement_foyer.condition_2_du_832_25 égal à
    vrai

  définition coefficient_prise_en_charge égal à
    calcul_apl_logement_foyer.coefficient_prise_en_charge_d832_25

champ d'application CalculAllocationLogement:
  # TODO informatique: mieux gérer où on fait l'arrondi des ressources parce
  # que là par exemple c'est pas explicitement précisé dans la loi.
  définition ressources_ménage état avec_arrondi égal à
    # Cette formule arrondit à la centaine d'euros supérieure. Essayez quelques
    # exemples pour vous en convaincre, dont 100 et 150.
    arrondi_argent de ((ressources_ménage *€ 1%) +€ 0,49€) *€ 100,0
```

3° " L " est l'équivalence de loyer prise en compte, déterminée selon les dispositions
de l'article D. 842-16 ;

4° " C " est le montant forfaitaire au titre des charges, fixé par arrêté en fonction
de la composition familiale ;

5° " L0 " est le loyer minimal ; il est calculé selon les dispositions du deuxième
alinéa de l'article D. 832-26, dans lequel il se substitue à " E0 ".

```catala
champ d'application CalculAllocationLogementFoyer:
  # En définissant comme vraie la condition du 2° de D832-25, on accède
  # logiquement aux disposition du deuxième alinéa de D832-26 (voir le jeu
  # des renvois).
  définition calcul_équivalence_loyer_minimale.condition_2_du_832_25 égal à vrai
  définition calcul_équivalence_loyer_minimale.ressources_ménage_arrondies
    égal à ressources_ménage_arrondies

  définition calcul_nombre_parts.nombre_personnes_à_charge égal à
    nombre_personnes_à_charge
  définition calcul_nombre_parts.situation_familiale_calcul_apl égal à
   situation_familiale_calcul_apl
  définition calcul_nombre_parts.condition_2_du_832_25 égal à vrai

  définition calcul_équivalence_loyer_minimale.n_nombre_parts_d832_25 égal à
    calcul_nombre_parts.n_nombre_parts_d832_25
  définition loyer_minimal égal à
    calcul_équivalence_loyer_minimale.montant
```

Le montant ainsi calculé est minoré d'un montant fixé forfaitairement par arrêté.

```catala
champ d'application CalculAllocationLogementFoyer:
  définition traitement_aide_finale de aide_finale
  état minoration_forfaitaire égal à
    si aide_finale -€ montant_forfaitaire_d842_15 <€ 0€ alors
      0 €
    sinon
      (aide_finale -€ montant_forfaitaire_d842_15)
```

Ce résultat est minoré d'un abattement au titre de la dépense nette minimale. Les
modalités de calcul de la dépense nette, de sa valeur minimale et de l'abattement
sont fixées à l'article D. 842-17.

```catala
champ d'application CalculAllocationLogementFoyer:
  définition traitement_aide_finale de aide_finale état dépense_nette_minimale
  égal à
    soit aide_finale égal à traitement_aide_finale de aide_finale dans
    soit abattement égal à abattement_dépense_nette_minimale de aide_finale dans
    si aide_finale -€ abattement <€ 0€
    alors 0 €
    sinon aide_finale -€ abattement
```

Si le montant obtenu par application des dispositions des alinéas précédents est
supérieur au montant de la redevance supportée par le résident, il est rapporté
au montant de cette redevance.

```catala
# Qu'est-ce que la redevance supportée par le résident? Le loyer
# du logement-foyer?
# OUI, selon réponse de DGALN/DHUP/FE4 du 25/05/2022.
champ d'application CalculAllocationLogementFoyer:
  définition traitement_aide_finale de aide_finale état redevance
  égal à
    soit aide_finale égal à traitement_aide_finale de aide_finale dans
    si aide_finale >€ redevance
    alors redevance
    sinon aide_finale
```

Ce dernier résultat est diminué d'un montant représentatif des contributions
sociales qui s'y appliquent, arrondi à l'euro inférieur, puis majoré de ce
montant représentatif.

```catala
champ d'application CalculAllocationLogementFoyer:
  définition traitement_aide_finale de aide_finale
  état contributions_sociales_arrondi
  égal à
    soit aide_finale égal à traitement_aide_finale de aide_finale dans
    soit crds égal à contributions_sociales.montant de aide_finale dans
    soit aide_finale_moins_crds_arrondie égal à
      arrondi_argent de ((aide_finale -€ crds) -€ 0,50€)
    dans
    si
      aide_finale_moins_crds_arrondie +€ crds >=€ 0€
    alors
      aide_finale_moins_crds_arrondie +€ crds
    sinon 0€
```

Lorsque le montant mensuel de l'aide calculé selon les dispositions des alinéas
précédents est inférieur à un montant fixé par arrêté, il n'est pas procédé à
son versement.

```catala
champ d'application CalculAllocationLogementFoyer:
  définition traitement_aide_finale de aide_finale état montant_minimal égal à
    soit aide_finale égal à traitement_aide_finale de aide_finale dans
    si aide_finale <€ montant_minimal_aide_d842_15
    alors 0 € sinon aide_finale
```

####### Article D842-16 | LEGIARTI000038878653

Un arrêté fixe l'équivalence de loyer " L ", définie au 3° de l'article D. 842-15,
pour chacune des catégories de personnes résidant dans un logement-foyer ou assimilé,
mentionné au 3° de l'article D. 823-9 .

Ces catégories comprennent :

1° Les étudiants logés en chambre ;

2° Les étudiants logés dans une chambre ayant fait l'objet d'une réhabilitation ;

3° Les personnes dont l'âge est au moins égal à celui prévu par l' article L. 161-17-2
du code de la sécurité sociale , augmenté de cinq années, sauf en cas d'inaptitude au
travail, les personnes titulaires de l'allocation mentionnée à l' article L. 815-1 du
code de la sécurité sociale dont l'âge est au moins égal à soixante-cinq ans, ainsi
que les personnes handicapées ;

4° Les autres personnes.

```catala-metadata
déclaration énumération CatégorieÉquivalenceLoyerAllocationLogementFoyer:
  # Les catégories 1° et 2° ici ne s'appliquent qu'aux chambres pour étudiants
  # gérées par les CROUS, comme décrit au 30 de l'article D823-9.
  -- ÉtudiantLogéEnChambreCROUS
  -- ÉtudiantLogéEnChambreCROUSRéhabilitée
  -- PersonnesÂgéesSelon3DeD842_16
  -- AutresPersonnes
```

####### Article D842-17 | LEGIARTI000038878651

La dépense nette de logement, définie au dixième alinéa de l'article D. 842-15 , est
égale à la différence entre l'équivalence de loyer prise en compte " L ", majorée du
montant forfaitaire au titre des charges " C ", et le montant mensuel de l'aide
calculé selon les modalités précisées aux deuxième à neuvième alinéas du même article.

```catala
champ d'application CalculAllocationLogementFoyer:
  définition dépense_nette_minimale de allocation_mensuelle égal à
    équivalence_loyer +€ montant_forfaitaire_charges -€ allocation_mensuelle
```

Lorsque sa valeur est inférieure à un montant fixé par arrêté, l'abattement sur
le montant mensuel de l'aide est égal à la différence constatée.

```catala
champ d'application CalculAllocationLogementFoyer:
  définition abattement_dépense_nette_minimale de allocation_mensuelle égal à
  si dépense_nette_minimale de allocation_mensuelle <€
    montant_minimal_dépense_nette_d842_17
  alors
    montant_minimal_dépense_nette_d842_17 -€
       dépense_nette_minimale de allocation_mensuelle
  sinon
    0€
```

####### Article D842-18 | LEGIARTI000038878649

Les arrêtés prévus à la présente section sont pris par les ministres chargés
du logement, du budget, de la sécurité sociale, et de l'agriculture.
