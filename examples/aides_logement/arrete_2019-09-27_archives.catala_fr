# Archives de l'arrêté du 27 septembre 2019 relatif au calcul des aides personnelles au logement et de la prime de déménagement

## Articles valables du 1er octobre 2020 au 1er octobre 2021

### Article 7 | LEGIARTI000042378448

Les plafonds de loyers visés au 2° de l'article D. 823-16 du même code sont fixés comme suit (en euros) :

---------------------------------------------------------------------------------------------------
Zone Personne seule Couple sans personne à charge Personne seule ou couple    Par personne à
                                                  ayant une personne à charge charge supplémentaire
---- -------------- ----------------------------- --------------------------- ---------------------
I	   296,82         357,99                        404,60	                    58,70

II     258,69         316,64                        356,30                      51,86

III    242,46         293,92                        329,56                      47,23
---------------------------------------------------------------------------------------------------


Nota:

Conformément à l’article 3, I de l’arrêté du 25 septembre 2020,
ces dispositions sont applicables pour les prestations dues à compter du 1er
octobre 2020.


```catala
champ d'application CalculAidePersonnaliséeLogementLocatif
  sous condition date_courante <@ |2021-10-01| et
    date_courante >=@ |2020-10-01|:

  # Colonne 1
  étiquette base définition plafond_loyer_d823_16_2 sous condition
    (situation_familiale_calcul_apl sous forme PersonneSeule) et
    nombre_personnes_à_charge = 0
  conséquence égal à
    selon zone sous forme
    -- Zone1: 296,82€
    -- Zone2: 258,59€
    -- Zone3: 242,46€

  # Colonne 2
  étiquette base définition plafond_loyer_d823_16_2 sous condition
    (situation_familiale_calcul_apl sous forme Couple) et
    nombre_personnes_à_charge = 0
  conséquence égal à
    selon zone sous forme
    --Zone1: 357,99€
    --Zone2: 316,64€
    --Zone3: 293,92€

  # Colonnes 3 et 4
  étiquette base définition plafond_loyer_d823_16_2 sous condition
    nombre_personnes_à_charge >= 1
  conséquence égal à
    selon zone sous forme
    --Zone1:
      404,60€ +€ 58,70€ *€ (entier_vers_décimal de
        (nombre_personnes_à_charge - 1))
    --Zone2:
      356,30€ +€ 51,86€ *€ (entier_vers_décimal de
        (nombre_personnes_à_charge - 1))
    --Zone3:
      329,56€ +€ 47,23€ *€ (entier_vers_décimal de
        (nombre_personnes_à_charge - 1))

```

### Article 8 | LEGIARTI000042378446

Dans le cas où le logement occupé est une chambre, les plafonds
de loyers visés au 2° de l'article D. 823-16 du même code sont
fixés comme suit, quelle que soit la taille de la famille (en euros) :

- 90 % des loyers plafonds de location pour une personne isolée ;
- 75 % des loyers plafonds de location pour une personne isolée,
dans le cas des personnes âgées ou handicapées adultes hébergées
à titre onéreux chez des particuliers.

On obtient les loyers plafonds suivants (en euros) :

MONTANTS DES LOYERS PLAFONDS CHAMBRE EN APL ET EN AL
(arrondis au centime d'euro le plus proche)

----------------------------------------
Bénéficiaires	          Zones	Montants
------------------------- ----- --------
Cas général	              I	    267,14

                          II	232,82

                          III   218,21


Cas des personnes âgées   I     222,62
ou handicapées adultes
hébergées à titre onéreux
chez des particuliers

                          II    194,02

                          III   181,85
----------------------------------------


NOTA :

Conformément à l’article 3, I de l’arrêté du 25 septembre 2020, ces dispositions
sont applicables pour les prestations dues à compter du 1er octobre 2020.

```catala
champ d'application CalculAidePersonnaliséeLogementLocatif
  sous condition
    date_courante <@ |2021-10-01| et date_courante >=@ |2020-10-01| et
    logement_est_chambre:

  étiquette chambre exception base
  définition plafond_loyer_d823_16_2 égal à
    selon zone sous forme
    -- Zone1: 267,14€
    -- Zone2: 232,82€
    -- Zone3: 218,21€

  exception chambre définition plafond_loyer_d823_16_2 sous condition
    âgées_ou_handicap_adultes_hébergées_onéreux_particuliers
  conséquence égal à
    selon zone sous forme
    -- Zone1: 222,62€
    -- Zone2: 194,02€
    -- Zone3: 181,85€
```

### Article 9 | LEGIARTI000042378444

Les montants forfaitaires au titre des charges visés au 3° de
l'article D. 823-16 du même code sont fixés comme suit (en euros) :

----------------------------------
Désignation	          Toutes zones
--------------------- ------------
Bénéficiaire isolé ou 53,99
couple sans personne
à charge

Par personne          12,24
supplémentaire
à charge
----------------------------------

NOTA :

Conformément à l’article 3, I de l’arrêté du 25 septembre 2020,
ces dispositions sont applicables pour les prestations dues à
compter du 1er octobre 2020.

```catala
champ d'application CalculAidePersonnaliséeLogementLocatif
  sous condition date_courante <@ |2021-10-01| et
    date_courante >=@ |2020-10-01|:
  définition montant_forfaitaire_charges_d823_16 égal à
    53,99€ +€ 12,24€ *€ (entier_vers_décimal de nombre_personnes_à_charge)
```

### Article 13 | LEGIARTI000044137423

La participation minimale P0 définie au 2° de l'article D. 823-17 du même code est
égale à la plus élevée des deux valeurs suivantes : 8,5 % de la somme du loyer éligible
défini au 2° de l'article D. 823-16 du même code et du forfait charge ou 35,24 euros.

```catala
champ d'application CalculAidePersonnaliséeLogementLocatif
  sous condition date_courante <@ |2021-10-01| et
    date_courante >=@ |2020-10-01|:
  définition participation_minimale égal à
    si
      (loyer_éligible +€ montant_forfaitaire_charges_d823_16)
        *€ 8,5% >=€ 35,24 €
    alors
      (loyer_éligible +€ montant_forfaitaire_charges_d823_16) *€ 8,5%
    sinon
      35,24 €
```

NOTA :

Conformément à l’article 3, I de l’arrêté du 25 septembre 2020, ces dispositions
sont applicables pour les prestations dues à compter du 1er octobre 2020.

### Article 14 | LEGIARTI000042378440

Pour l'application de l'article D. 823-17 du même code, le taux de participation
personnelle Tp du ménage, exprimé en pourcentage, est calculé selon la formule suivante :

$$\text{Tp} = \text{TF} + \text{TL}$$

dans laquelle :

1° TF représente un taux fonction de la taille de la famille donné par le tableau suivant :


VALEURS DE TF

Bénéficiaires                                        TF
---------------------------------------------------- ------
Isolé	                                               2,83%
Couple sans personne à charge	                       3,15%
Personne seule ou couple ayant une personne à charge 2,70%
2 enfants ou 2 personnes                             2,38%
3 enfants ou 3 personnes                             2,01%
4 enfants ou 4 personnes                             1,85%
5 enfants ou 5 personnes                             1,79%
6 enfants ou 6 personnes                             1,73%
Majoration par personne à charge	                   -0,06%

```catala
champ d'application CalculAidePersonnaliséeLogementLocatif
  sous condition date_courante >=@ |2020-10-01| et
  date_courante <@ |2021-10-01|:
  définition taux_composition_familiale égal à
    si nombre_personnes_à_charge = 0 alors
      selon situation_familiale_calcul_apl sous forme
      -- PersonneSeule: 2,83%
      -- Couple: 3,15%
    sinon (si nombre_personnes_à_charge = 1 alors
      2,70%
    sinon (si nombre_personnes_à_charge = 2 alors
      2,38%
    sinon (si nombre_personnes_à_charge = 3 alors
      2,01%
    sinon (si nombre_personnes_à_charge = 4 alors
      1,85%
    sinon (si nombre_personnes_à_charge = 5 alors
      1,79%
    sinon (si nombre_personnes_à_charge = 6 alors
      1,73%
    sinon
      (1,73% -. (0,06% *. (entier_vers_décimal de
        (nombre_personnes_à_charge - 6))))
    ))))))
# TODO informatique: corriger le parseur pour éviter d'avoir à mettre
# toutes ces parenthèses.
```

2° TL représente un taux complémentaire fixé ci-dessous en fonction de la valeur du rapport
RL entre le loyer retenu dans la limite du plafond L et un loyer de référence LR : $\text{RL} = \text{L} / \text{LR}$.

RL est exprimé en pourcentage et arrondi à la deuxième décimale.

```catala
champ d'application CalculAidePersonnaliséeLogementLocatif
  sous condition date_courante >=@ |2020-10-01| et
    date_courante <@ |2021-10-01|:
  définition rapport_loyers égal à
    arrondi_décimal de ((loyer_éligible /€ loyer_référence) *. 100,0) /. 100,0
```

Pour la détermination de TL , les taux progressifs et les tranches successives de RL mentionnés
au 3° de l'article D. 823-17 du même code sont fixés comme suit :

- 0 % pour la tranche de RL inférieure à 45 % ;

- 0,45 % pour la tranche de RL entre 45 % et 75 % ;

- 0,68 % pour la tranche de RL supérieure à 75 %.

TL est exprimé en pourcentage et arrondi à la troisième décimale.
Le tableau suivant traduit cette formule :

Si $\text{RL}<45\%$ Si $45\% < \text{RL} < 75\%$               Si $\text{RL} >75 \%$
------------------- ------------------------------------------ -----------------------------------------------------------
$\text{TL}=0 \%$    $\text{TL}=0,45 \%\times (\text{RL}-45\%)$ $\text{TL}=0,45\%\times30 \%+0,68 \%\times(\text{RL}-75\%)$

```catala
champ d'application CalculAidePersonnaliséeLogementLocatif
  sous condition date_courante >=@ |2020-10-01| et
    date_courante <@ |2021-10-01|:
  définition taux_loyer_éligible état formule égal à
    si rapport_loyers <. 45% alors 0% sinon (
    si rapport_loyers >=. 45% et rapport_loyers <. 75% alors
      0,45% *. (rapport_loyers -. 0,45%)
    sinon (si rapport_loyers >=. 75% alors
      0,45% *. 30% +. 0,68% *. (rapport_loyers -. 75%)
    sinon 0,0))
  définition taux_loyer_éligible état arrondi égal à
    # La troisième décimale en pourcentage est en fait la cinquième décimale
    (arrondi_décimal de (taux_loyer_éligible *. 100000,0)) /. 100000,0
```

Le loyer de référence LR est défini selon le tableau suivant (en euros) :

Composition du ménage	                                Montant
---------------------------------------------------- --------
Bénéficiaire isolé	                                  258,69
Couple sans personne à charge	                      316,64
Personne seule ou couple ayant une personne à charge  356,3
Majoration par personne à charge	                  51,86

```catala
champ d'application CalculAidePersonnaliséeLogementLocatif
  sous condition date_courante >=@ |2020-10-01| et
    date_courante <@ |2021-10-01|:
  définition loyer_référence égal à
    si nombre_personnes_à_charge = 0 alors
      selon situation_familiale_calcul_apl sous forme
      -- PersonneSeule: 258,69€
      -- Couple: 316,64€
    sinon (356,30€ +€
      (51,86€ *€ (entier_vers_décimal de (nombre_personnes_à_charge - 1))))
```

NOTA :
Conformément à l’article 3, I de l’arrêté du 25 septembre 2020, ces
dispositions sont applicables pour les prestations dues à compter du
1er octobre 2020.


### Article 16 | LEGIARTI000042378438

Dans le cas des colocataires prévus à l'article D. 823-18 du même code :

1° Les plafonds de loyers sont fixés à 75 % des plafonds de loyers définis au 2° de l'article
D. 823-16 du même code et fixés à l'article 7.

Les montants obtenus par l'application de ces pourcentages sont arrondis au centime d'euro le plus proche.
On obtient les loyers plafonds suivants (en euros) :

--------------------------------------------------------
Zone	                             I       II     III
---------------------------------- ------- ------ ------
Personne seule	                    222,62 194,02 181,85

Couple sans personne à charge	    268,49 237,48 220,44

Personne seule ou couple ayant une
personne à charge	                303,45 267,23 247,17

Par personne à
charge supplémentaire               44,03  38,90  35,42
--------------------------------------------------------

```catala
champ d'application CalculAidePersonnaliséeLogementLocatif
  sous condition date_courante <@ |2021-10-01| et
    date_courante >=@ |2020-10-01| et colocation:

  exception base définition plafond_loyer_d823_16_2 sous condition
    (situation_familiale_calcul_apl sous forme PersonneSeule) et
    nombre_personnes_à_charge = 0
  conséquence égal à
    selon zone sous forme
    -- Zone1: 222,62€
    -- Zone2: 194,02€
    -- Zone3: 181,85€

  exception base définition plafond_loyer_d823_16_2 sous condition
    (situation_familiale_calcul_apl sous forme Couple) et
    nombre_personnes_à_charge = 0
  conséquence égal à
    selon zone sous forme
    --Zone1: 268,49€
    --Zone2: 237,48€
    --Zone3: 220,44€

  exception base définition plafond_loyer_d823_16_2 sous condition
    nombre_personnes_à_charge >= 1
  conséquence égal à
    selon zone sous forme
    --Zone1:
      303,45€ +€ 44,03€ *€ (entier_vers_décimal de
        (nombre_personnes_à_charge - 1))
    --Zone2:
      267,23€ +€ 38,90€ *€ (entier_vers_décimal de
        (nombre_personnes_à_charge - 1))
    --Zone3:
      247,17€ +€ 35,42€ *€ (entier_vers_décimal de
        (nombre_personnes_à_charge - 1))
```

2° Le montant forfaitaire au titre des charges est fixé comme suit (en euros) :

Composition du foyer	           Montant
-------------------------------- -------
Bénéficiaire isolé               26,99
Couple sans personne à charge	 53,99
Majoration par personne à charge 12,24

```catala
champ d'application CalculAidePersonnaliséeLogementLocatif
  sous condition date_courante <@ |2021-10-01| et
    date_courante >=@ |2020-10-01| et colocation:
  définition montant_forfaitaire_charges_d823_16 égal à
    (selon situation_familiale_calcul_apl sous forme
    -- PersonneSeule: 26,99€
    -- Couple: 53,99€) +€
    12,24€ *€ (entier_vers_décimal de nombre_personnes_à_charge)
```

NOTA :

Conformément à l’article 3, I de l’arrêté du 25 septembre 2020, ces
dispositions sont applicables pour les prestations dues à compter du
1er octobre 2020.


### Article 27 | LEGIARTI000042378428

Pour l'application de l'article D. 832-24 du même code, les équivalences de loyer et de charges locatives plafonds
sont fixées comme suit (en euros) :

Désignation                                                  Zone I Zone II Zone III
------------------------------------------------------------ ------ ------- --------
Bénéficiaire isolé	                                         444,43 406,43  385,78
Couple sans personne à charge	                               521,02 474,33  448,69
Bénéficiaire isolé ou couple ayant une personne à charge     555,55 505,75  476,02
Bénéficiaire isolé ou couple ayant deux personnes à charge   594,54 541,38  507,44
Bénéficiaire isolé ou couple ayant trois personnes à charge  633,69 576,87  538,84
Bénéficiaire isolé ou couple ayant quatre personnes à charge 683,5  614,69  574,16
Par personne supplémentaire à charge	                       70,89  64,07	  59,46

NOTA :
Conformément à l’article 3, I de l’arrêté du 25 septembre 2020, ces dispositions
sont applicables pour les prestations dues à compter du 1er octobre 2020.

```catala
champ d'application CalculAidePersonnaliséeLogementFoyer sous condition
  date_courante <@ |2021-10-01| et date_courante >=@ |2020-10-01|:
  définition plafond_équivalence_loyer_éligible égal à
    (selon zone sous forme
    -- Zone1: (
      si nombre_personnes_à_charge = 0 alors
        (selon situation_familiale_calcul_apl sous forme
         -- PersonneSeule: 444,43 €
         -- Couple: 521,01€)
      sinon (si nombre_personnes_à_charge = 1 alors
        555,55 €
      sinon (si nombre_personnes_à_charge = 2 alors
        594,54 €
      sinon (si nombre_personnes_à_charge = 3 alors
        633,69 €
      sinon
        (683,5 € +€
        70,89€ *€ (entier_vers_décimal de (nombre_personnes_à_charge - 4)))))))
    -- Zone2: (
      si nombre_personnes_à_charge = 0 alors
        (selon situation_familiale_calcul_apl sous forme
         -- PersonneSeule: 406,43 €
         -- Couple: 474,33€)
      sinon (si nombre_personnes_à_charge = 1 alors
        505,75 €
      sinon (si nombre_personnes_à_charge = 2 alors
        541,38 €
      sinon (si nombre_personnes_à_charge = 3 alors
        576,87 €
      sinon
        (614,69 € +€
        64,07€ *€ (entier_vers_décimal de (nombre_personnes_à_charge - 4)))))))
    -- Zone3: (
      si nombre_personnes_à_charge = 0 alors
        (selon situation_familiale_calcul_apl sous forme
         -- PersonneSeule: 385,78 €
         -- Couple: 448,69€)
      sinon (si nombre_personnes_à_charge = 1 alors
        476,02 €
      sinon (si nombre_personnes_à_charge = 2 alors
        507,44 €
      sinon (si nombre_personnes_à_charge = 3 alors
        538,84 €
      sinon
        (574,16 € +€
        59,46€ *€ (entier_vers_décimal de (nombre_personnes_à_charge - 4)))))))
    )
```


## Articles valables du 1er janvier 2020 au 1er janvier 2022

### Article 15 | LEGIARTI000041489192


Pour l'application du 5° de l'article D. 823-17 du même code, le forfait " R0 " est fixé selon
le tableau suivant (en euros) :

Composition du foyer	                     MONTANT  (en euros)
-----------------------------------------  -------------------
Personne seule sans personne à charge	     4 588
Couple sans personne à charge	             6 572
Personne seule ou couple ayant :
    - une personne à charge	                 7 839
    - deux personnes à charge	             8 015
    - trois personnes à charge	             8 322
    - quatre personnes à charge	             8 631
    - cinq personnes à charge	             8 938
    - six personnes à charge                 9 246
    - par personne à charge supplémentaire   305

```catala
champ d'application CalculAidePersonnaliséeLogementLocatif
  sous condition date_courante <@ |2022-01-01| et
    date_courante >=@ |2020-01-01|:
  définition abattement_forfaitaire_d823_17 égal à
    si nombre_personnes_à_charge = 0 alors
      selon situation_familiale_calcul_apl sous forme
      -- PersonneSeule: 4 588 €
      -- Couple: 6 572 €
    sinon (si nombre_personnes_à_charge = 1 alors
      7 839 €
    sinon (si nombre_personnes_à_charge = 2 alors
      8 015 €
    sinon (si nombre_personnes_à_charge = 3 alors
      8 322 €
    sinon (si nombre_personnes_à_charge = 4 alors
      8 631 €
    sinon (si nombre_personnes_à_charge = 5 alors
      8 938 €
    sinon (si nombre_personnes_à_charge = 6 alors
      9 246 €
    sinon
      (9 246€ +€ (305 € *€ (entier_vers_décimal de
        (nombre_personnes_à_charge - 6))))
    ))))))
```

NOTA :
Conformément à l'article 2 de l'arrêté du 3 janvier 2020 ( NOR : LOGL1934006A ),
ces dispositions sont applicables pour les prestations dues à compter
du 1er janvier 2020.
