## Partie réglementaire - Décrets en Conseil d'Etat

### Livre 5 : Prestations familiales et prestations assimilées

#### Titre 1 : Champ d'application - Généralités

##### Chapitre 2 : Champ d'application.

###### Article R512-2|LEGIARTI000006750602

Les enfants ouvrent droit aux prestations familiales jusqu'à l'âge de vingt
ans sous réserve que leur rémunération n'excède pas le plafond fixé au deuxième
alinéa du présent article.

```catala
# Attention: ici la condition de ressources au dessous du plafond est une
# répétition par rapport au texte de L512-3. On ne remet donc pas le code
# ici correspondant à cette limitation.

champ d'application ÉligibilitéPrestationsFamiliales :
  définition âge_l512_3_2 égal à 20 an
```

Le plafond de rémunération mentionné au 2° de l'article L. 512-3 est égal, pour
un mois, à 55 % du salaire minimum interprofessionnel de croissance défini aux
articles L. 141-1 à L. 141-9 du code du travail, multiplié par 169.


```catala
champ d'application ÉligibilitéPrestationsFamiliales :
  définition plafond_l512_3_2 égal à
   (smic.brut_horaire * 55 %) * 169,0
```

Pour ceux des enfants qui bénéficient d'avantages en nature, l'évaluation de ces
avantages devra être faite suivant les barèmes fixés pour l'application de la
législation sur les assurances sociales.


```catala
# Le programme Catala ne tient pas en compte des avantages en nature.
# TODO: Liane, faire de la recherche juridique sur le sujet.
```

### Livre 7 : Régimes divers - Dispositions diverses

#### Titre 5 : Départements d'outre-mer

##### Chapitre 5 : Prestations familiales et prestations assimilées

###### Article R755-0-2|LEGIARTI000006752633

Le plafond de rémunération des enfants à charge mentionnés à l'article
L. 512-3 est égal, pour un mois, à 55 % du salaire minimum de croissance en
vigueur dans chacun des départements mentionnés à l'article L. 751-1 ,
multiplié par 169.

```catala
champ d'application ÉligibilitéPrestationsFamiliales :
  exception
  définition plafond_l512_3_2
  sous condition régime_outre_mer_l751_1
  conséquence égal à
    (smic.brut_horaire * 55%) * 169,0
```
